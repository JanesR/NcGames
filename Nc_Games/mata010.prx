#INCLUDE "MATA010.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "APWIZARD.CH"
#INCLUDE "FWADAPTEREAI.CH"
  
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA010  ³ Autor ³ Jorge Queiroz         ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de atualizacao de Produtos                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATA010(ExpA1,ExpN1)                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA2 = (ROT.AUT) Array dos campos                         ³±±
±±³          ³ ExpN1 = (ROT.AUT) Numero da opcao selecionada              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Descri‡…o ³ PLANO DE MELHORIA CONTINUA                     MATA010.PRX ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ITEM PMC  ³ Responsavel              ³ Data       ³ BOPS               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³      01  ³Marcos V. Ferreira        ³24/01/2006  ³                    ³±±
±±³      02  ³Erike Yuri da Silva       ³21/12/2005  ³                    ³±±
±±³      03  ³                          ³            ³                    ³±±
±±³      04  ³Ricardo Berti             ³08/03/2006  ³093897              ³±±
±±³      05  ³Nereu Humberto Junior     ³05/04/2006  ³096215              ³±±
±±³      06  ³                          ³            ³                    ³±±
±±³      07  ³Marcos V. Ferreira        ³24/01/2006  ³                    ³±±
±±³      07* ³Ricardo Berti             ³04/05/2006  ³097462              ³±±
±±³      08  ³Ricardo Berti             ³08/03/2006  ³093897              ³±±
±±³      09  ³Nereu Humberto Junior     ³05/04/2006  ³096215              ³±±
±±³      10  ³Erike Yuri da Silva       ³21/12/2005  ³                    ³±±
±±³      10* ³Ricardo Berti             ³04/05/2006  ³097462              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATA010(xRotAuto,nOpc)

Local aHelpp		:={}
Local aHelpE		:={}
Local aHelpS		:={}
local lCoresPE 		:= .F.
Local cFiltraSB1	:= " "
Local bFiltraBrw	:= " "
Local aIndexSB1		:= {}
Local cFiltro       := ""

nOpc := If (nOpc == Nil,3,nOpc)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private l010Auto := ( xRotAuto <> NIL )
Private aRotAuto := Nil
Private aRotina  := MenuDef()
Private aCpoAltSB1 := {} // vetor usado na gravacao do historico de alteracoes 
Private aCmpoSB1   := {} // vetor usado na gravacao do historico de alteracoes 

STATIC lHistFiscal := IIf(FindFunction("HistFiscal"),HistFiscal(), .F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCadastro := OemtoAnsi(STR0008)         //"Atualiza‡„o de Produtos"

PRIVATE INCLUI    := .F.
PRIVATE ALTERA    := .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao de variaveis para rotina de inclusao automatica    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aMemos:={{"B1_DESC_I","B1_VM_I"},{"B1_DESC_GI","B1_VM_GI"},{"B1_DESC_P","B1_VM_P"},{"B1_CODOBS","B1_OBS"},{"B1_CODPROC","B1_VM_PROC"}}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada para Tratamento de Campos Memo - Virtuais   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "MT010MEM" ) 
	aMemUser := ExecBlock( "MT010MEM", .F., .F. ) 
	If ValType( aMemUser ) == "A"
		AEval( aMemUser, { |x| AAdd( aMemos, x ) } ) 	
	EndIf 	
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Esta vari vel serve como sem foro para copia ou repeti‡„o de produtos. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private lCopia    := .F.

//Inclusao da pergunte e help de forma dinamica
// "MTA010", "01","Descr. Prod X Forn ?" "Atualiza"/ "Nao Atualiza"
PutSx1( "MTA010", "02","Prod e Complemento ?","Prod e Complemento ?","Prod e Complemento ?","mv_ch2","N",1,0,2,"C","","","","",;
	"mv_par02","Sim","Sim","Yes","","Nao","No","No","","","","","","","","","")
aHelpp:={"Indica se permite a manutenção no ","cadastro de produto junto com o cadastro","de complemento em uma unica tela"}
aHelpE:={}
aHelpS:={}                                          
PutSX1Help("P.MTA01002.",aHelpP,aHelpE,aHelpS)	

AjustaSX2()

AjustaSX3()

AjustaHelp()

PERGUNTE("MTA010",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ativa tecla F4 para comunicacao com Saldos dos Lotes         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !l010Auto
	Set Key VK_F4 TO MT010F4()
	Set Key VK_F12 TO MT010Perg()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Seleciona o Alias SYP para garantir a abertura ( NAO RETIRAR ) ³
//³ do arquivo durante a transacao.(AVERAGE)                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SYP")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If l010Auto
	aRotAuto := xRotAuto
	MBrowseAuto(nOpc,aRotAuto,"SB1")
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verificacao de filtros na Mbrowse                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( ExistBlock("MA010FIL") )
		cFiltraSB1 := AllTrim(ExecBlock("MA010FIL",.F.,.F.))
		If Valtype(cFiltraSB1) == "C" .And. !Empty(cFiltraSB1)
			bFiltraBrw 	:= {|| FilBrowse("SB1",@aIndexSB1,@cFiltraSB1)}
			Eval(bFiltraBrw)
			If ( Eof() )
				HELP(" ",1,"RECNO")
			EndIf
		EndIf
	ElseIf ( ExistBlock("M010FILB") )
		cFiltro := AllTrim(ExecBlock("M010FILB",.F.,.F.))
		If Valtype(cFiltro) <> "C"
			cFiltro := ""		
		EndIf
	EndIf

	dbSelectArea("SB1")
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ MT010COR - Ponto de Entrada para customizar o array aCores             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock("MT010COR")
		lCoresPE := .F.
		aCores := ExecBlock("MT010COR",.F.,.F.)
		If (Valtype(aCores) == "A" .And. Len(aCores) > 0)
		   lCoresPE := .T.
		EndIf
	EndIf	

	mBrowse( 6, 1,22,75,"SB1",,,,,,IIf(lCoresPE,aCores,Nil),,,,,,,, IF(!Empty(cFiltro), cFiltro, NIL) )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Finaliza o uso da funcao FilBrowse e retorna os indices padroes.       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(cFiltraSB1) .And. Len(aIndexSB1) > 0
		EndFilBrw("SB1",aIndexSB1)
	EndIf

EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desativa a tecla F4 neste momento por seguranca              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !l010Auto
	SET KEY VK_F4 TO
	Set Key VK_F12 To
EndIF
Return(.T.)


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Funcao criada porque no FINA010 tambem existe A010Deleta     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Function Mata010Deleta(cAlias,nReg,nOpc)

Return (A010Deleta(cAlias,nReg,nOpc))


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010Deleta³ Autor ³ Jorge Queiroz         ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de exclusao de Produtos                           ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A010Deleta(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada 			              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A010Deleta(cAlias,nReg,nOpc)

Static lMTA010E  := NIL
Static lMTA010ET := NIL
Static lMTA010OK := NIL
Static lMT010EXC := NIL

Local cAliasABI  := ""
Local cAliasADJ  := ""
Local cAliasAD1  := ""
Local cAliasAGC  := ""
Local cAliasAA3  := ""
Local cAliasAA4  := ""
Local cAliasADB  := ""
Local cAliasDTC  := ""
Local cAliasDE5  := ""
Local cAliasCNB  := ""
Local cAliasDU5  := ""
Local cAliasAG8  := ""
Local cAliasAIB  := ""
Local cAliasSU1  := ""
Local cAliasSUG  := ""
Local cAliasSU2  := ""
Local cAliasSUW  := ""
Local cAliasSUF  := ""
Local cAliasAA7	 := ""
Local cAliasAAI	 := ""
Local cAliasAAK	 := ""
Local cAliasAAN	 := ""
Local cAliasAAU	:= ""
Local cAliasSL2 := ""
Local cAliasAII := ""
Local cArqInd    := ""
Local cQuery     := ""
Local cSeek      := ""
Local cMsgNegacao:= ""         
Local cProdSGI   := ""         
Local cAliasSGI  := ""
Local cAliasSDX	:= ""
Local cAliasSAY	:= ""
Local nIndex     := 0
LOCAL LVar01:=1,nLinhas:=0,bCampo,nOpcA,cCod
LOCAL oDlg, lErro := .F.
Local lFantasma   := .F.
Local nRecnoSM0   := SM0->(RecNo())
Local aSM0CodFil  := {}
Local aFiliais    := {}
Local nBusca
Local lPodeApagar := .T.
Local lApagaSB5   := .T.
Local aButtons    := {}
Local aUsrBut     := {}
Local lGrade      := .F.
Local cCodGr      := ""
Local nTamRef     := Val(Substr(GetMv("MV_MASCGRD"),1,2))
Local lContinua   :=.T.           
Local lIntPms     := GetMv("MV_INTPMS",.F.,"N")=="S"
Local lIntRMSOL   := iif(GetMv("MV_RMCOLIG",.F.,0)==0,.F.,.T.)
Local aIntSB1	   := {}
Local aObjects    := {}
Local aSize       := {} 
Local aInfo       := {}
Local aPosObj     := {}  
Local lIntACD	   := SuperGetMV("MV_INTACD",.F.,"0") == "1"
//Local lIntSFC  	:= FindFunction("IntegraSFC") .And. IntegraSFC() .And. !IsInCallStack("AUTO010")
Local lIntSFC  	:= FindFunction("ExisteSFC") .And. ExisteSFC("SB1") .And. !IsInCallStack("AUTO010")
Local lIntDPR 	:= FindFunction("IntegraDPR") .And. IntegraDPR() .And. !IsInCallStack("AUTO010")// Determina se existe integracao com o DPR 
Local nRecCod	:= 1

//-- Variavel usada para verificar se o disparo da funcao IntegDef() pode ser feita manualmente
Local lIntegDef  :=  FindFunction("GETROTINTEG") .And. FindFunction("FWHASEAI") .And. FWHasEAI("MATA010",.T.,,.T.) 

Local bCampoSB1 := { |x| SB1->(Field(x)) }   
Local aCmps     := {}

l010Auto  := If (Type("l010Auto") =="U",.F.,l010Auto)
lMTA010ET := If(lMTA010ET == NIL,ExistTemplate("MTA010E"),lMTA010ET)
lMTA010E  := If(lMTA010E  == NIL,ExistBlock("MTA010E"),lMTA010E)
lMTA010OK := If(lMTA010OK == NIL,ExistBlock("MTA010OK"),lMTA010OK)
lMT010EXC := If(lMT010EXC == NIL,ExistBlock("MT010EXC"),lMT010EXC)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aTELA[0][0],aGETS[0]
bCampo := {|nCPO| Field(nCPO) }

dbSelectArea("SA7")
dbSetOrder(2)
If dbSeek(xFilial("SA7")+SB1->B1_COD)     
     Help(" ",1,"MT010DPRO")
     Return
EndIf

#IFDEF TOP
   cAliasDU5 := GetNextAlias()
   cQuery    := ""

   cQuery += " SELECT COUNT(*) TOT_PROD "
   cQuery += "   FROM " + RetSqlName( "DU5" )
   cQuery += "  WHERE DU5_FILIAL='" + xFilial( "DU5" ) + "'"
   cQuery += "    AND DU5_CODPRO = '" + SB1->B1_COD + "'"
   cQuery += "    AND D_E_L_E_T_ = ' '"

   cQuery := ChangeQuery( cQuery )
   dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasDU5,.F.,.T. )

   If (cAliasDU5)->TOT_PROD > 0
      (cAliasDU5)->(DbCloseArea())
      Aviso(STR0032,STR0049,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por uma tabela de seguro no ambiente TMS e nao podera ser excluido"###"Ok"
      Return
   Endif

   (cAliasDU5)->(DbCloseArea())
   
   If ChkFile("AG8")
	   cAliasAG8 := GetNextAlias()
	   cQuery    := ""

	   cQuery += " SELECT COUNT(*) QTD_PROD "
	   cQuery += "   FROM " + RetSqlName( "AG8" )
	   cQuery += "  WHERE AG8_FILIAL='" + xFilial( "AG8" ) + "'"
	   cQuery += "    AND AG8_COD = '" + SB1->B1_COD + "'"
	   cQuery += "    AND D_E_L_E_T_ = ' '"

	   cQuery := ChangeQuery( cQuery )
	   dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAG8,.F.,.T. )

	   If (cAliasAG8)->QTD_PROD > 0
		  (cAliasAG8)->(DbCloseArea())
		  Aviso(STR0032,STR0063,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por uma tabela - Assunto x Inf´s.Ch e nao podera ser excluido."###"Ok"
		  Return
	   Endif

	   (cAliasAG8)->(DbCloseArea())
   EndIf 
   
   cAliasSUW := GetNextAlias()
   cQuery    := ""

   cQuery += " SELECT COUNT(*) TOT_PROD "
   cQuery += "   FROM " + RetSqlName( "SUW" )
   cQuery += "  WHERE UW_FILIAL='" + xFilial( "SUW" ) + "'"
   cQuery += "    AND UW_PRODUTO = '" + SB1->B1_COD + "'"
   cQuery += "    AND D_E_L_E_T_ = ' '"

   cQuery := ChangeQuery( cQuery )
   dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSUW,.F.,.T. )

   If (cAliasSUW)->TOT_PROD > 0
      (cAliasSUW)->(DbCloseArea())
      Aviso(STR0032,STR0069,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por uma tabela de seguro no ambiente TMS e nao podera ser excluido"###"Ok"
      Return
   Endif
   (cAliasSUW)->(DbCloseArea()) 
   
   cAliasSUF := GetNextAlias()
   cQuery    := ""

   cQuery += " SELECT COUNT(*) TOT_PROD "
   cQuery += "   FROM " + RetSqlName( "SUF" )
   cQuery += "  WHERE UF_FILIAL='" + xFilial( "SUF" ) + "'"
   cQuery += "    AND UF_PRODUTO = '" + SB1->B1_COD + "'"
   cQuery += "    AND D_E_L_E_T_ = ' '"

   cQuery := ChangeQuery( cQuery )
   dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSUF,.F.,.T. )

   If (cAliasSUF)->TOT_PROD > 0
      (cAliasSUF)->(DbCloseArea())
      Aviso(STR0032,STR0070,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por uma tabela (Marketing) e nao podera ser excluido"###"Ok"
      Return
   Endif
   (cAliasSUF)->(DbCloseArea()) 
   
   cAliasAA7 := GetNextAlias()
   cQuery    := ""

   cQuery += " SELECT COUNT(*) TOT_PROD "
   cQuery += "   FROM " + RetSqlName( "AA7" )
   cQuery += "  WHERE AA7_FILIAL='" + xFilial( "AA7" ) + "'"
   cQuery += "    AND AA7_CODPRO = '" + SB1->B1_COD + "'"
   cQuery += "    AND D_E_L_E_T_ = ' '"

   cQuery := ChangeQuery( cQuery )
   dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAA7,.F.,.T. )

   If (cAliasAA7)->TOT_PROD > 0
      (cAliasAA7)->(DbCloseArea())
      Aviso(STR0032,STR0071,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por uma tabela (Marketing) e nao podera ser excluido"###"Ok"
      Return
   Endif
   (cAliasAA7)->(DbCloseArea())  

   cAliasAAI := GetNextAlias()
   cQuery    := ""

   cQuery += " SELECT COUNT(*) TOT_PROD "
   cQuery += "   FROM " + RetSqlName( "AAI" )
   cQuery += "  WHERE AAI_FILIAL='" + xFilial( "AAI" ) + "'"
   cQuery += "    AND AAI_CODPRO = '" + SB1->B1_COD + "'"
   cQuery += "    AND D_E_L_E_T_ = ' '"

   cQuery := ChangeQuery( cQuery )
   dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAAI,.F.,.T. )

   If (cAliasAAI)->TOT_PROD > 0
      (cAliasAAI)->(DbCloseArea())
      Aviso(STR0032,STR0072,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por uma tabela (FAQ) e nao podera ser excluido."###"Ok"
      Return
   Endif
   (cAliasAAI)->(DbCloseArea()) 
   
   cAliasAAK := GetNextAlias()
   cQuery    := ""

   cQuery += " SELECT COUNT(*) TOT_PROD "
   cQuery += "   FROM " + RetSqlName( "AAK" )
   cQuery += "  WHERE AAK_FILIAL='" + xFilial( "AAK" ) + "'"
   cQuery += "    AND AAK_CODPRO = '" + SB1->B1_COD + "'" +" OR AAK_CODCOM = '" + SB1->B1_COD + "'"
   cQuery += "    AND D_E_L_E_T_ = ' '"

   cQuery := ChangeQuery( cQuery )
   dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAAK,.F.,.T. )

   If (cAliasAAK)->TOT_PROD > 0
      (cAliasAAK)->(DbCloseArea())
      Aviso(STR0032,STR0073,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por uma tabela (Obsolescência) e nao podera ser excluido."###"Ok"
      Return
   Endif
   (cAliasAAK)->(DbCloseArea())      
   
   cAliasAAN := GetNextAlias()
   cQuery    := ""

   cQuery += " SELECT COUNT(*) TOT_PROD "
   cQuery += "   FROM " + RetSqlName( "AAN" )
   cQuery += "  WHERE AAN_FILIAL='" + xFilial( "AAN" ) + "'"
   cQuery += "    AND AAN_CODPRO = '" + SB1->B1_COD + "'"
   cQuery += "    AND D_E_L_E_T_ = ' '"

   cQuery := ChangeQuery( cQuery )
   dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAAN,.F.,.T. )

   If (cAliasAAN)->TOT_PROD > 0
      (cAliasAAN)->(DbCloseArea())
      Aviso(STR0032,STR0074,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por uma tabela (Prestação de Serviços) e nao podera ser excluido."###"Ok"
      Return
   Endif
   (cAliasAAN)->(DbCloseArea()) 
 
   If AliasInDic("ABI")
   		cAliasABI := GetNextAlias()
   		
   		cQuery := "SELECT COUNT(*) RECABI FROM "
		cQuery += RetSqlName("ABI") + " ABI "
		cQuery += " WHERE "                                    
		cQuery += "ABI_FILIAL = '"+xFilial("ABI")+"' AND "
		cQuery += "ABI_CODPRO = '" + SB1->B1_COD + "' AND "
		cQuery += "ABI.D_E_L_E_T_ = ' ' "

		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),cAliasABI,.F.,.T.)

		If (cAliasABI)->RECABI > 0 
			(cAliasABI)->(dbCloseArea())
			Aviso(STR0032,STR0075,{STR0031},2) //Este produto esta sendo utilizado em um projeto no módulo Gestão de Serviços e não poderá ser excluido 
			Return
		Endif							
		
		(cAliasABI)->(dbCloseArea())
	EndIf
	If AliasInDic("ADJ")
   		cAliasADJ := GetNextAlias()
   		cQuery := "SELECT COUNT(*) RECADJ FROM "
		cQuery += RetSqlName("ADJ") + " ADJ "
		cQuery += " WHERE "                                    
		cQuery += "ADJ_FILIAL = '"+xFilial("ADJ")+"' AND "
		cQuery += "ADJ_PROD = '" + SB1->B1_COD + "' AND "
		cQuery += "ADJ.D_E_L_E_T_ = ' ' "

		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),cAliasADJ,.F.,.T.)

		If (cAliasADJ)->RECADJ > 0 
			(cAliasADJ)->(dbCloseArea())
			Aviso(STR0032,STR0076,{STR0031},2) //Este produto está sendo utilizado em oportunidades de venda, e não poderá ser excluído. 
			Return
		Endif							
		
		(cAliasADJ)->(dbCloseArea())
	EndIf
	
	If AliasInDic("AD1")
   		cAliasAD1 := GetNextAlias()
   		cQuery := "SELECT COUNT(*) RECAD1 FROM "
		cQuery += RetSqlName("AD1") + " AD1 "
		cQuery += " WHERE "                                    
		cQuery += "AD1_FILIAL = '"+xFilial("AD1")+"' AND "
		cQuery += "AD1_CODPRO = '" + SB1->B1_COD + "' AND "
		cQuery += "AD1.D_E_L_E_T_ = ' ' "

		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),cAliasAD1,.F.,.T.)

		If (cAliasAD1)->RECAD1 > 0 
			(cAliasAD1)->(dbCloseArea())
			Aviso(STR0032,STR0076,{STR0031},2) //Este produto está sendo utilizado em oportunidades de venda, e não poderá ser excluído. 
			Return
		Endif							
		
		(cAliasAD1)->(dbCloseArea())
	EndIf		   
	
	If AliasInDic("AGC")
   		cAliasAGC := GetNextAlias()
   		cQuery := "SELECT COUNT(*) RECAGC FROM "
		cQuery += RetSqlName("AGC") + " AGC "
		cQuery += " WHERE "                                    
		cQuery += "AGC_FILIAL = '"+xFilial("AGC")+"' AND "
		cQuery += "AGC_CODSB1 = '" + SB1->B1_COD + "' AND "
		cQuery += "AGC.D_E_L_E_T_ = ' ' "

		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),cAliasAGC,.F.,.T.)

		If (cAliasAGC)->RECAGC > 0 
			(cAliasAGC)->(dbCloseArea())
			Aviso(STR0032,STR0077,{STR0031},2) //Este produto está sendo utilizado em regras de monitoração de chamados, e não poderá ser excluído.
			Return
		Endif							
		
		(cAliasAGC)->(dbCloseArea())
	EndIf

	If AliasInDic("AAU")
   		cAliasAAU := GetNextAlias()
   		cQuery := "SELECT COUNT(*) RECAAU FROM "
		cQuery += RetSqlName("AAU") + " AAU "
		cQuery += " WHERE "                                    
		cQuery += "AAU_FILIAL = '"+xFilial("AAU")+"' AND "
		cQuery += "AAU_PRODUT = '" + SB1->B1_COD + "' AND "
		cQuery += "AAU.D_E_L_E_T_ = ' ' "

		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),cAliasAAU,.F.,.T.)

		If (cAliasAAU)->RECAAU > 0 
			(cAliasAAU)->(dbCloseArea())
			Aviso(STR0032,STR0078,{STR0031},2) //Este produto está sendo utilizado na Vistoria Técnica, e não poderá ser excluído.
			Return
		Endif							
		
		(cAliasAAU)->(dbCloseArea())
	EndIf		
	
	If AliasInDic("SL2")
   		cAliasSL2 := GetNextAlias()
   		cQuery := "SELECT COUNT(*) RECSL2 FROM "
		cQuery += RetSqlName("SL2") + " SL2 "
		cQuery += " WHERE "                                    
		cQuery += "L2_FILIAL = '"+xFilial("SL2")+"' AND "
		cQuery += "L2_PRODUTO = '" + SB1->B1_COD + "' AND "
		cQuery += "SL2.D_E_L_E_T_ = ' ' "

		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),cAliasSL2,.F.,.T.)

		If (cAliasSL2)->RECSL2 > 0 
			(cAliasSL2)->(dbCloseArea())
			Aviso(STR0032,STR0079,{STR0031},2) //Este produto está sendo utilizado no orçamento (Venda Direta/ Venda Assistida), e não poderá ser excluído.
			Return
		Endif							
		
		(cAliasSL2)->(dbCloseArea())
	EndIf
	
	If AliasInDic("SDX")
   		cAliasSDX := GetNextAlias()
   		cQuery := "SELECT COUNT(*) RECSDX FROM "
		cQuery += RetSqlName("SDX") + " SDX "
		cQuery += " WHERE "                                    
		cQuery += "DX_FILIAL = '"+xFilial("SDX")+"' AND "
		cQuery += "DX_PRODUTO = '" + SB1->B1_COD + "' AND "
		cQuery += "SDX.D_E_L_E_T_ = ' ' "

		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),cAliasSDX,.F.,.T.)

		If (cAliasSDX)->RECSDX > 0 
			(cAliasSDX)->(dbCloseArea())
			Aviso(STR0032,STR0080,{STR0031},2) //Este produto está sendo utilizado na Programação de Entrega, e não poderá ser excluído.
			Return
		Endif							
		
		(cAliasSDX)->(dbCloseArea())
	EndIf  
			
	If AliasInDic("SAY")
   		cAliasSAY := GetNextAlias()
   		cQuery := "SELECT COUNT(*) RECSAY FROM "
		cQuery += RetSqlName("SAY") + " SAY "
		cQuery += " WHERE "                                    
		cQuery += "AY_FILIAL = '"+xFilial("SAY")+"' AND "
		cQuery += "AY_PRODUTO = '" + SB1->B1_COD + "' AND "
		cQuery += "SAY.D_E_L_E_T_ = ' ' "

		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),cAliasSAY,.F.,.T.)

		If (cAliasSAY)->RECSAY > 0 
			(cAliasSAY)->(dbCloseArea())
			Aviso(STR0032,STR0081,{STR0031},2) //Este produto está sendo utilizado na Publicação de preços, e não poderá ser excluído.
			Return
		Endif							
		
		(cAliasSAY)->(dbCloseArea())
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica o Cad. Margem Mínima      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If AliasInDic('AII')
		
			cAliasAII := GetNextAlias()
			cQuery    := ""
			cQuery += " SELECT COUNT(*) QTDPROD FROM " + RetSqlName("AII")
			cQuery += " WHERE "
			cQuery += "AII_FILIAL = '" + xFilial("AAI") + "' AND "
			cQuery += "AII_CODPRO = '" + SB1->B1_COD    + "' AND "
			cQuery += "D_E_L_E_T_ = ' '"

			cQuery := ChangeQuery( cQuery )
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAII,.F.,.T. )

			If (cAliasAII)->QTDPROD > 0
				(cAliasAII)->(dbCloseArea())
				Aviso(STR0032,STR0082,{STR0031},2) //Este produto está sendo utilizado no Cad. Margem Mínima, e não poderá ser excluído.
				Return
			Endif

			(cAliasAII)->(dbCloseArea())		
	EndIf	
		
#ENDIF		

If ExistTemplate( "MA010BUT" )
	If Valtype( aUsrBut := ExecTemplate( "MA010BUT", .f., .f. ) ) == "A"
		AEval( aUsrBut, { |x| AAdd( aButtons, x ) } )
	EndIF
EndIf

If ExistBlock( "MA010BUT" )
	If Valtype( aUsrBut := Execblock( "MA010BUT", .f., .f. ) ) == "A"
		AEval( aUsrBut, { |x| AAdd( aButtons, x ) } )
	EndIF
EndIf

SB4->(DbSetOrder(1))

PERGUNTE("MTA010",.F.)

A010Int( 1, nOpc, aIntSB1 )

While .T.  
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ajusta a largura para o tamanho padrao Protheus ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aSize := MsAdvSize()       
	aSize[5] := 1155     
			
	aObjects := {}
	AAdd( aObjects, { 100, 100, .T., .T. } )
	aInfo    := { aSize[1], aSize[2], aSize[3], aSize[4], 2, 2 } 
		
	aPosObj := MsObjSize( aInfo, aObjects )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia para processamento dos Gets          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nOpcA:=0

	IF !SoftLock(cAlias)
		lContinua := .F.
		Exit
	Endif

	If lContinua
		If !( l010Auto )
			SB5->(DbSetOrder(1))
			If MV_PAR02 == 1 .AND. SB5->(DbSeek(xFilial("SB5")+SB1->B1_COD))
				nOpcA := A010DELPeC(cAlias,nReg,nOpc)
			Else
				DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL
				nOpcA:=EnChoice( cAlias, nReg, nOpc,,,,,aPosObj[1])
				ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()},,aButtons)
			EndIf
		Else
			nOpca := 2
		EndIf
		DbSelectArea(cAlias)

		IF nOpcA == 2
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Antes de deletar eu vou verificar se existe movimentacao     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			// Preenche um array com as filiais

			dbSelectArea("SM0")
			dbGoTop()
			do While ! Eof()
				If SM0->M0_CODIGO == cEmpAnt
					Aadd(aSM0CodFil, FWGETCODFILIAL)
				Endif
				dbSkip()
			Enddo
			msGoto(nRecnoSM0)

			cCod := SB1->B1_COD
			lGrade := (SB1->B1_GRADE=="S")
			dbSelectArea("SB2")      // saldos fisico e financeiro

			aFiliais := If(! Empty(FwFilial("SB2")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SB2")})

			For nBusca := 1 to Len(aFiliais)
				dbSeek(aFiliais[nBusca]+cCod)
				While !EOF() .And. aFiliais[nBusca]+cCod == B2_FILIAL+B2_COD
					If B2_QATU != 0
						HELP(" ",1,"MA0101")
						lErro := .T.
						Exit
					EndIf
					dbSkip()
				EndDo
			Next

			IF  lMTA010OK .and. !lErro
				lErro := ExecBlock("MTA010OK",.f.,.f.)
				If ValType(lErro) <> "L"
					lErro := .T.
				EndIf
				lErro := !lErro
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Funcao Especifica SIGAEIC - AVERAGE                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			lErro := If(!lErro,!MTA010OK(),lErro)

			If lErro
				dbSelectArea(cAlias)
				MsUnLock()
				lPodeApagar := .F.
				Exit
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Funcao Especifica SIGAMDT - NG INFORMATICA                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If FindFunction("MdtValESb1")
				lErro := MdtValESb1(cCod)
			EndIF
			If lErro
				dbSelectArea(cAlias)
				MsUnLock()
				lPodeApagar := .F.
				Exit
			EndIf			
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Funcao Especifica NG INFORMATICA                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If FindFunction("NGVALSX9")
				lErro := !NGVALSX9("SB1",{"TN3","TNF","TNB","TOH","TNX"},.T.)
			Endif
			If lErro
				dbSelectArea(cAlias)
				MsUnLock()
				lPodeApagar := .F.
				Exit
			EndIf

			dbSelectArea("SB9")      // Saldos Iniciais

			aFiliais := If(! Empty(FwFilial("SB9")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SB9")})

			For nBusca := 1 to Len(aFiliais)
				If dbSeek(aFiliais[nBusca]+cCod)
					If (lFantasma := (RetFldProd(SB1->B1_COD,"B1_FANTASM") == "S"))  // Projeto Implementeacao de campos MRP e FANTASM no SBZ
						lErro := .F.
						// colocado o alias do B9
						While SB9->( !Eof()) .And. !lErro .And. aFiliais[nBusca]+cCod==SB9->B9_FILIAL+SB9->B9_COD
							lErro := (SB9->B9_QINI # 0)
							SB9->( dbSkip())
						End
						If lErro
							Exit
						Endif
					EndIf
				Endif
			Next

			If lErro
				HELP(" ",1,"MA0101")
				dbSelectArea(cAlias)
				MsUnLock()
				lPodeApagar := .F.
				Exit
			Endif

			dbSelectArea("SC1")      // solicitacoes de compra

			aFiliais := If(! Empty(FwFilial("SC1")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SC1")})

			For nBusca := 1 to Len(aFiliais)
				dbSetOrder(2)  // seleciona o indice por produto
				dbSeek(aFiliais[nBusca]+cCod)
				dbSetOrder(1)  // retorna ao indice principal do arquivo
				If Found()
					HELP(" ",1,"MA10SC")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SC2")      // ordens de producao

			aFiliais := If(! Empty(FwFilial("SC2")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SC2")})

			For nBusca := 1 to Len(aFiliais)
				dbSetOrder(2)  // seleciona o indice por produto
				dbSeek(aFiliais[nBusca]+cCod)
				dbSetOrder(1)  // retorna ao indice principal do arquivo
				If Found()
					HELP(" ",1,"MA010OP")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SC4")      // previsoes de vendas

			aFiliais := If(! Empty(FwFilial("SC4")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SC4")})

			For nBusca := 1 to Len(aFiliais)
				dbSeek(aFiliais[nBusca]+cCod)
				If Found()
					HELP(" ",1,"MA010_03")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SC6")      // itens dos pedidos de vendas

			aFiliais := If(! Empty(FwFilial("SC6")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SC6")})

			For nBusca := 1 to Len(aFiliais)
				dbSetOrder(2)  // seleciona o indice por produto
				dbSeek(aFiliais[nBusca]+cCod)
				dbSetOrder(1)  // retorna ao indice principal do arquivo
				If Found()
					HELP(" ",1,"MA010_04")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SC7")      // pedido de compra

			aFiliais := If(! Empty(FwFilial("SC7")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SC7")})

			For nBusca := 1 to Len(aFiliais)
				dbSetOrder(2)  // seleciona o indice por produto
				dbSeek(aFiliais[nBusca]+cCod)
				dbSetOrder(1)  // retorna ao indice principal do arquivo
				If Found()
					HELP(" ",1,"MA10PC")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SC3")      	// Autorizacao de Entrega

			aFiliais := If(! Empty(FwFilial("SC3")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SC3")})

			For nBusca := 1 to Len(aFiliais)
				dbSetOrder(3)  				// seleciona o indice por produto
				dbSeek(aFiliais[nBusca]+cCod)
				dbSetOrder(1)  				// retorna ao indice principal do arquivo
				If Found()
					HELP(" ",1,"A010AE")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SD1")      // itens das notas fiscais de entrada

			aFiliais := If(! Empty(FwFilial("SD1")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SD1")})
			For nBusca := 1 to Len(aFiliais)
				dbSetOrder(2)  // seleciona o indice por produto
				dbSeek(aFiliais[nBusca]+cCod)
				dbSetOrder(1)  // retorna ao indice principal do arquivo
				If Found()
					HELP(" ",1,"MA10NE")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SD2")      // itens das notas fiscais de saida

			aFiliais := If(! Empty(FwFilial("SD2")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SD2")})
			For nBusca := 1 to Len(aFiliais)
				dbSetOrder(1)
				dbSeek(aFiliais[nBusca]+cCod)
				#IFDEF SHELL
					If Found() .AND. SD1->D1_CANCEL != "S"
						HELP(" ",1,"MA10NE")
						dbSelectArea(cAlias)
						MsUnLock()
						lPodeApagar := .F.
						Exit
					EndIf
				#ELSE
					If Found()
						HELP(" ",1,"MA10NE")
						dbSelectArea(cAlias)
						MsUnLock()
						lPodeApagar := .F.
						Exit
					EndIf
				#ENDIF
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SD3")      // movimentacoes internas

			aFiliais := If(! Empty(FwFilial("SD3")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SD3")})
			For nBusca := 1 to Len(aFiliais)
				dbSetOrder(3)  // seleciona o indice por produto
				dbSeek(aFiliais[nBusca]+cCod)
				dbSetOrder(1)  // retorna ao indice principal do arquivo
				If Found()
					HELP(" ",1,"MA010MI")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SG1")      // estruturas dos produtos

			aFiliais := If(! Empty(FwFilial("SG1")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SG1")})
			For nBusca := 1 to Len(aFiliais)
				dbSeek(aFiliais[nBusca]+cCod)
				If Found()
					HELP(" ",1,"MA010_07")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			// Uso o mesmo aFiliais pois se trata (ainda) do mesmo arquivo
			For nBusca := 1 to Len(aFiliais)
				dbSetOrder(2)  // seleciona o indice por componente
				dbSeek(aFiliais[nBusca]+cCod)
				dbSetOrder(1)  // retorna ao indice principal do arquivo
				If Found()
					HELP(" ",1,"MA010_08")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SG2")      // roteiro de fabricacao

			aFiliais := If(! Empty(FwFilial("SG2")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SG2")})
			For nBusca := 1 to Len(aFiliais)
				dbSeek(aFiliais[nBusca]+cCod)
				If Found()
					HELP(" ",1,"MA010_09")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SBG")      // Sugestao de Or‡amento
			dbSetOrder(1)

			aFiliais := If(! Empty(FwFilial("SBG")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SBG")})
			For nBusca := 1 to Len(aFiliais)
				dbSeek(aFiliais[nBusca]+cCod)
				If Found()
					HELP(" ",1,"MA010_10")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SBH")      // Sugestao de Or‡amento
			dbSetOrder(1)

			aFiliais := If(! Empty(FwFilial("SBH")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SBH")})
			For nBusca := 1 to Len(aFiliais)
				dbSeek(aFiliais[nBusca]+cCod)
				If Found()
					HELP(" ",1,"MA010_10")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SBH")      // Sugestao de Or‡amento (Componente)
			If !SIX->(DbSeek("SBH2"))    						// procurando por indice novo  
				Aviso(STR0007,STR0059,{STR0031},3)					// "Atenção!" "Ocorreu um erro durante a atualização da tabela." "Atualize o dicionário através do UPDFAT15. Disponível pela FNC 01000000293422010."
				dbSelectArea(cAlias)
				MsUnLock()
 				lPodeApagar := .F.
				Exit
 			EnDif                                                  
			dbSetOrder(2)			 // BH_FILIAL+BH_CODCOMP+BH_PRODUTO

			aFiliais := If(! Empty(FwFilial("SBH")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SBH")})
			For nBusca := 1 to Len(aFiliais)
				dbSeek(aFiliais[nBusca]+cCod)
				If Found()
					HELP(" ",1,"MA010_10")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			dbSelectArea("SCK")      // Sugestao de Or‡amento

			aFiliais := If(! Empty(FwFilial("SCK")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SCK")})
			For nBusca := 1 to Len(aFiliais)
				dbSetOrder(3)
				dbSeek(aFiliais[nBusca]+cCod)
				dbSetOrder(1)
				If Found()
					HELP(" ",1,"MA010_11")
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeApagar
				Exit
			Endif

			dbSelectArea("SCP")      // Solicitacao ao Armazem
			dbSetOrder(2)

			aFiliais := If(! Empty(FwFilial("SCP")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SCP")})
			For nBusca := 1 to Len(aFiliais)
				If dbSeek(aFiliais[nBusca]+cCod)
					Aviso(STR0032,STR0038,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por uma Solict.ao Armazem e nao podera ser excluido."###"Ok"
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeAPagar
				Exit
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se existe algum contrato de direito autoral com o produto selecionado.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("AH1")      // Contrato de Direito Autoral

			aFiliais := If(! Empty(FwFilial("AH1")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("AH1")})
			For nBusca := 1 to Len(aFiliais)
				dbSetOrder(1)
				dbSeek(aFiliais[nBusca]+cCod)
				If Found()
					HELP(" ",1,"MA010_12",,OemToAnsi(STR0016),1,0) //"O produto nao podera ser excluido pois existem contratos de direito autoral com este preoduto "
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next  

			dbSelectArea("SIX")
			If SIX->(dbSeek("AH8"+"3"))
				dbSelectArea("AH8")      // Cadastro de Produto Master - SIGACDA
				aFiliais := If(! Empty(FwFilial("AH8")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("AH8")})
				For nBusca := 1 to Len(aFiliais)
					dbSetOrder(3)
					dbSeek(aFiliais[nBusca]+cCod)
					If Found()
						HELP(" ",1,"MA010_13") 
						dbSelectArea(cAlias)
						MsUnLock()
						lPodeApagar := .F.
						Exit
					EndIf
				Next  
			EndIf	
				
			

			If ! lPodeApagar
				Exit
			Endif         
			    
			If lPodeApagar .And. FindFunction("ALIASINDIC") .And. AliasIndic('NNV')
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se existem Variedades de Produto  - Gestao Agricola   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea("NNV")
				dbSetOrder(1)
				aFiliais := If(! Empty(FwFilial()) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial()})
				For nBusca := 1 to Len(aFiliais)
					If dbSeek( aFiliais[nBusca] + SB1->B1_COD)
						HELP(" ",1,"MA010AGR")
						lPodeApagar := .F.
						Exit
					EndIf
				Next
			Endif 
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se existe Produto x Endereço cadastrado (ACD)                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
            If lIntACD .And. AliasInDic("CBJ") .And. FindFunction("MTA010PXE")
	            If MTA010PXE(SB1->B1_COD)
    	           lPodeApagar := .F.
                   Exit
        	    EndIf 
            EndIf 
            
           	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se existe Mestre de Inventario cadastrado para o produto (ACD)        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
            If lIntACD .And. AliasInDic("CBJ") .And. FindFunction("MTA010MI")
	            If MTA010MI(SB1->B1_COD)
    	           lPodeApagar := .F.
                   Exit
        	    EndIf 
            EndIf 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se existe algum indicador do produto cadastrado                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SBZ")      // Indicador de Produtos
			dbSetOrder(1)

			aFiliais := If(! Empty(FwFilial("SBZ")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SBZ")})
			For nBusca := 1 to Len(aFiliais)
				If dbSeek(aFiliais[nBusca]+cCod)
					Aviso(STR0032,STR0039,{STR0031},2) //"Atencao!"###"O produto nao poderá ser excluído pois há um indicador cadastrado em Indicadores do Produto."###"Ok"
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

            If ! lPodeAPagar
				Exit
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se existe Base Instalada                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			#IFDEF TOP
				cAliasAA3 := GetNextAlias()
				cQuery    := ""

				cQuery += "SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "AA3" ) + " "
				cQuery += "WHERE "
				cQuery += "AA3_FILIAL='" + xFilial( "AA3" ) + "' AND "
				cQuery += "AA3_CODPRO='" + cCod             + "' AND "
				cQuery += "D_E_L_E_T_=' '"

				cQuery := ChangeQuery( cQuery )
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAA3,.F.,.T. )

				If (cAliasAA3)->QTDBASE > 0
					Help(" ",1,"NODELSB1")
					lPodeApagar := .F.
				Endif

				dbSelectArea(cAliasAA3)
				dbCloseArea()
				dbSelectArea("AA3")
			#ELSE
				cAliasAA3 := "AA3"
				cQuery    := ""

				cQuery := "AA3_FILIAL=='" + xFilial( "AA3" ) + "' .And. "
				cQuery += "AA3_CODPRO=='" + cCod             + "'"

				cArqInd   := CriaTrab(NIL,.F.)
				IndRegua(cAliasAA3,cArqInd,"AA3_FILIAL+AA3_CODPRO",,cQuery) //"Selecionando Registros ..."

				nIndex := RetIndex("AA3")
				dbSetIndex(cArqInd+OrdBagExT())
				dbSetOrder(nIndex+1)
				dbGotop()

				If (cAliasAA3)->(!Eof())
					Help(" ",1,"NODELSB1")
					lPodeApagar := .F.
				Endif

				dbSelectArea(cAliasAA3)
				dbClearFilter()
				RetIndex("AA3")
				FErase(cArqInd+OrdBagExt())
			#ENDIF		

			If !lPodeApagar
				Exit
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se existe Acessorio da Base Instalada            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			#IFDEF TOP
				cAliasAA4 := GetNextAlias()
				cQuery    := ""

				cQuery += "SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "AA4" ) + " "
				cQuery += "WHERE "
				cQuery += "AA4_FILIAL='" + xFilial( "AA4" ) + "' AND "
				cQuery += "AA4_PRODAC='" + cCod             + "' AND "
				cQuery += "D_E_L_E_T_=' '"

				cQuery := ChangeQuery( cQuery )
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAA4,.F.,.T. )

				If (cAliasAA4)->QTDBASE > 0
					Help(" ",1,"NODELSB1")
					lPodeApagar := .F.
				EndIf

				dbSelectArea(cAliasAA4)
				dbCloseArea()
				dbSelectArea("AA4")
			#ELSE
				cAliasAA4 := "AA4"
				cArqInd   := CriaTrab(NIL,.F.)

				cQuery := "AA4_FILIAL=='" + xFilial( "AA4" ) + "' .And. "
				cQuery += "AA4_PRODAC=='" + cCod             + "'"

				IndRegua(cAliasAA4,cArqInd,"AA4_FILIAL+AA4_PRODAC",,cQuery) //"Selecionando Registros ..."

				nIndex := RetIndex("AA4")
				dbSetIndex(cArqInd+OrdBagExT())
				dbSetOrder(nIndex+1)
				dbGotop()

				If (cAliasAA4)->(!Eof())
					Help(" ",1,"NODELSB1")
					lPodeApagar := .F.
				EndIf

				dbSelectArea(cAliasAA4)
				dbClearFilter()
				RetIndex("AA4")
				FErase(cArqInd+OrdBagExt())
			#ENDIF

			If !lPodeApagar
				Exit
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se existe Tabela de precos                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lPodeApagar
				DA1->(dbSetOrder(2))
				If DA1->(MsSeek(xFilial("DA1")+cCod))
					Help(" ",1,"NODELETA",, STR0019,2,0 )
					lPodeApagar := .F.				
				Endif

				If ! lPodeApagar
					Exit
				Endif

			Endif

			dbSelectArea("AIB")
			aFiliais := If(! Empty(FwFilial("AIB")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("AIB")})
			For nBusca := 1 to Len(aFiliais)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se existe tabela de Preço ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				#IFDEF TOP
					cAliasAIB := GetNextAlias()
					cQuery    := ""

					cQuery += "SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "AIB" ) + " "
					cQuery += "WHERE "
					cQuery += "AIB_FILIAL='" + aFiliais[nBusca] + "' AND "
					cQuery += "AIB_CODPRO='" + cCod             + "' AND "
					cQuery += "D_E_L_E_T_=' '"

					cQuery := ChangeQuery( cQuery )
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAIB,.F.,.T. )

					If (cAliasAIB)->QTDBASE > 0
						Help(" ",1,"NODELETA",, STR0019,2,0 )
						lPodeApagar := .F.
					Endif

					dbSelectArea(cAliasAIB)
					dbCloseArea()
				#ELSE
					cAliasAIB := "AIB"
					cQuery    := ""

					cQuery := "AIB_FILIAL=='" + aFiliais[nBusca] + "' .And. "
					cQuery += "AIB_CODPRO=='" + cCod             + "'"

					cArqInd   := CriaTrab(NIL,.F.)
					IndRegua(cAliasAIB,cArqInd,"AIB_FILIAL+AIB_CODPRO",,cQuery) //"Selecionando Registros ..."

					nIndex := RetIndex("AIB")
					dbSetIndex(cArqInd+OrdBagExT())
					dbSetOrder(nIndex+1)
					dbGotop()

					If (cAliasAIB)->(!Eof())
						Help(" ",1,"NODELETA",, STR0019,2,0 )
						lPodeApagar := .F.
					Endif

					dbSelectArea(cAliasAIB)
					dbClearFilter()
					RetIndex("AIB")
					FErase(cArqInd+OrdBagExt())
				#ENDIF
			Next nBusca

			If !lPodeApagar
				Exit
			EndIf

			dbSelectArea("ADB")
			aFiliais := If(! Empty(FwFilial("ADB")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("ADB")})
			For nBusca := 1 to Len(aFiliais)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se existe contrato de parceria ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				#IFDEF TOP
					cAliasADB := GetNextAlias()
					cQuery    := ""

					cQuery += "SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "ADB" ) + " "
					cQuery += "WHERE "
					cQuery += "ADB_FILIAL='" + aFiliais[nBusca] + "' AND "
					cQuery += "ADB_CODPRO='" + cCod             + "' AND "
					cQuery += "D_E_L_E_T_=' '"

					cQuery := ChangeQuery( cQuery )
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasADB,.F.,.T. )

					If (cAliasADB)->QTDBASE > 0
						Aviso(STR0032,STR0030,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por um contrato de parceria e nao podera ser excluido."###"Ok"
						lPodeApagar := .F.
					Endif

					dbSelectArea(cAliasADB)
					dbCloseArea()
				#ELSE
					cAliasADB := "ADB"
					cQuery    := ""

					cQuery := "ADB_FILIAL=='" + aFiliais[nBusca] + "' .And. "
					cQuery += "ADB_CODPRO=='" + cCod             + "'"

					cArqInd   := CriaTrab(NIL,.F.)
					IndRegua(cAliasADB,cArqInd,"ADB_FILIAL+ADB_CODPRO",,cQuery) //"Selecionando Registros ..."

					nIndex := RetIndex("ADB")
					dbSetIndex(cArqInd+OrdBagExT())
					dbSetOrder(nIndex+1)
					dbGotop()

					If (cAliasADB)->(!Eof())
						Aviso(STR0032,STR0030,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por um contrato de parceria e nao podera ser excluido."###"Ok"
						lPodeApagar := .F.
					Endif

					dbSelectArea(cAliasADB)
					dbClearFilter()
					RetIndex("ADB")
					FErase(cArqInd+OrdBagExt())
				#ENDIF
			Next nBusca

			If !lPodeApagar
				Exit
			EndIf

			#IFDEF TOP
				dbSelectArea("CNB")
				aFiliais := If(! Empty(FwFilial("CNB")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("CNB")})
				For nBusca := 1 to Len(aFiliais)
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica se existe planilha de contrato - Gestao de Contratos ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

					cAliasCNB := GetNextAlias()
					cQuery    := ""

					cQuery += "SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "CNB" ) + " CNB "
					cQuery += "WHERE "
					cQuery += "CNB_FILIAL='" + aFiliais[nBusca] + "' AND "
					cQuery += "CNB_PRODUT='" + cCod             + "' AND "
					cQuery += "D_E_L_E_T_=' '"

					cQuery := ChangeQuery( cQuery )
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasCNB,.F.,.T. )

					If (cAliasCNB)->QTDBASE > 0
						Aviso(STR0032,STR0041,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por um contrato do ambiente 'Gestão de Contratos' e nao podera ser excluido"###"Ok"
						lPodeApagar := .F.
					Endif

					dbSelectArea(cAliasCNB)
					dbCloseArea()
				Next nBusca
	
				If !lPodeApagar
					Exit
				EndIf
			#ENDIF

			If IntTMS() //Validacao de exclusao do produto - ambiente SIGATMS
				#IFDEF TOP
					dbSelectArea("DTC")
					aFiliais := Iif(! Empty(FwFilial("DTC")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("DTC")})
					For nBusca := 1 To Len(aFiliais)
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Verifica se existe nota fiscal          ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						cAliasDTC := GetNextAlias()
						cQuery    := ""

						cQuery += "SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "DTC" ) + " "
						cQuery += "WHERE "
						cQuery += "DTC_FILIAL='" + aFiliais[nBusca] + "' AND "
						cQuery += "DTC_CODPRO='" + cCod             + "' AND "
						cQuery += "D_E_L_E_T_=' '"

						cQuery := ChangeQuery( cQuery )
						dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasDTC,.F.,.T. )

						If (cAliasDTC)->QTDBASE > 0
							Aviso(STR0032,STR0035,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por uma nota fiscal de cliente e nao podera ser excluido."###"Ok"
							lPodeApagar := .F.
						EndIf

						dbSelectArea(cAliasDTC)
						dbCloseArea()

						If !lPodeApagar
							Exit
						EndIf

					Next nBusca

					If lPodeApagar
						dbSelectArea("DE5")
						aFiliais := Iif(! Empty(FwFilial("DE5")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("DE5")})
						For nBusca := 1 To Len(aFiliais)
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Verifica se existe nota fiscal de EDI   ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							cAliasDE5 := GetNextAlias()
							cQuery    := ""

							cQuery += "SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "DE5" ) + " "
							cQuery += "WHERE "
							cQuery += "DE5_FILIAL='" + aFiliais[nBusca] + "' AND "
							cQuery += "DE5_CODPRO='" + cCod             + "' AND "
							cQuery += "D_E_L_E_T_=' '"

							cQuery := ChangeQuery( cQuery )
							dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasDE5,.F.,.T. )

							If (cAliasDE5)->QTDBASE > 0
								Aviso(STR0032,STR0036,{STR0031},2) //"Atencao!"###"Este produto esta sendo utilizado por uma nota fiscal de EDI e nao podera ser excluido."###"Ok"
								lPodeApagar := .F.
							Endif

							dbSelectArea(cAliasDE5)
							dbCloseArea()

							If !lPodeApagar
								Exit
							EndIf

						Next nBusca
				    Endif
					If !lPodeApagar
						Exit
					EndIf
				#ENDIF
			EndIf                                                                           
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ VERIFICA SE O PRODUTO ESTA CADASTRADO E EM USO POR ALGUMA COMPOSICAO NO PMS    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
            If lIntPms .And. lPodeApagar 
				lPodeApagar := PmsVerComp(cCod)

				If !lPodeApagar
					Exit
				EndIf

			EndIf                 

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se existe registros cadastrados (WMS)                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If	IntDL(cCod)
				If	lPodeApagar
					dbSelectArea("DCG")
					dbSetOrder(1) //DCG_FILIAL+DCG_CODPRO+DCG_SERVIC
					aFiliais := Iif(!Empty(FwFilial("DCG")) .And. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("DCG")})
					For nBusca := 1 To Len(aFiliais)
						If	DCG->(dbSeek(aFiliais[nBusca]+cCod))
							Help("", 1, "MT010WMS")
							lPodeApagar := .F.
						EndIf
					Next nBusca
				EndIf
				If	lPodeApagar
					dbSelectArea("DCH")
					dbSetOrder(1) //DCH_FILIAL+DCH_CODPRO+DCH_CODZON
					aFiliais := Iif(!Empty(FwFilial("DCH")) .And. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("DCH")})
					For nBusca := 1 To Len(aFiliais)
						If	DCH->(dbSeek(aFiliais[nBusca]+cCod))
							Help("", 1, "MT010WM1")
							lPodeApagar := .F.
						EndIf
					Next nBusca
				EndIf
				If	lPodeApagar
					dbSelectArea("DC3")
					dbSetOrder(1) //DC3_FILIAL+DC3_CODPRO+DC3_LOCAL+DC3_ORDEM
					aFiliais := Iif(!Empty(FwFilial("DC3")) .And. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("DC3")})
					For nBusca := 1 To Len(aFiliais)
						If	DC3->(dbSeek(aFiliais[nBusca]+cCod))
							Help("", 1, "MT010WM2")
							lPodeApagar := .F.
						EndIf
					Next nBusca
				EndIf
				If !lPodeApagar
					Exit
				EndIf
			EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica regra de desconto³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If	lPodeApagar
	 			#IFDEF TOP
					cAliasACP := GetNextAlias()
					cQuery    := ""
					cQuery += " SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "ACP" )
					cQuery += " WHERE "
					cQuery += " ACP_FILIAL='" + xFilial("ACP") + "' AND "
					cQuery += " ACP_CODPRO='" + cCod            + "' AND "
					cQuery += " D_E_L_E_T_=' '"

					cQuery := ChangeQuery( cQuery )
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasACP,.F.,.T. )

					If (cAliasACP)->QTDBASE > 0
						cMsgNegacao := STR0053 //"Este produto esta sendo utilizado por uma regra de desconto"
						lPodeApagar := .F.
					Endif

					(cAliasACP)->(dbCloseArea())

				#ELSE 
					cAliasACP := "ACP"
					cQuery    := ""

					cQuery := "ACP_FILIAL=='" + xFilial( "ACP" ) + "' .And. "
					cQuery += "ACP_CODPRO=='" + cCod             + "'"

					cArqInd   := CriaTrab(NIL,.F.)
					IndRegua(cAliasACP,cArqInd,"ACP_FILIAL+ACP_CODPRO",,cQuery)//"Selecionando Registros ..."

					nIndex := RetIndex("ACP")
					dbSetIndex(cArqInd+OrdBagExT())
					dbSetOrder(nIndex+1)
					dbGotop()

					If (cAliasACP)->(!Eof())
						cMsgNegacao := STR0053 //"Este produto esta sendo utilizado por uma regra de desconto"
						lPodeApagar := .F.
					Endif

					dbSelectArea(cAliasACP)
					dbClearFilter()
					RetIndex("ACP")
					FErase(cArqInd+OrdBagExt())
			 	#ENDIF
			EndIf
			
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica regra de bonificação³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lPodeApagar
				#IFDEF TOP
					cAliasACQ_R := GetNextAlias()
					cQuery    := ""
					cQuery += " SELECT ACQ_CODPRO PROD FROM " + RetSqlName( "ACQ" )
					cQuery += "  WHERE ACQ_FILIAL='" + xFilial("ACR") + "' AND ACQ_CODPRO='" + cCod + "' AND D_E_L_E_T_ = ' ' "
					cQuery += " UNION "
					cQuery += " SELECT ACR_CODPRO PROD FROM " + RetSqlName( "ACR" )
					cQuery += "  WHERE ACR_FILIAL='" + xFilial("ACR") + "' AND ACR_CODPRO='" + cCod + "' AND D_E_L_E_T_ = ' ' "
					
					cQuery := ChangeQuery( cQuery )
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasACQ_R,.F.,.T. )
					
					If (cAliasACQ_R)->(!Eof())
						cMsgNegacao := STR0054 //"Este produto esta sendo utilizado por uma regra de bonificação"
						lPodeApagar := .F.
					Endif

					(cAliasACQ_R)->(dbCloseArea())

				#ELSE 
					cAliasACQ := "ACQ"
					cAliasACR := "ACR"
					cQuery    := ""

					cQuery := "ACQ_FILIAL=='" + xFilial( "ACQ" ) + "' .And. "
					cQuery += "ACQ_CODPRO=='" + cCod             + "'"

					cArqInd   := CriaTrab(NIL,.F.)
					IndRegua(cAliasACQ,cArqInd,"ACQ_FILIAL+ACQ_CODPRO",,cQuery)//"Selecionando Registros ACQ..."

					nIndex := RetIndex("ACQ")
					dbSetIndex(cArqInd+OrdBagExT())
					dbSetOrder(nIndex+1)
					dbGotop()

					If (cAliasACQ)->(!Eof())
						cMsgNegacao := STR0054 //"Este produto esta sendo utilizado por uma regra de bonificação"
						lPodeApagar := .F.
					Endif

					dbSelectArea(cAliasACQ)
					dbClearFilter()
					RetIndex("ACQ")
					FErase(cArqInd+OrdBagExt())
					
					If lPodeApagar
						cQuery := "ACR_FILIAL=='" + xFilial( "ACR" ) + "' .And. "
						cQuery += "ACR_CODPRO=='" + cCod             + "'"

						cArqInd   := CriaTrab(NIL,.F.)
						IndRegua(cAliasACR,cArqInd,"ACR_FILIAL+ACR_CODPRO",,cQuery)//"Selecionando Registros ACR..."
	
						nIndex := RetIndex("ACR")
						dbSetIndex(cArqInd+OrdBagExT())
						dbSetOrder(nIndex+1)
						dbGotop()
	
						If (cAliasACR)->(!Eof())
							cMsgNegacao := STR0054 //"Este produto esta sendo utilizado por uma regra de bonificação"
							lPodeApagar := .F.
						Endif
	
						dbSelectArea(cAliasACR)
						dbClearFilter()
						RetIndex("ACR")
						FErase(cArqInd+OrdBagExt())
					EndIf
			 	#ENDIF
			EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica regra de negocios   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lPodeApagar
				#IFDEF TOP
					cAliasACX_N := GetNextAlias()
					cQuery    := ""
					cQuery += " SELECT ACX_CODPRO PROD FROM " + RetSqlName( "ACX" )
					cQuery += "  WHERE ACX_FILIAL='" + xFilial("ACX") + "' AND ACX_CODPRO='" + cCod + "' AND D_E_L_E_T_ = ' ' "
					cQuery += " UNION "
					cQuery += " SELECT ACN_CODPRO PROD FROM " + RetSqlName( "ACN" )
					cQuery += "  WHERE ACN_FILIAL='" + xFilial("ACN") + "' AND ACN_CODPRO='" + cCod + "' AND D_E_L_E_T_ = ' ' "
					
					cQuery := ChangeQuery( cQuery )
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasACX_N,.F.,.T. )
					
					If (cAliasACX_N)->(!Eof())
						cMsgNegacao := STR0055 //"Este produto esta sendo utilizado por uma regra de negocios"
						lPodeApagar := .F.
					Endif

					(cAliasACX_N)->(dbCloseArea())

				#ELSE 
					cAliasACX := "ACX"
					cAliasACN := "ACN"
					cQuery    := ""

					cQuery := "ACX_FILIAL=='" + xFilial( "ACX" ) + "' .And. "
					cQuery += "ACX_CODPRO=='" + cCod             + "'"

					cArqInd   := CriaTrab(NIL,.F.)
					IndRegua(cAliasACX,cArqInd,"ACX_FILIAL+ACX_CODPRO",,cQuery)//"Selecionando Registros ACX..."

					nIndex := RetIndex("ACX")
					dbSetIndex(cArqInd+OrdBagExT())
					dbSetOrder(nIndex+1)
					dbGotop()

					If (cAliasACX)->(!Eof())
						cMsgNegacao := STR0055 //"Este produto esta sendo utilizado por uma regra de negocios"
						lPodeApagar := .F.
					Endif

					dbSelectArea(cAliasACX)
					dbClearFilter()
					RetIndex("ACX")
					FErase(cArqInd+OrdBagExt())
					
					If lPodeApagar
						cQuery := "ACN_FILIAL=='" + xFilial( "ACN" ) + "' .And. "
						cQuery += "ACN_CODPRO=='" + cCod             + "'"

						cArqInd   := CriaTrab(NIL,.F.)
						IndRegua(cAliasACN,cArqInd,"ACN_FILIAL+ACN_CODPRO",,cQuery)//"Selecionando Registros ACN..."
	
						nIndex := RetIndex("ACN")
						dbSetIndex(cArqInd+OrdBagExT())
						dbSetOrder(nIndex+1)
						dbGotop()
	
						If (cAliasACN)->(!Eof())
							cMsgNegacao := STR0055 //"Este produto esta sendo utilizado por uma regra de negocios"
							lPodeApagar := .F.
						Endif
	
						dbSelectArea(cAliasACN)
						dbClearFilter()
						RetIndex("ACN")
						FErase(cArqInd+OrdBagExt())
					EndIf
				#ENDIF
			EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica regra de bonif. financeira³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If	lPodeApagar
	 			#IFDEF TOP
					cAliasAI2 := GetNextAlias()
					cQuery    := ""
					cQuery += " SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "AI2" )
					cQuery += " WHERE "
					cQuery += " AI2_FILIAL='" + xFilial("AI2") + "' AND "
					cQuery += " AI2_CODPRO='" + cCod            + "' AND "
					cQuery += " D_E_L_E_T_=' '"

					cQuery := ChangeQuery( cQuery )
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAI2,.F.,.T. )

					If (cAliasAI2)->QTDBASE > 0
						cMsgNegacao := STR0056 //"Este produto esta sendo utilizado por uma regra de bonificação financeira"
						lPodeApagar := .F.
					Endif

					(cAliasAI2)->(dbCloseArea())

				#ELSE 
					cAliasAI2 := "AI2"
					cQuery    := ""

					cQuery := "AI2_FILIAL=='" + xFilial( "AI2" ) + "' .And. "
					cQuery += "AI2_CODPRO=='" + cCod             + "'"

					cArqInd   := CriaTrab(NIL,.F.)
					IndRegua(cAliasAI2,cArqInd,"AI2_FILIAL+AI2_CODPRO",,cQuery)//"Selecionando Registros ..."

					nIndex := RetIndex("AI2")
					dbSetIndex(cArqInd+OrdBagExT())
					dbSetOrder(nIndex+1)
					dbGotop()

					If (cAliasAI2)->(!Eof())
						cMsgNegacao := STR0056 //"Este produto esta sendo utilizado por uma regra de bonificação financeira"
						lPodeApagar := .F.
					Endif

					dbSelectArea(cAliasAI2)
					dbClearFilter()
					RetIndex("AI2")
					FErase(cArqInd+OrdBagExt())
			 	#ENDIF
			EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica verba de vendas           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lPodeApagar
	 			#IFDEF TOP
					cAliasACL := GetNextAlias()
					cQuery    := ""
					cQuery += " SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "ACL" )
					cQuery += " WHERE "
					cQuery += " ACL_FILIAL='" + xFilial("ACL") + "' AND "
					cQuery += " ACL_CODPRO='" + cCod            + "' AND "
					cQuery += " D_E_L_E_T_=' '"

					cQuery := ChangeQuery( cQuery )
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasACL,.F.,.T. )

					If (cAliasACL)->QTDBASE > 0
						cMsgNegacao := STR0057 //"Este produto esta sendo utilizado por uma verba de vendas"
						lPodeApagar := .F.
					Endif

					(cAliasACL)->(dbCloseArea())

				#ELSE 
					cAliasACL := "ACL"
					cQuery    := ""

					cQuery := "ACL_FILIAL=='" + xFilial( "ACL" ) + "' .And. "
					cQuery += "ACL_CODPRO=='" + cCod             + "'"

					cArqInd   := CriaTrab(NIL,.F.)
					IndRegua(cAliasACL,cArqInd,"ACL_FILIAL+ACL_CODPRO",,cQuery)//"Selecionando Registros ..."

					nIndex := RetIndex("ACL")
					dbSetIndex(cArqInd+OrdBagExT())
					dbSetOrder(nIndex+1)
					dbGotop()

					If (cAliasACL)->(!Eof())
						cMsgNegacao := STR0057 //"Este produto esta sendo utilizado por uma verba de vendas"
						lPodeApagar := .F.
					Endif

					dbSelectArea(cAliasACL)
					dbClearFilter()
					RetIndex("ACL")
					FErase(cArqInd+OrdBagExt())
			 	#ENDIF
			EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica Meta de vendas            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If	lPodeApagar
	 			#IFDEF TOP
					cAliasSCT := GetNextAlias()
					cQuery    := ""
					cQuery += " SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "SCT" )
					cQuery += " WHERE "
					cQuery += " CT_FILIAL='" + xFilial("SCT") + "' AND "
					cQuery += " CT_PRODUTO='" + cCod            + "' AND "
					cQuery += " D_E_L_E_T_=' '"

					cQuery := ChangeQuery( cQuery )
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSCT,.F.,.T. )

					If (cAliasSCT)->QTDBASE > 0
						cMsgNegacao := STR0058 //"Este produto esta sendo utilizado por uma meta de vendas"
						lPodeApagar := .F.
					Endif

					(cAliasSCT)->(dbCloseArea())

				#ELSE 
					cAliasSCT := "SCT"
					cQuery    := ""

					cQuery := "CT_FILIAL=='" + xFilial( "SCT" ) + "' .And. "
					cQuery += "CT_PRODUTO=='" + cCod             + "'"

					cArqInd   := CriaTrab(NIL,.F.)
					IndRegua(cAliasSCT,cArqInd,"CT_FILIAL+CT_PRODUTO",,cQuery)//"Selecionando Registros ..."

					nIndex := RetIndex("SCT")
					dbSetIndex(cArqInd+OrdBagExT())
					dbSetOrder(nIndex+1)
					dbGotop()

					If (cAliasSCT)->(!Eof())
						cMsgNegacao := STR0058 //"Este produto esta sendo utilizado por uma meta de vendas"
						lPodeApagar := .F.
					Endif

					dbSelectArea(cAliasSCT)
					dbClearFilter()
					RetIndex("SCT")
					FErase(cArqInd+OrdBagExt())
			 	#ENDIF
			EndIf         
               
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica Produtos Alternativos     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If	lPodeApagar
				dbSelectArea("SGI")
	 			#IFDEF TOP 
					cAliasSGI := GetNextAlias()
					cQuery := ""
					cQuery += " SELECT COUNT(*) QTDREG "
					cQuery += "   FROM " + RetSqlName( "SGI" )
					cQuery += "  WHERE GI_FILIAL  = '"+xFilial("SGI") + "'"					
					cQuery += "    AND GI_PRODORI = '"+cCod+ "'"
					cQuery += "    AND D_E_L_E_T_ = ' '"

					cQuery := ChangeQuery( cQuery )
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSGI,.F.,.T. )

					If (cAliasSGI)->QTDREG > 0
						cMsgNegacao := STR0060 //"Este produto possui Alternativos Vinculados"
						lPodeApagar := .F. 
					Else                         
						(cAliasSGI)->(dbCloseArea())					    
						cAliasSGI := GetNextAlias()
						cQuery := ""
						cQuery += " SELECT GI_PRODORI "
						cQuery += "   FROM " + RetSqlName( "SGI" )
						cQuery += "  WHERE GI_FILIAL  = '"+xFilial("SGI") + "'"					
						cQuery += "    AND GI_PRODALT = '"+cCod+ "'"
						cQuery += "    AND D_E_L_E_T_ = ' '"

						cQuery := ChangeQuery( cQuery )
						dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSGI,.F.,.T. )						
						     
						While !(cAliasSGI)->(Eof())
							cProdSGI+= Iif(Len(cProdSGI)>0,"/","")+(cAliasSGI)->GI_PRODORI
							dbSkip()
						Enddo
						If Len(cProdSGI)>0
							cMsgNegacao := STR0061+cProdSGI //"Produto Alternativo vinculado ao produto Original: "
							lPodeApagar := .F. 
						Endif                         							
	                    
	 				Endif

					(cAliasSGI)->(dbCloseArea())

				#ELSE 
					cAliasSGI := "SGI"
					cQuery    := ""

					cQuery := "GI_FILIAL=='" + xFilial( "SGI" ) + "' .And. "
					cQuery += "GI_PRODORI=='" + cCod            + "'"

					cArqInd   := CriaTrab(NIL,.F.)
					IndRegua(cAliasSGI,cArqInd,"GI_FILIAL+GI_PRODORI",,cQuery)//"Selecionando Registros ..."

					nIndex := RetIndex("SGI")
					dbSetIndex(cArqInd+OrdBagExT())
					dbSetOrder(nIndex+1)
					dbGotop()

					If (cAliasSGI)->(!Eof())
						cMsgNegacao := STR0060 //"Este produto possui Alternativos vinculados"
						lPodeApagar := .F.                         
					Else
						cAliasSGI := "SGI"
						cQuery    := ""
						cQuery := "GI_FILIAL=='" + xFilial( "SGI" ) + "' .And. "
						cQuery += "GI_PRODALT=='"+ cCod             + "'"
						cArqInd   := CriaTrab(NIL,.F.)
						IndRegua(cAliasSGI,cArqInd,"GI_FILIAL+GI_PRODORI",,cQuery)//"Selecionando Registros ..."
						nIndex := RetIndex("SGI")
						
						dbSetIndex(cArqInd+OrdBagExT())
						dbSetOrder(nIndex+1)
						dbGotop()												
						
						While !(cAliasSGI)->(Eof())
							cProdSGI+= Iif(Len(cProdSGI)>0,"/","")+(cAliasSGI)->GI_PRODORI
							dbSkip()
						Enddo
						If Len(cProdSGI)>0
							cMsgNegacao := STR0061+cProdSGI //"Produto Alternativo vinculado ao produto Origem: "
							lPodeApagar := .F. 
						Endif                         							

					Endif

					dbSelectArea(cAliasSGI)
					dbClearFilter()
					RetIndex("SGI")
					FErase(cArqInd+OrdBagExt())
			 	#ENDIF
			EndIf         

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica os itens do acessório TMK ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If	lPodeApagar
	 			#IFDEF TOP
					cAliasSUG := GetNextAlias()
					cQuery    := ""
					cQuery += " SELECT COUNT(*) QTDACESS FROM " + RetSqlName("SUG")
					cQuery += " WHERE "
					cQuery += " UG_FILIAL  = '" + xFilial("SUG") + "' AND "
					cQuery += " UG_PRODUTO = '" + cCod           + "' AND "
					cQuery += " D_E_L_E_T_ = ' '"

					cQuery := ChangeQuery( cQuery )
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSUG,.F.,.T. )

					If (cAliasSUG)->QTDACESS > 0
						cMsgNegacao := STR0067	//"Este produto esta sendo utilizado pelo Cadastro de Acessórios."
						lPodeApagar := .F.
					Endif

					(cAliasSUG)->(dbCloseArea())
                    
					If	lPodeApagar
						cAliasSU1 := GetNextAlias()
						cQuery    := ""
						cQuery += " SELECT COUNT(*) QTDACESS FROM " + RetSqlName("SU1")
						cQuery += " WHERE "
						cQuery += " U1_FILIAL  = '" + xFilial("SU1") + "' AND "
						cQuery += " U1_ACESSOR = '" + cCod           + "' AND "
						cQuery += " D_E_L_E_T_ = ' '"
		
						cQuery := ChangeQuery( cQuery )
						dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSU1,.F.,.T. )
		
						If (cAliasSU1)->QTDACESS > 0
							cMsgNegacao := STR0067	//"Este produto esta sendo utilizado pelo Cadastro de Acessórios."
							lPodeApagar := .F.
						Endif
		
						(cAliasSU1)->(dbCloseArea())
					EndIf
				#ENDIF
			EndIf                        

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica o Cad. Concorrentes - TMK ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If	lPodeApagar
	 			#IFDEF TOP
					cAliasSU2 := GetNextAlias()
					cQuery    := ""
					cQuery += " SELECT COUNT(*) QTDPROD FROM " + RetSqlName("SU2")
					cQuery += " WHERE "
					cQuery += " U2_FILIAL  = '" + xFilial("SU2") + "' AND "
					cQuery += " U2_COD = '" + cCod           + "' AND "
					cQuery += " D_E_L_E_T_ = ' '"

					cQuery := ChangeQuery( cQuery )
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSU2,.F.,.T. )

					If (cAliasSU2)->QTDPROD > 0
						cMsgNegacao := STR0068	//"Este produto esta sendo utilizado pelo Cadastro de Concorrentes."
						lPodeApagar := .F.
					Endif

					(cAliasSU2)->(dbCloseArea())
				#ENDIF
			EndIf
            
			If !lPodeApagar
				Help(" ",1,"NODELETA",,cMsgNegacao,2,0 )
				Exit
			EndIf
         
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se existe vinculo do cliente com o cadastro de Apontamento de Visitas ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			#IFDEF TOP
				If MA10VerAV()
					Exit
				EndIf
			#ENDIF		
		
			dbSelectArea(cAlias)

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Chama rotina para integracao com SFC(Chao de Fabrica) ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lPodeApagar .And. (lIntSFC .Or. lIntDPR)
				lPodeApagar := A010IntSFC(nOpc)
			EndIf

			// Ponto de Entrada para tratamento diverso antes da efetivacao de exclusao do produto            
            If lMT010EXC
            	ExecBlock("MT010EXC",.F.,.F.)
            EndIf               
			// Função usada para validar se existe integração do Protheus com o RM Solum e
			// Para chamar a validação específica do RM Solum na deleção do Produto. (PMSXFUNB)
			if lIntRMSOL
				If !PmsSlmPrd("PRD")
					Exit
				Endif
			Endif
                                                                       	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se existe vinculo do produto no cadastro de amarracao CategoriaXProduto ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("ACV")	// Categoria x Grupo ou Produto  
			dbSetOrder(5) 		// Ordem: ACV_FILIAL+ACV_CODPRO+ACV_CATEGO

			aFiliais := If(! Empty(xFilial("ACV")) .and. Empty(xFilial("SB1")), aClone(aSM0CodFil), {xFilial("ACV")})
			For nBusca := 1 to Len(aFiliais)
				dbSeek(aFiliais[nBusca]+cCod)
				If Found()
				    Aviso(STR0032,STR0062,{STR0031},1) //"Atencao!"###"Este produto não poderá ser excluído, pois existe vínculo do mesmo no cadastro de amarração de Categoria X Produto."###"Ok"
					dbSelectArea(cAlias)
					MsUnLock()
					lPodeApagar := .F.
					Exit
				EndIf
			Next

			If ! lPodeApagar
				Exit
			Endif         

			Begin Transaction
            
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ apos passar por todas as verificacoes , deleta o registro    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

				dbSelectArea(cAlias)
				RecLock(cAlias,.F.,.T.)
				dbDelete()
				
				//Deletando informações complementares do Produto (SIGAEIC/SIGAEDC/SIGAEEC) - Trade Easy
				If FindFunction("EYJCOMP") .And. (nModulo == 17 .Or. nModulo == 50  .Or. nModulo == 29)
				   EYJ->(DbSetOrder(1))
				   If EYJ->(DbSeek(xFilial("EYJ") + cCod))
				      If EYJ->(RecLock("EYJ",.F.))
				         EYJ->(DbDelete())
				      EYJ->(MsUnlock())
				      EndIf				   
				   EndIf
				EndIf 
                                                           		
				If lGrade
					cCodGr := Substr(cCod,1,nTamRef) 
				  	nRecCod:=(cAlias)->( recno() )
					If (cAlias)->(!DbSeek(xFilial(cAlias)+cCodGr))
						dbSelectArea("SB4")
						aFiliais := If(! Empty(FwFilial("SB4")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SB4")})
						For nBusca := 1 to Len(aFiliais)
							If dbSeek(aFiliais[nBusca]+cCodGr)
								RecLock("SB4",.F.,.T.)
								dbDelete()
							EndIf
						Next
					Endif
					(cAlias)->( msgoto(nRecCod) )
				Endif           
                
				dbSelectArea("SB2")
				aFiliais := If(! Empty(FwFilial("SB2")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SB2")})
				For nBusca := 1 to Len(aFiliais)
					dbSeek(aFiliais[nBusca]+cCod)
					While !EOF() .And. aFiliais[nBusca]+cCod == B2_FILIAL+B2_COD
						RecLock("SB2",.F.,.T.)
						dbDelete()
						dbSkip()
					EndDo
				Next

				dbSelectArea("SB3")

				aFiliais := If(! Empty(FwFilial("SB3")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SB3")})
				For nBusca := 1 to Len(aFiliais)
					dbSeek(aFiliais[nBusca]+cCod)
					If Found()
						RecLock("SB3",.F.,.T.)
						dbDelete()
					EndIf
				Next

				lApagaSB5 := .T.
				dbSelectArea("SB5")
				If Empty(FwFilial("SB5")) .And. !Empty(FwFilial("SB1"))
					cSeek := SB1->B1_FILIAL+AllTrim(SB1->B1_COD) 
					For nBusca := 1 to Len(aSM0CodFil)
						dbSelectArea("SB1")
						dbSetOrder(1)
						If dbSeek(aSM0CodFil[nBusca]+cCod)
							If cSeek <> (aSM0CodFil[nBusca]+AllTrim(cCod))
								lApagaSB5 := .F. 
								Exit
							EndIf	
						EndIf						
					Next nBusca
				EndIf					
				If lApagaSB5
					dbSelectArea("SB5")
					aFiliais := If(! Empty(FwFilial("SB5")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SB5")})
					For nBusca := 1 to Len(aFiliais)
						dbSeek(aFiliais[nBusca]+cCod)
						If Found()
							RecLock("SB5",.F.,.T.)
							dbDelete()
						EndIf
					Next
				EndIf	

				If lFantasma
					dbSelectArea("SB9")
					aFiliais := If(! Empty(FwFilial("SB9")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SB9")})
					For nBusca := 1 to Len(aFiliais)
						dbSeek(aFiliais[nBusca]+cCod)
						While !Eof() .And. aFiliais[nBusca]+cCod==B9_FILIAL+B9_COD
							If B9_QINI == 0
								RecLock("SB9",.F.,.T.)
								dbDelete()
							Endif
							dbSkip()
						End
					Next
				Endif

				dbSelectArea("SA5")
				dbSetOrder(2)
				aFiliais := If(! Empty(FwFilial("SA5")) .and. Empty(FwFilial("SB1")), aClone(aSM0CodFil), {xFilial("SA5")})
				For nBusca := 1 to Len(aFiliais)
					dbSeek(aFiliais[nBusca]+cCod)
					While !EOF() .And. aFiliais[nBusca] == A5_FILIAL .and. cCod == A5_PRODUTO
						RecLock("SA5",.F.,.T.)
						dbDelete()
						dbSkip()
					EndDo
				Next
				dbSetOrder(1)
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Remove a amarracao cod. Externo X cod. Interno - tabela XXF - De/Para                    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lPodeApagar .And. FindFunction("CFGA070Mnt") .And. FWXX4Seek("MATA010")
					If ( XX4->XX4_SENDER == '1' )			
		      		CFGA070Mnt( , 'SB1', 'B1_COD', , SB1->B1_COD, .T. )  
		   		EndIf
				EndIf   

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Pontos de Entrada											 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				IF lMTA010ET
					ExecTemplate("MTA010E",.f.,.f.)
				Endif

				IF lMTA010E
					ExecBlock("MTA010E",.f.,.f.)
				Endif

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Funcao Especifica SIGAEIC - AVERAGE                        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				MTA010E()

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Funcao Especifica SIGAEIC - AVERAGE                        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ				
				If lHistFiscal
					aCmps :=  RetCmps("SB1",bCampoSB1)	
					GrvHistFis("SB1", "SS4", aCmps) 
				EndIf	
				

			End Transaction
			
			A010Int( 2, nOpc, aIntSB1 )

			If FindFunction ("LJ110AltOk")
				LJ110AltOk()
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Tratamento para a chamada de função IntegDef() ³
			//Se a funcao  GETROTINTEG() existir no RPO, significa que a EnchoiceBar³
			//nao chama a IntegDef, logo deve ser chamada manualmente             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			If lPodeApagar .And. lIntegDef
				FwIntegDef("MATA010") 			                                         	
			EndIf
			
		ELSE
			MsUnLock()
		ENDIF
		EXIT
	EndIf
EndDo
dbSelectArea(cAlias)

If (ExistBlock("MT010CAN"))
	ExecBlock("MT010CAN",.F.,.F.,{nOpcA})
EndIf


Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³A010DelPeC³ Autor ³Alex Sandro Valario    ³ Data ³ 16/09/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Rotina de exclusao de produto visualizando o complemento   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpN1 := A010DelPeC(ExpC1,ExpN2,ExpN3)                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN2 = Numero do registro                                 ³±±
±±³          ³ ExpN3 = Numero da opcao selecionada 			              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpN1 = 1(confirmado),2(nao confirmou) ou 3(erro tratado)  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010DELPeC(cAlias,nReg,nOpc)

Local nOpcA 	:= 0
Local aArea    := GetArea()
Local cCpoFil  := PrefixoCpo(cAlias)+"_FILIAL"
Local cMemo    := ""
LOcal nX       := 0
Local oDlg
Local nTop
Local nLeft
Local nBottom
Local nRight
Local nRegSB5	:= SB5->(RecNo())  
Local aObjects    := {}
Local aSize       := {} 
Local aInfo       := {}
Local aPosObj     := {} 

//enchoice
Local oFolder
Private oEnc01
Private oEnc02
Private aTELA:=NIL,aGets:=NIL

Private Altera :=.F.
Private Inclui :=.F.   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta a largura para o tamanho padrao Protheus ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSize := MsAdvSize()       
		
aObjects := {}
AAdd( aObjects, { 100, 100, .T., .T. } )
aInfo    := { aSize[1], aSize[2], aSize[3], aSize[4], 2, 2 } 

aPosObj := MsObjSize(aInfo,aObjects,.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³VerIfica se esta' vizualizando um registro da mesma filial            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SB5")
DbSelectArea(cAlias)
If (cAlias)->(FieldPos(cCpoFil))==0 .Or. (cAlias)->(FieldGet(FieldPos(cCpoFil))) == xFilial(cAlias)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta a entrada de dados do arquivo						     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RegToMemory("SB1", .F., .F. )
	RegToMemory("SB5",.F. , .F. )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa variaveis para campos Memos Virtuais						 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Type("aMemos")=="A"
		For nX := 1 To Len(aMemos)
			cMemo := aMemos[nX][2]
			If ExistIni(cMemo)
				&cMemo := InitPad(SX3->X3_RELACAO)
			Else
				&cMemo := ""
			EndIf
		Next nX
	EndIf

/*	If SetMDIChild()
		oMainWnd:ReadClientCoors()
		nTop := 40
		nLeft := 30
		nBottom := oMainWnd:nBottom-80
		nRight := oMainWnd:nRight-70
	Else
		nTop := 135
		nLeft := 0
		nBottom := TranslateBottom(.T.,28)
		nRight := 632
	EndIf
*/
//	DEFINE MSDIALOG oDlg TITLE cCadastro FROM nTop,nLeft TO nBottom,nRight PIXEL OF oMainWnd
    DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL
	oFolder := TFolder():New(13,,{STR0033,STR0034},{STR0033,STR0034},oDlg,,,,.t.,.F.,oDlg:nWidth,oDlg:nHeight,)	 //"Produto"###"Complemento"###"Produto"###"Complemento"
	oFolder:align:= CONTROL_ALIGN_ALLCLIENT
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Enchoice 01                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SB1")
	oEnc01:= MsMGet():New("SB1" ,nReg ,nOpc,,,,,aPosObj[1],,,,,,oFolder:aDialogs[1])
	oEnc01:oBox:align:= CONTROL_ALIGN_ALLCLIENT

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Enchoice 02                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SB5")
	oEnc02:=MsMGet():New("SB5",nRegSB5,nOpc,,,,,aPosObj[1],,,,,,oFolder:aDialogs[2])
	oEnc02:oBox:align:= CONTROL_ALIGN_ALLCLIENT

	dbSelectArea("SB1")
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpcA := 2,oDlg:End()},{|| nOpcA := 0,oDlg:End()})	
	
Else
	Help(" ",1,"A000FI")
	nOpcA := 3
EndIf
RestArea(aArea)

Return(nOpcA)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A010Tipo ³ Autor ³ Jose Lucas            ³ Data ³ 12.11.93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica a existencia do Tipo na Tabela de Parametros.     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A010Tipo()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MatA010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010Tipo()

LOCAL cVar, cSavAlias := Alias(), lRet := .T.

cVar := &(ReadVar())

dbSelectArea("SX5")
dbSeek(xFilial("SX5")+"02"+cVar)
If !Found()
	Help(" ",1,"MA01002")
	lRet := .F.
EndIf
dbSelectArea(cSavAlias)
Return(lRet)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A010Grupo³ Autor ³ Jose Lucas            ³ Data ³ 12.11.93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica a existencia do Grupo na Tabela de Parametros.    ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A010Grupo()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MatA010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010Grupo()

LOCAL cVar, cSavAlias := Alias(), lRet := .T.

cVar := &(ReadVar())

dbSelectArea("SBM")
dbSetOrder(1)
dbSeek(xFilial("SBM")+cVar)

If !Found()
	Help(" ",1,"MA01002")
	lRet := .F.
EndIf

//-- MIL - CODITE - Validação para não permitir mesmo Grupo+Codite quando utilizado o Módulo de Concessionárias
if isincallstack('MATA010')
	If lRet .And. SuperGetMV("MV_VEICULO",.F.,"N") == "S" .And. !Empty(M->B1_CODITE) 
	   lRet := ExistChav("SB1",M->B1_GRUPO+M->B1_CODITE,7)
	EndIf
EndIf
dbSelectArea(cSavAlias)
Return(lRet)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A010Mult ³ Autor ³ Jose Lucas            ³ Data ³ 26.11.93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Comsistir se B1_LE e B1_QE sao multiplos.                  ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A010Mult()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MatA010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010Mult()

LOCAL cVar, cCampo, lRet := .T.

cCampo := ReadVar()
cVar := &(ReadVar())

If AllTrim(cCampo) == "B1_LE"
	If !Empty(M->B1_QE) .And. !Empty(M->B1_LE)
		If QtdComp(Mod( M->B1_LE,M->B1_QE )) > QtdComp(0)
			Help(" ",1,"MA01003")
			lRet := .F.
		EndIf
	EndIf
Else
	If !Empty(M->B1_LE) .And. !Empty(M->B1_QE)
		If QtdComp(Mod( M->B1_LE,M->B1_QE )) > QtdComp(0)
			Help(" ",1,"MA01003")
			lRet := .F.
		EndIf
	EndIf
EndIf
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A010MONO ³ Autor ³ Marcelo B. Abe        ³ Data ³ 04/04/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Validacao de Fornecedor de Dominio do Canal    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A010MONO(void)                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SX3->X3_VALID(B1_MONO)                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function A010MONO()

Local lRet := .T.
If EMPTY(M->B1_PROC) .And. M->B1_MONO=="S"
	HELP(" ",1,"A010MONO")
	lRet := .F.
Endif
Return(lRet)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A010PROC ³ Autor ³ Marcelo B. Abe        ³ Data ³ 04/04/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Validacao do Fornecedor Padrao.                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A010PROC(void)                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SX3->X3_VALID(B1_PROC)                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010PROC()

Local lRet  := .T.
Local cLoja := Space(2)

If Empty(M->B1_PROC)
	M->B1_MONO:=SPACE(1)
	M->B1_LOJPROC :=Space(2)
Else   
	If M->B1_PROC != SA2->A2_COD      
		SA2->(DbSetOrder(1))
		SA2->(DbSeek(xFilial("SA2")+M->B1_PROC))
	Endif 
	cLoja := SA2->A2_LOJA	                                         
	lRet := ExistCpo("SA2",M->B1_PROC+cLoja)
	M->B1_LOJPROC := cLoja
Endif

Return(lRet)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³A010Visul ³ Autor ³ Erike Yuri da Silva   ³ Data ³18/04/05  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para visualizar produto e complemento             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpN1 := A010Visul(ExpC1,ExpN2,ExpN3)                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³			 ³ ExpN2 = Numero do registro                                 ³±±
±±³			 ³ ExpN3 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpN1 = 1(confirmado),2(nao confirmou)					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Mata010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010Visul(cAlias,nReg,nOpc)
Local nOpcA		:= 3
Local aArea		:= GetArea()
Local aAreaSB1	:= SB1->(GetArea())
Local aAreaSB5	:= SB5->(GetArea())
Local aUsrBut	:= {}
Local aButtons	:= {}
Local aCpos		:= {}
Local cPerg     := "MTA010"
Local aMvPar    := {}
Local nCnt      := 0
Local aRotAnt   := {}
Local NTamSX1   := Len(SX1->X1_GRUPO)

DEFAULT cAlias	:= Alias()
DEFAULT nReg 	:= SB1->(Recno())
DEFAULT nOpc	:= 2

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Salva o conteudo dos parametros mv_par?? e da aRotina  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SX1->( MsSeek(PADR(cPerg,NTamSX1)) )
Do While SX1->( !Eof() .And. X1_GRUPO == PADR(cPerg,NTamSX1) )
	nCnt++
	Aadd(aMvPar, &('MV_PAR'+StrZero(nCnt,2)) )
	SX1->( dbSkip() )
EndDo
If Type("aRotina")=='A'
	aRotAnt := aClone(aRotina)
EndIf
aRotina := MenuDef() // Atualiza o aRotina com as opcoes do MATA010

Pergunte(cPerg,.F.)

//Inclusao automatica de botoes atraves de ponto de entrada

If ExistTemplate( "MA010BUT" )
	If Valtype( aUsrBut := ExecTemplate( "MA010BUT", .f., .f. ) ) == "A"
		AEval( aUsrBut, { |x| AAdd( aButtons, x ) } )
	EndIF
EndIf

If ExistBlock( "MA010BUT" )
	If Valtype( aUsrBut := Execblock( "MA010BUT", .f., .f. ) ) == "A"
		AEval( aUsrBut, { |x| AAdd( aButtons, x ) } )
	EndIF
EndIf

//Ponto de Entrada para definir quais campos da SB1 serao carregados na visualizacao
If ExistBlock("MT010VCP")
	aCpos:= ExecBlock("MT010VCP",.F.,.F.)
	If ValType(aCpos) <> "A" 
		aCpos:= {}
	EndIf
EndIf

If MV_PAR02 == 1
	SB5->(DbSetOrder(1))
	If SB5->(DbSeek(xFilial("SB5")+SB1->B1_COD))
		nOpcA:=A010VilPeC( cAlias, nReg, nOpc,aButtons,IIF(Len(aCpos)>0,aCpos,Nil))
	Else
		nOpcA:=AxVisual( cAlias, nReg, nOpc,IIF(Len(aCpos)>0,aCpos,Nil),,,,aButtons)
	EndIf
Else
	nOpcA:=AxVisual( cAlias, nReg, nOpc,IIF(Len(aCpos)>0,aCpos,Nil),,,,aButtons)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura o conteudo dos parametros mv_par?? e do aRotina³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aRotAnt) > 0
	aRotina := aClone(aRotAnt)
EndIf
For nCnt := 1 To Len(aMvPar)
	&('MV_PAR'+StrZero(nCnt,2)) := aMvPar[nCnt]
Next nCnt

RestArea(aAreaSB1)
RestArea(aAreaSB5)
RestArea(aArea)
Return(nOpcA)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³A010VilPeC³ Autor ³                       ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para visualizar dados complementares do produto   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpN1 := A010VilPeC(ExpC1,ExpN2,ExpN3,ExpA1)               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³			 ³ ExpN2 = Numero do registro                                 ³±±
±±³			 ³ ExpN3 = Numero da opcao selecionada                        ³±±
±±³			 ³ ExpA1 = Array contendo os botoes da rotina                 ³±±
±±³			 ³ ExpA2 = Campos da SB1 que serao utilizados na Visualizacao ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpN1 = 1(confirmado),2(nao confirmou)					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Mata010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010VilPeC(cAlias,nReg,nOpc,aButtons,aCpos)

Local aArea    := GetArea()
Local nOpcA    := 3
Local cCpoFil  := PrefixoCpo(cAlias)+"_FILIAL"
Local cMemo    := ""
LOcal nX       := 0
Local oDlg
Local lVirtual :=.F. // Qdo .F. carrega inicializador padrao nos campos virtuais
Local nTop
Local nLeft
Local nBottom
Local nRight
Local nRegSB5	:= SB5->(RecNo())

//enchoice
Local oFolder
Private oEnc01
Private oEnc02
Private aTELA:=NIL,aGets:=NIL

Private Altera :=.F.
Private Inclui :=.F.

Default aButtons := {} 


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta a largura para o tamanho padrao Protheus ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSize := MsAdvSize()       
		
aObjects := {}
AAdd( aObjects, { 100, 100, .T., .T. } )
aInfo    := { aSize[1], aSize[2], aSize[3], aSize[4], 2, 2 } 

aPosObj := MsObjSize(aInfo,aObjects,.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³VerIfica se esta' vizualizando um registro da mesma filial            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SB5")
DbSelectArea(cAlias)
If (cAlias)->(FieldPos(cCpoFil))==0 .Or. (cAlias)->(FieldGet(FieldPos(cCpoFil))) == xFilial(cAlias)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta a entrada de dados do arquivo						     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RegToMemory(cAlias, .F., .F. )
	RegToMemory("SB5",.F. , .F. )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa variaveis para campos Memos Virtuais						 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Type("aMemos")=="A"
		For nX := 1 To Len(aMemos)
			cMemo := aMemos[nX][2]
			If ExistIni(cMemo)
				&cMemo := InitPad(SX3->X3_RELACAO)
			Else
				&cMemo := ""
			EndIf
		Next nX
	EndIf

	If SetMDIChild()
		oMainWnd:ReadClientCoors()
		nTop := 40
		nLeft := 30
		nBottom := oMainWnd:nBottom-80
		nRight := oMainWnd:nRight-70
	Else
		nTop := 135
		nLeft := 0
		nBottom := TranslateBottom(.T.,28)
		nRight := 632
	EndIf

    DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL
//	DEFINE MSDIALOG oDlg TITLE cCadastro FROM nTop,nLeft TO nBottom,nRight PIXEL OF oMainWnd
	oFolder := TFolder():New(13,,{STR0033,STR0034},{STR0033,STR0034},oDlg,,,,.t.,.F.,oDlg:nWidth,oDlg:nHeight,)	 //"Produto"###"Complemento"###"Produto"###"Complemento"
	oFolder:align:= CONTROL_ALIGN_ALLCLIENT
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Enchoice 01                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SB1")
	oEnc01:= MsMGet():New("SB1" ,nReg ,nOpc,,,,IIF(ValType(aCpos)=="A",aCpos,Nil),aPosObj[1],   ,       ,        ,          ,      ,oFolder:aDialogs[1])
	oEnc01:oBox:align:= CONTROL_ALIGN_ALLCLIENT

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Enchoice 02                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SB5")
	oEnc02:=MsMGet():New("SB5",nRegSB5,nOpc,,,,/*aAcho*/,aPosObj[1],,,,,,oFolder:aDialogs[2])
	oEnc02:oBox:align:= CONTROL_ALIGN_ALLCLIENT

	dbSelectArea("SB1")
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpcA := 1,oDlg:End()},{|| nOpcA := 2,oDlg:End()},,aButtons)	
Else
	Help(" ",1,"A000FI")
	nOpcA := 3
EndIf
RestArea(aArea)
Return(nOpcA)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010Inclui³ Autor ³ Marcelo B. Abe        ³ Data ³ 20/04/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Inclusao de Produtos                           ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A010Inclui(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada 			              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010()                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010INCLUI(cAlias,nReg,nOpc)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aButtons := {}
Local aUsrBut  := {}
Local aCampos  := {}
Local nSaveSX8 := GetSx8Len() 
Local lPIMSINT := (SuperGetMV("MV_PIMSINT",.F.,.F.) .And. FindFunction("PIMSGeraXML")) .And. SBM->(FieldPos("BM_CLASGRU")) > 0 // Indica se Existe Integração Protheus x PIMS Graos
Local lUsaMsg	 := SuperGetMV("MV_MSGPIMS",.F.,.F.) //Nova integração via MGS Unica 
Local aIntSB1  := {}
Local uMv_par01,uMv_par02 
Local lCopiaBKP 

//-- Variavel usada para verificar se o disparo da funcao IntegDef() pode ser feita manualmente
Local lIntegDef  :=  FindFunction("GETROTINTEG") .And. FindFunction("FWHASEAI") .And. FWHasEAI("MATA010",.T.,,.T.) 

Private aTELA[0][0],aGETS[0]

l010Auto:= If(Type('l010Auto') <> 'L',.f.,l010Auto)
lCopia  := If(Type('lCopia') <> 'L',.f.,lCopia)  
lCopiaBkp:=lCopia

If lCopia .And. !IsInCallStack("MATA010")
	lCopia:=.F.
EndIf

bCampo := {|nCPO| Field(nCPO) }

INCLUI := .T.
ALTERA := .F.

If Type("MV_PAR01")<>"U"
	uMv_par01 := MV_PAR01 
EndIf

If Type("MV_PAR02")<>"U"
	uMv_par02 := MV_PAR02
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desativa a tecla F4                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

PERGUNTE("MTA010",.F.)

If !l010Auto
	SET KEY VK_F4 TO
EndIf

If ExistTemplate( "MA010BUT" )
	If Valtype( aUsrBut := ExecTemplate( "MA010BUT", .f., .f. ) ) == "A"
		AEval( aUsrBut, { |x| AAdd( aButtons, x ) } )
	EndIF
EndIf

If ExistBlock( "MA010BUT" )
	If Valtype( aUsrBut := Execblock( "MA010BUT", .f., .f. ) ) == "A"
		AEval( aUsrBut, { |x| AAdd( aButtons, x ) } )
	EndIF
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Integracao PIMS GRAOS                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lPIMSINT .And. !lUsaMsg
	// Tratamento para adicinar o campo BM_CLASGRU no layout do arquivo XML 
	SX3->(DbSetOrder(2))
   	SX3->(DbSeek("BM_CLASGRU"))
	AADD(aCampos, {SX3->(X3_TITULO), SX3->(X3_DESCRIC), SX3->(X3_CAMPO), SX3->(X3_TIPO), SX3->(X3_TAMANHO), SX3->(X3_DECIMAL),NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL})
EndIf

While .T.
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia para processamento dos Gets          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nOpcA:=0

	If ( l010Auto )
		Begin Transaction
			nOpcA := AxInclui(cAlias, nReg, nOpc,/*aAcho*/,/*cFunc*/,/*aCpos*/,"A010TudoOk(nOpc)",/*lF3*/,/*cTransact*/,aButtons,,aRotAuto,/*lVirtual*/)
			If __lSX8
				IF nOpcA == 1
					While ( GetSx8Len() > 0 )
						ConfirmSX8()
					Enddo
					A010Int( 2, nOpc, aIntSB1 )
				Else
					While ( GetSx8Len() > 0 )
						RollBackSX8()
					Enddo
				EndIf
			EndIf
		End Transaction
	Else
		If lCopia
			Begin Transaction
				If mv_par02 == 1
					SB5->(DbSetOrder(1))
					SB5->(DbSeek(xFilial("SB5")+SB1->B1_COD))
					nOpcA:=A010IncPeC( cAlias, nReg, nOpc,"A010LeReg",,"A010TudoOk(nOpc)",aButtons,.T.)
				Else
					nOpcA:=AxInclui( cAlias, nReg, nOpc,,"A010LeReg",,"A010TudoOk(nOpc)",,,aButtons,,,.T.)
				EndIf	
				If __lSX8
					IF nOpcA == 1
						While ( GetSx8Len() > nSaveSX8 )
							ConfirmSx8()
						Enddo
						A010Int( 2, nOpc, aIntSB1 )
					Else
						While ( GetSx8Len() > nSaveSX8 )
							RollBackSX8()
						Enddo
					EndIf
				EndIf
			End Transaction
		Else	
			Begin Transaction
				If mv_par02 == 1
					nOpcA:=A010IncPeC( cAlias, nReg, nOpc, , ,"A010TudoOk(nOpc)",aButtons) // incluir Produto e complemento
				Else
					nOpcA:=AxInclui( cAlias, nReg, nOpc,,,,"A010TudoOk(nOpc)",,,aButtons)
				EndIf
			End Transaction
		EndIf
	EndIf

	dbSelectArea(cAlias)

	IF nOpcA == 1
		A010Int( 2, nOpc, aIntSB1 )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Faz as Consistencias entre o F.D. do Canal com o SA5.                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If B1_MONO == 'S'
			A010FDC()
		Else
			A010AtuDesc()
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se for codigo inteligente acrescenta estrutura                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		A093VldCod(B1_COD,.T.,,,,,,,,,.T.)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Envia e-mail ref. Inclusao de novos produtos - 033 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		MEnviaMail("033",{B1_FILIAL,B1_COD,B1_DESC,B1_TIPO,B1_LOCPAD,B1_UM,CUSERNAME})

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Pontos de entrada depois da confirma‡Æo.                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ExistTemplate("MT010INC")
			ExecTemplate("MT010INC")
		Endif

		If ExistBlock("MT010INC")
			ExecBlock("MT010INC",.F.,.F.)
		Endif   
		/*  GRAVACAO DO B1_CODITE QUANDO INTEGRADO COM CONCESSIONARIAS */      
		If SuperGetMV("MV_VEICULO",.F.,"N")=="S" .and. Empty(SB1->B1_CODITE)
			Reclock("SB1",.F.)
			SB1->B1_CODITE := SB1->B1_COD
			MSUnlock()
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Integracao PIMS GRAOS                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lPIMSINT .And. !lUsaMsg
		    PIMSGeraXML("Item",STR0047,"2","SB1",aCampos) //--## Cadastro de produtos
		EndIf
		/*--------------------*/

		
		If FindFunction ("LJ110AltOk")
			LJ110AltOk()
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Tratamento para a chamada de função IntegDef() ³
		//Se a funcao  GETROTINTEG() existir no RPO, significa que a EnchoiceBar³
		//nao chama a IntegDef, logo deve ser chamada manualmente             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
		If lIntegDef					
			FwIntegDef("MATA010") 
		EndIf

	Endif

	Exit
End
dbSelectArea(cAlias)

If (ExistBlock("MT010CAN"))
	ExecBlock("MT010CAN",.F.,.F.,{nOpcA})
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ativa tecla  para comunicacao com Saldos dos Lotes           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !l010Auto
	Set Key VK_F4 TO MT010F4()
EndIf  

If Type("MV_PAR01")<>"U" 
	MV_PAR01 := uMv_par01
EndIf

If Type("MV_PAR02")<>"U"
	MV_PAR02 := uMv_par02
EndIf

lCopia:=lCopiaBKP

Return Nil


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010Altera³ Autor ³ Marcelo B. Abe        ³ Data ³ 20/04/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Alteracao de Produtos                          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A010Altera(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada 			              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010()                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010ALTERA(cAlias,nReg,nOpc)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aButtons   := {}
Local aUsrBut    := {}
Local aCampos    := {}
Local lTemSB5    := .T.
Local nX         := 0
Local lHistTab   := SuperGetMV("MV_HISTTAB", .F., .F.)
Local lPIMSINT   := (SuperGetMV("MV_PIMSINT",.F.,.F.) .And. FindFunction("PIMSGeraXML")) .And. SBM->(FieldPos("BM_CLASGRU")) > 0 // Indica se Existe Integração Protheus x PIMS Graos
Local lUsaMsg	   := SuperGetMV("MV_MSGPIMS",.F.,.F.) //Nova integração via MGS Unica 
Local cFilialAIF := xFilial("AIF")
Local cFilialSB1 := xFilial("SB1")
Local dDataAlt
Local cHoraAlt
Local aIntSB1	 := {}  
Local bCampoSB1 := { |x| SB1->(Field(x)) }   
Local aCmps     := {}

//-- Variavel usada para verificar se o disparo da funcao IntegDef() pode ser feita manualmente
Local lIntegDef  :=  FindFunction("GETROTINTEG") .And. FindFunction("FWHASEAI") .And. FWHasEAI("MATA010",.T.,,.T.) 

Private aTELA[0][0],aGETS[0]

bCampo := {|nCPO| Field(nCPO) }
l010Auto := If(Type("l010Auto") == "U", .f.,l010Auto)

INCLUI := .F.
ALTERA := .T.
//Ajuste no dicionario referente, de forma que o B1_OPC, OBrigatoriamente seja visual
VerB1_OPC()

//inclusao dinamica do botao para poder selecionar os opcionais, no minimo quando houver estrutura.
SG1->(DbSetOrder(1))
If SG1->(DbSeek(xFilial('SG1')+SB1->B1_COD))
	AAdd(aButtons,{'PRODUTO',{|| A010SeleOpc() },STR0029 }) //"Opcionais Default"
EndIf
//inclusao automatica de botoes atraves de ponto de entrada

If ExistTemplate( "MA010BUT" )
	If Valtype( aUsrBut := ExecTemplate( "MA010BUT", .f., .f. ) ) == "A"
		AEval( aUsrBut, { |x| AAdd( aButtons, x ) } )
	EndIF
EndIf

If ExistBlock( "MA010BUT" )
	If Valtype( aUsrBut := Execblock( "MA010BUT", .f., .f. ) ) == "A"
		AEval( aUsrBut, { |x| AAdd( aButtons, x ) } )
	EndIF
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Salva dados antes da alteracao.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCmps :=  RetCmps("SB1",bCampoSB1)	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Integracao PIMS GRAOS                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lPIMSINT .And. !lUsaMsg
	// Tratamento para adicinar o campo BM_CLASGRU no layout do arquivo XML 
	SX3->(DbSetOrder(2))
   	SX3->(DbSeek("BM_CLASGRU"))
	AADD(aCampos, {SX3->(X3_TITULO), SX3->(X3_DESCRIC), SX3->(X3_CAMPO), SX3->(X3_TIPO), SX3->(X3_TAMANHO), SX3->(X3_DECIMAL),NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL})
EndIf

PERGUNTE("MTA010",.F.)

A010Int( 1, nOpc, aIntSB1 )

While .T.
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia para processamento dos Gets          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nOpcA:=0

	Begin Transaction
		If ( l010Auto )
			nOpcA := AxAltera( cAlias, nReg, nOpc,,,,,"A010TUDOOK(nOpc)",,, aButtons,, aRotAuto )
		Else
			If MV_PAR02 == 1
				SB5->(DbSetOrder(1))
				If SB5->(DbSeek(xFilial("SB5")+SB1->B1_COD))
					nOpcA := A010AltPeC(cAlias,nReg,nOpc,,,"A010TUDOOK(nOpc)", ,aButtons)
				Else
					lTemSB5 := .F.
				EndIf
			Else
				nOpcA := AxAltera( cAlias, nReg, nOpc,,,,,"A010TUDOOK(nOpc)",,,aButtons)
			EndIf
		EndIf
		
		dbSelectArea(cAlias)

		If nOpcA == 1

			A010Int( 2, nOpc, aIntSB1 )
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atualiza a tabela QE6 - Especificacao de Produtos                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				QAtuB12QE6()	//SIGAQIE

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atualiza a tabela QP6 - Especificacao de Produtos                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
				QAtuB12QP6()	//SIGAQIP			
	

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Faz as Consistencias entre o F.D. do Canal com o SA5.                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If B1_MONO=="S"
				A010FDC()
			Else
				A010AtuDesc()
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Pontos de entrada depois da confirma‡ao da altera‡ao.                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ExistTemplate("MT010ALT")
				ExecTemplate("MT010ALT",.f.,.f.)
			Endif

			If ExistBlock("MT010ALT")
				ExecBlock("MT010ALT",.f.,.f.)
			Endif
			/*  GRAVACAO DO B1_CODITE QUANDO INTEGRADO COM CONCESSIONARIAS */
			If SuperGetMV("MV_VEICULO",.F.,"N")=="S" .and. Empty(SB1->B1_CODITE)
				Reclock("SB1",.F.)
				SB1->B1_CODITE := SB1->B1_COD
				MSUnlock()
			Endif
			/*--------------------*/

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Gravar o historico das alteracoes realizadas pelo usuario    ³
			//³na tabela AIF usando o vetor aCpoAltSB1 que foi carregado na ³
			//³funcao A010TdOk().                                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lHistTab .And. Len(aCpoAltSB1) > 0
				dDataAlt := Date()
				cHoraAlt := Time()
				For nX := 1 To Len(aCpoAltSB1)
					MSGrvHist(cFilialAIF,;			// Filial de AIF
					          cFilialSB1,;			// Filial da tabela SB1
					          "SB1",;				// Tabela SB1
					          "",;					// Codigo do cliente
					          "",;					// Loja do cliente
					          aCpoAltSB1[nX][1],;	// Campo alterado
					          aCpoAltSB1[nX][2],;	// Conteudo antes da alteracao
					          dDataAlt,;			// Data da alteracao
					          cHoraAlt,;			// Hora da alteracao
					          SB1->B1_COD)			// Codigo do produto
				Next nX
				aCpoAltSB1 := {} // limpar o vetor
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Gravacao do historico das alteracoes.³
			//³                                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lHistFiscal .And. Len(aCmpoSB1) > 0			
				GrvHistFis("SB1", "SS4", aCmps) 
				aCmpoSB1 := {} // limpar o vetor				
				aCmps    := {}
			EndIf	

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Integracao PIMS GRAOS                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lPIMSINT .And. !lUsaMsg
		    	SBM->(DbSeek(xFilial("SBM")+SB1->B1_GRUPO))
		    	PIMSGeraXML("Item",STR0047,"2","SB1",aCampos) //--## Cadastro de produtos
			EndIf
			
			If FindFunction ("LJ110AltOk")
				LJ110AltOk()
			EndIf
	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Tratamento para a chamada de função IntegDef() ³
			//Se a funcao  GETROTINTEG() existir no RPO, significa que a EnchoiceBar³
			//nao chama a IntegDef, logo deve ser chamada manualmente             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
			If lIntegDef						
				FwIntegDef("MATA010")  
			EndIf		
		Endif
	End Transaction
	Exit
End  

If (ExistBlock("MT010CAN"))
	ExecBlock("MT010CAN",.F.,.F.,{nOpcA})
EndIf
If !lTemSB5
	Aviso(STR0032,STR0037,{STR0031},2)//"Atencao!"###"Alteracao nao Permitida. Quando o Parametro 'Prod e Complemento ?' estiver como 'Sim', e' necessario que o produto possua complemento cadastrado ..."###"Ok"
EndIf

dbSelectArea(cAlias)
Return Nil


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010FDC   ³ Autor ³ Marcelo B. Abe        ³ Data ³ 20/04/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Alteracao de Produtos                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A010FDC (void)                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .F.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ A010INCLUI e A010ALTERA                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010FDC(void)

LOCAL lExiste  :=.F.
LOCAL nCont    := 0
LOCAL aAreaAnt := GetArea()

dbSelectArea("SA5")
dbSetOrder(2)
dbSeek(xFilial("SA5")+SB1->B1_COD)
While !Eof() .And. (A5_FILIAL+A5_PRODUTO == xFilial("SA5")+SB1->B1_COD)

	If A5_FORNECE == SB1->B1_PROC   //Se ja Existe
		lExiste:=.T.
	Else
		RecLock("SA5",.F.,.T.)           //Eliminar outros Fornecedores
		dbDelete()
		MsUnLock()
		nCont += 1
	Endif
	dbSkip()
End

If !lExiste .And. !Empty(SB1->B1_PROC)   //Caso nao Haja,crie.
	dbSelectArea("SA2")
	dbSeek(xFilial("SA2")+SB1->B1_PROC)
	dbSelectArea("SB1")
	dbSeek(xFilial("SB1")+SB1->B1_COD)
	dbSelectArea("SA5")
	RecLock("SA5",.T.)
	Replace         A5_FILIAL  With xFilial("SA5"),;
		A5_FORNECE With SB1->B1_PROC,;
		A5_LOJA    With SA2->A2_LOJA,;
		A5_NOMEFOR With SA2->A2_NOME,;
		A5_PRODUTO With SB1->B1_COD ,;
		A5_NOMPROD With SB1->B1_DESC
EndIf

RestArea(aAreaAnt)
Return(.F.)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010Preco ³ Autor ³ Marcos Bregantim      ³ Data ³ 17.05.94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validar Data de Referencia do Preco                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A010Preco()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A010Preco()

LOCAL cPer := Substr(ReadVar(),10,1), cVar
If "B1_"$ReadVar()
	cVar := "B1_DTREFP" + cPer
Elseif "B5_"$ReadVar()
	cVar := "B5_DTREFP" + cPer
Endif

If &(ReadVar()) > 0
	M->&cVar := dDataBase
Endif
Return(.T.)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010Grade ³ Autor ³ Rosane Luciane Chene  ³ Data ³ 23.10.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se o produto nao foi cadastrado como referencia   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A010Grade()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T./.F.                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010Grade()

Local aArea 
Local cProduto	  :=&(ReadVar())
Local cMascara    := SuperGetMv("MV_MASCGRD")
Local lRet		  :=.T.
Local nTamRef     := Val(Substr(cMascara,1,2))

lGrade	  := GetMV("MV_GRADE")


If lGrade
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³So salva ambiente se o parametro MV_GRADE estiver habilitado            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aArea := GetArea()

	cProduto := Substr(cProduto,1,nTamRef)

	dbSelectArea("SB4")
	dbSetOrder(1)
	If ( MsSeek(xFilial("SB4")+cProduto,.F.) .And. !Empty(cProduto) .And. ;
		 AllTrim(SubStr(cProduto,nTamRef+1)) == "" )
		Help(" ",1,"A010GRADE")
		lRet := .F.
	Endif 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Restaura a entrada da Rotina                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RestArea(aArea)
Endif

Return(lRet)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010Mod   ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 15/07/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se o centro de custos cadastrado p/ a MOD existe  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A010Mod()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010Mod()

Local cCC:=Substr(&(ReadVar()),4,9)
Local cAliasCC := If(CtbInUse(), "CTT", "SI3")
Local lRet	   := .T.
dbSelectArea(cAliasCC)
If !(dbSeek(xFilial(cAliasCC)+cCC))
	Help(" ",1,"A010MOD")
	lRet := .F.
Endif
Return(lRet)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010Contr ³ Autor ³ Cristina Ogura        ³ Data ³ 18/07/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica a existencia de contratos para o produto           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A010Contr()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA010                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010Contr()

Local cSavAlias := Alias()
Local nSavOrd
Local lRet		:= .T.

dbSelectArea("SC3")
nSavOrd := IndexOrd()
dbSetOrder(3)

If Altera .And. !(cPaisLoc $ "ARG|POR|EUA") .And. M->B1_CONTRAT == "N"
	If dbSeek(xFilial("SC3")+M->B1_COD)
		While !Eof() .And. SC3->C3_FILIAL+SC3->C3_PRODUTO == xFilial("SC3")+M->B1_COD
			If SC3->C3_ENCER != "E"
				Help(" ",1,"A010AE")
				lRet := .F.
				Exit
			EndIf
			dbSkip()
		EndDo
	EndIf
EndIf

dbSelectArea("SC3")
dbSetOrder(nSavOrd)
dbSelectArea(cSavAlias)

Return(lRet)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010Import³ Autor ³ MICROSIGA/AVERAGE     ³ Data ³ 20/08/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Consiste campos relativos a Importacao (SIGAEIC)            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A010Import()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA010                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010Import()

Local lRet:=.T.

If M->B1_IMPORT == "S" .And. Empty(M->B1_UNI)
	Help("",1,"A010UNI")
	lRet:=.F.
Endif
Return(lRet)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010TudoOk³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 17/07/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica se o registro nao foi incluido por outra estacao   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A010TudoOk(ExpN1)		                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 - Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA010                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010TudoOk(nOpc)

Local lRet      := .T.
Local l010TOkT  := ExistTemplate("A010TOK")
Local l010TOk   := Existblock("A010TOK")
Local nX        := 0
Local nY  		 := 0
Local lHistTab  := SuperGetMV("MV_HISTTAB", .F., .F.)
Local bCampoSB1 := { |x| SB1->(Field(x)) }
Local aArea     := GetArea()
//Local lIntSFC 	:= FindFunction('IntegraSFC') .And. IntegraSFC() .And. !IsInCallStack("AUTO010")
Local lIntSFC  	:= FindFunction("ExisteSFC") .And. ExisteSFC("SB1") .And. !IsInCallStack("AUTO010")
Local lIntDPR 	:= FindFunction('IntegraDPR') .And. IntegraDPR() .And. !IsInCallStack("AUTO010")// Determina se existe integracao com o DPR
Local lDAmarCt	:= SuperGetMV("MV_DAMARCT",.F.,.F.)
Local nOpcSFC	:= If(lCopia,3,nOpc) //Quando copia do registro, muda operação para inclusão 

lRet:=ExistChav("SB1",M->B1_COD)
If lRet
	If l010TOkT
		lRet:= ExecTemplate("A010TOK",.F.,.F.)
		If ValType(lRet) # "L"
			lRet :=.T.
		EndIf
	EndIf

	If lRet
		If l010TOk
			lRet:= Execblock("A010TOK",.F.,.F.)
			If ValType(lRet) # "L"
				lRet :=.T.
			EndIf
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Carregar o vetor aCpoAltSB1 caso o campo B1_DESC tenha  ³
	//³sido alterado. Somente sera gravado na tabela AIF apos  ³
	//³a validacao completa da rotina.                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	 
	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿	
	//³Alterado por Patricia Rajao em 24/05/2010               ³ 
	//³Carregar o vetor aCpoAltSB1 caso os campos: B1_DESC,    ³ 	
	//³B1_PICMENT e B1_PICMRET tenham sido alterados.          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet .And. !Inclui .And. lHistTab .And. FindFunction("MSGrvHist")
		aCpoAltSB1 := {}   
		DbSelectArea("SB1")
		For nX := 1 to FCount()
			IF (eVal( bCampoSB1, nX) == "B1_DESC") .OR. (eVal( bCampoSB1, nX) == "B1_PICMENT") .OR. (eVal( bCampoSB1, nX) == "B1_PICMRET")
				If !(M->&( eVal( bCampoSB1, nX) ) == &( eVal( bCampoSB1, nX) ))
					aAdd( aCpoAltSB1, { eVal( bCampoSB1, nX), &( eVal( bCampoSB1, nX) ) } )
				EndIf
			EndIf
		Next nX
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Valida o campo B1_LOCALIZ   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
	If lRet .And. M->B1_LOCALIZ <> "S" .And. M->B1_LOCALIZ <> "N"
		Help(" ",1,"B1_LOCALIZ")
		lRet := .F.
	EndIf
		 
	If lRet
		lRet := AvalLocali(M->B1_COD,.F.)
	Endif
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gravacao do log de produtos ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	If lRet .And. ALTERA .And. FindFunction("MATA013")
		A013GrvLog("SB1",M->B1_COD)
	Endif
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Chama rotina para integracao com SFC(Chao de Fabrica) ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. (lIntSFC .Or. lIntDPR)
	lRet := A010IntSFC(nOpcSFC)	
EndIf

//Consiste amarração da Conta Contábil X Centro de Custo
If lRet .And. lDAmarCt
	If !Empty(M->B1_CONTA) .And. !Empty(M->B1_CC) .And. (!CtbAmarra(M->B1_CONTA,M->B1_CC,M->B1_ITEMCC,M->B1_CLVL))
	   lRet:=.F.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
//³Carrega array, caso algum campo tenha sido alterado.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
If lRet .And. !Inclui .And. lHistFiscal
	aCmpoSB1 := {}   
	DbSelectArea("SB1")
	For nY := 1 to FCount()
		If !(M->&( eVal( bCampoSB1, nY) ) == &( eVal( bCampoSB1, nY) ))
			aAdd( aCmpoSB1, { eVal( bCampoSB1, nY), &( eVal( bCampoSB1, nY) ) } )
		EndIf
	Next nY
	
	If  SB1->(FieldPos("B1_IDHIST"))>0	.And. Len(aCmpoSB1)>0 .And. lHistFiscal
		M->B1_IDHIST := IdHistFis()
	EndIf
EndIf	

//-- MIL - CODITE - Validação para não permitir mesmo Grupo+Codite quando utilizado o Módulo de Concessionárias
If lRet .And. SuperGetMV("MV_VEICULO",.F.,"N") == "S" .And. !Empty(M->B1_CODITE)
   lRet := ExistChav("SB1",M->B1_GRUPO+M->B1_CODITE,7)
EndIf

RestArea(aArea)
Return(lRet)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AVALF4   ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 01/12/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada da funcao F4                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum 	                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MT010F4()
Local cFilBkp := cFilAnt
Set Key VK_F4 TO
If FWModeAccess("SB1")=="E"
	cFilAnt := SB1->B1_FILIAL
EndIf	
MaViewSB2(SB1->B1_COD)
cFilAnt := cFilBkp
Set Key VK_F4 TO MT010F4()
Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MTA010OK ³ Autor ³ AVERAGE               ³ Data ³ 28/07/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se existe Solic. de Importacao               	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                   		  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Mta010ok()

Local lRet := .t.
Local cArea:= ALIAS()

If nModulo == 17	// SIGAEIC
	dbSelectArea("SW1")
	dbSetOrder(3)
	If dbSeek(xFilial("SW1")+SB1->B1_COD)
		MsgInfo(STR0014) //"Produto possui Solicitacao de Importacao em andamento"
		lRet:=.F.
	else
		lRet:=.T.
	endif
	dbSetOrder(1)
	dbSelectArea(cArea)
Endif
Return(lRet)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MTA010E  ³ Autor ³ AVERAGE               ³ Data ³ 28/07/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava campo memo     do EIC                          	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum 	                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                   		  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Mta010e()

If nModulo == 17			//SIGAEIC
	MSMM(SB1->B1_DESC_I,,,,2)
	MSMM(SB1->B1_DESC_GI,,,,2)
	MSMM(SB1->B1_DESC_P,,,,2)
Endif
Return Nil


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A010Copia  ³ Autor ³ Larson Zordan         ³ Data ³ 17.07.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Ativa o sem foro informado que, na inclus„o, ser  usado      ³±±
±±³          ³ o produto corrente como modelo para o produto novo.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum               	                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.     	                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAEST                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function A010Copia()

Local aArea   	:= { Alias(), IndexOrd(), Recno() }
Local nPosSB1 	:= aArea[3]
Local aRots 	:= AClone(aRotina)
Local lContinua	:= .T.
Local lRetPE    := .T.

aRotina := { { OemtoAnsi(STR0015) ,"A010Copia" , 0, 3, 0, .F. }}           //"Copia"

If xFilial("SB1") # SB1->B1_FILIAL .or. (SB1->(EOF()) .and. SB1->(BOF()))
	HELP(" ",1,"ARQVAZIO")
	lContinua := .F.
Endif

If lContinua
	lCopia := .T.
	If (ExistBlock("MT010VLD"))
		lRetPE:=ExecBlock("MT010VLD",.F.,.F.) 
		If VALTYPE(lRetPE)<>"L"
			lRetPE := .T.
		EndIF
	EndIf       
	
	If lRetPE
   		A010Inclui("SB1",nPosSB1,1)
	EndIf

	dbSelectArea( aArea[1] )
	dbSetOrder( aArea[2] )
	lCopia := .F.
	aRotina := AClone(aRots)

EndIf

Return(.T.)


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A010LeReg  ³ Autor ³ Larson Zordan         ³ Data ³ 17.07.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Duplica um registro usando como base o registro posicionado  ³±±
±±³          ³ pelo MBrowse. Ser  chamada pela inclus„o quando o sem foro   ³±±
±±³          ³ lCopia estiver ligado na rotina de inclus„o.                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                    	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum 	                                                 	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAEST                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function A010LeReg()

Local aCpoNot   := {}
Local bCampo := { |nCPO| Field(nCPO) }
Local nScan  := 0
Local cCodMem:= ""
Local cMemo  := ""
Local i      := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto que Indica os campos que nao devem ser copiados    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "MTA010NC" )
	If Valtype( aCpoNot := ExecBlock( "MTA010NC", .F., .F. ) ) <> "A"
		aCpoNot   := {}
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Copia os campos do produto de referˆncia para a mem¢ria. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB1")
FOR i := 1 TO FCount()
	If !(FieldName( i ) == "B1_COD" .or. FieldName( i ) == "B1_CODITE") .And. Empty( AScan( aCpoNot,{|x| x == Upper(allTrim(FieldName( I )))} ) )

		If Empty( nScan := AScan( aMemos, { |x| x[1] == FieldName( i ) } ) )
			M->&(EVAL(bCampo,i)) := FieldGet(i)
		Else
			cCodMem := FieldGet(i)
			cMemo   := MSMM( cCodMem )
			M->&(aMemos[nScan,2]) := cMemo
		EndIf
	EndIf
NEXT i

Return Nil


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A010Potenci³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 31.07.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida potencia padrao digitada                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                    	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                 	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAEST                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function A010Potenci()

LOCAL lRet := .T.
If !Empty(&(ReadVar()))
	HELP(" ",1,"MA010NAOP")
	lRet:=.F.
EndIf
Return(lRet)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010LotQlt³ Autor ³Paulo Emidio de Barros ³ Data ³29/08/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Sugere o tipo de Rastreaabilidade, quando o produto estiver³±±
±±³ 		 ³ com opcao de envio para Inspecao no Quality				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a010LotQlt()

Local lRet := .T.
Return(lRet)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010AtuDesc³ Autor ³ Henry Fila           ³ Data ³ 23/09/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Atualizacao da descricao dos produtos no SA5   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A010AtuDesc()                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ A010INCLUI e A010ALTERA                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010AtuDesc()

Local aArea    := GetArea()
Local aAreaSB1 := SB1->(GetArea())
Local aAreaSA5 := SA5->(GetArea())

If SA5->(FieldPos("A5_NOMPROD")) > 0

	PERGUNTE("MTA010",.F.) // para restaurar as perguntas da rotina
	SA5->(dbSetOrder(2))
	SA5->(MsSeek(xFilial("SA5")+SB1->B1_COD))
	While SA5->(!Eof()) .And. (SA5->A5_FILIAL+SA5->A5_PRODUTO == xFilial("SA5")+SB1->B1_COD)
		If mv_par01 == 1
			RecLock("SA5",.F.)           //Eliminar outros Fornecedores
			SA5->A5_NOMPROD := SB1->B1_DESC
			MsUnLock()
		EndIf
		SA5->(dbSkip())
	EndDo
EndIf
RestArea(aAreaSB1)
RestArea(aAreaSA5)
RestArea(aArea)
Return Nil


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MT010Perg ³ Autor ³                      ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carrega o 'pergunte' para o grupo do programa              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ MT010Perg()  	                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010               	 	                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MT010Perg()

Pergunte("MTA010",.T.)
Return Nil


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010Wizard³ Autor ³Rodrigo de A Sartorio  ³ Data ³ 06/10/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Assistente para preenchimento de informacoes de logistica   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A010Wizard(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010()                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010Wizard(cAlias,nReg,nOpc)

LOCAL oWizard,oEncSB1,oEncSB5
LOCAL oBmpSB1,oBmpSB5,oBmpDC3
PRIVATE Altera:=.F.,Inclui:=.T.
PRIVATE nOpcx:=nOpc

DEFINE WIZARD oWizard TITLE OemToAnsi(STR0008) HEADER OemToAnsi(STR0021) MESSAGE " " TEXT OemtoAnsi(STR0022) PANEL NEXT {|| .T.} FINISH {|| .T.}
// Painel 2
CREATE PANEL oWizard HEADER OemToAnsi(STR0023) MESSAGE OemToAnsi(STR0024)+" SB1" PANEL BACK {|| .F.} NEXT {|| A010VLSB1(oEncSB1)  } FINISH {|| .T.} EXEC {|| .T.}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Enchoice  Cadastro de produtos             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB1")
dbSetOrder(1)
RegToMemory("SB1",.T.)
oEncSB1:=Msmget():New("SB1",nReg,nOpc,,,,,{0,0,140,290},,,,,,oWizard:oMPanel[2])

oEncSB1:Refresh()
// Painel 3
CREATE PANEL oWizard HEADER OemToAnsi(STR0025) MESSAGE OemToAnsi(STR0024)+" SB5" PANEL BACK {||.F.} NEXT {|| A010VLSB5(oEncSB5,oBmpDC3) } FINISH {|| .T.} EXEC {|| .T.}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Enchoice Cadastro de dados complementares  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB5")
dbSetOrder(1)
RegToMemory("SB5",.T.)
oEncSB5:=Msmget():New("SB5",SB5->(RECNO()),nOpc,,,,,{0,0,140,290},,,,,,oWizard:oMPanel[3])
oEncSB5:Refresh()
// Painel 4
CREATE PANEL oWizard HEADER OemToAnsi(STR0027) MESSAGE OemToAnsi(STR0028) PANEL BACK {|| .F.} NEXT {|| .T. } FINISH {|| .T.} EXEC {|| .T.}
// Definicao do objeto SAY para descricao do processo efetuado
@ 04,001 BITMAP oBmpSB1 RESNAME "CHECKED" Size 16,16 NOBORDER OF oWizard:oMPanel[4] Pixel
@ 07,016 Say OemToAnsi(STR0023) Size 100,8 OF oWizard:oMPanel[4] Pixel
@ 24,001 BITMAP oBmpSB5 RESNAME "CHECKED" Size 16,16 NOBORDER OF oWizard:oMPanel[4] Pixel
@ 27,016 Say OemToAnsi(STR0025) Size 100,8 OF oWizard:oMPanel[4] Pixel
@ 44,001 BITMAP oBmpDC3 RESNAME "CHECKED" Size 16,16 NOBORDER OF oWizard:oMPanel[4] Pixel
@ 47,016 Say OemToAnsi(STR0026) Size 100,8 OF oWizard:oMPanel[4] Pixel
ACTIVATE WIZARD oWizard CENTERED VALID {|| .T. }
Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010VLSB1 ³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 06/10/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a gravacao do registro no SB1 e grava o registro    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := A010VLSB1(ExpO1)			                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 =  Objeto que contem a enchoice no SB1               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 = .T. (valida p/gravar) ou .T. (nao valida gravacao) ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010VLSB1(oEncSB1)

Local lGrava:=Obrigatorio(oEncSB1:aGets,oEncSB1:aTela) .And. A010Tudook()
LOCAL bCampo:= {|nCPO| Field(nCPO) }
Local ix
If lGrava
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cria o registro do produto                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Begin Transaction
		RecLock("SB1",.T.)
		For ix := 1 TO FCount()
			FieldPut(ix,M->&(EVAL(bCampo,ix)))
		Next ix
		Replace B1_FILIAL With xFilial("SB1")
		MsUnlock()
	End Transaction
	// Inicializa o codigo do produto no complemento de produto
	// com o conteudo do campo B1_COD
	M->B5_COD:=M->B1_COD
EndIf
RETURN(lGrava)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010VLSB5 ³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 06/10/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a gravacao do registro no SB5 e grava o registro    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := A010VLSB5(ExpO1,ExpO2)		                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 =  Objeto que contem a enchoice no SB5               ³±±
±±³          ³ ExpO2 =  Objeto do BITMAP de checagem                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 = .T. (validacao OK) ou .T. (validacao nao Ok)		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010VLSB5(oEncSB5,oBmpDC3)

Local lGrava:=Obrigatorio(oEncSB5:aGets,oEncSB5:aTela)
LOCAL bCampo:= {|nCPO| Field(nCPO) }
Local ix
If lGrava
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cria o registro do complemento do produto                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Begin Transaction
		RecLock("SB5",.T.)
		For ix := 1 TO FCount()
			FieldPut(ix,M->&(EVAL(bCampo,ix)))
		Next ix
		Replace B5_FILIAL With xFilial("SB5")
		MsUnlock()
	End Transaction
	DLA030Manut("DC3",0,3,nil,M->B1_COD,M->B1_DESC)
	DC3->(dbSetOrder(1))
	If !DC3->(dbSeek(xFilial("DC3")+M->B1_COD))
		oBmpDC3:Load("NOCHECKED")
	EndIf
EndIf
RETURN(lGrava)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010IncPeC³ Autor ³Alex Sandro Valario    ³ Data ³ 16/09/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Inclusao de produto junto com o complemento                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A010IncPeC(ExpC1,ExpN1,ExpN2,ExpC2,ExpA1,ExpC3,ExpA2,ExpL1)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 =  Alias do arquivo								  ³±±
±±³          ³ ExpN1 =  Numero do registro			                      ³±±
±±³          ³ ExpN2 =  Numero da opcao selecionada	                      ³±±
±±³          ³ ExpC2 =  Nome da funcao para duplicar o reg.na inclusao,   ³±±
±±³          ³          qdo semaforo lcopia estiver ligado.            	  ³±±
±±³          ³ ExpA1 =  Array dos campos                                  ³±±
±±³          ³ ExpC3 =  Nome da funcao para TUDOOK		                  ³±±
±±³          ³ ExpA2 =  Array dos botoes								  ³±±
±±³          ³ ExpL1 =  Qdo .F. carrega inicializ.padrao p/campos virtuais³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010IncPeC(cAlias,nReg,nOpc,cFunc,aCpos,cTudoOk,aButtons,lVirtual)

Local aArea    := GetArea(cAlias)
Local aSvRot   := Nil
Local cMemo    := ""
Local lM010B5CP:= ExistBlock("M010B5CP")
Local lB5Descri:= .T.
Local nX       := 0
Local nPosCodB5:= 1
Local nOpcA    := 0
Local bCampo   := {|nCPO| Field(nCPO) }
Local bOk      := Nil
Local bOk2     := {|| .T.}
Local bOk3     := {|| a010Refresh(oFolder)}
Local oDlg     
Local aObjects    := {}
Local aSize       := {} 
Local aInfo       := {}
Local aPosObj     := {}  

//enchoice
Local oFolder
Private oEnc01
Private oEnc02
Private aTELA:=Nil,aGets:= Nil
DEFAULT cTudoOk := ".T."
DEFAULT bOk     := &("{|| "+cTudoOk+"}")


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo							 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpc == Nil
	nOpc := 3
	If Type("aRotina") == "A"
		aSvRot := aClone(aRotina)
	EndIf
	Private aRotina := { { " "," ",0,1 } ,{ " "," ",0,2 },{ " "," ",0,3 } }
EndIf
RegToMemory(cAlias, .T., .F. )
RegToMemory("SB5",!lCopia , .F. )
If lCopia
	M->B5_COD:=Space(TamSX3("B5_COD")[1])
	M->B1_COD:=CriaVar("B1_COD")
	If lM010B5CP .And. MV_PAR02 == 1
		lB5Descri:= ExecBlock("M010B5CP",.F.,.F.)
		If ValType (lB5Descri) != "L"
			lB5Descri:= .T.
		EndIf
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa variaveis para campos Memos Virtuais (GILSON)			 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("aMemos")=="A"
	For nX :=1 To Len(aMemos)
		cMemo := aMemos[nX][2]
		If ExistIni(cMemo)
			&cMemo := InitPad(SX3->X3_RELACAO)
		Else
			&cMemo := ""
		EndIf
	Next nX
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Funcoes executadas antes da chamada da Enchoice      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cFunc != NIL
	&cFunc.()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta a largura para o tamanho padrao Protheus ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSize := MsAdvSize()       
		
aObjects := {}
AAdd( aObjects, { 100, 100, .T., .T. } )
aInfo    := { aSize[1], aSize[2], aSize[3], aSize[4], 2, 2 } 

aPosObj := MsObjSize(aInfo,aObjects,.T.)

DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL
oFolder := TFolder():New(13,,{STR0033,STR0034},{STR0033,STR0034},oDlg,,,,.t.,.F.,oDlg:nWidth,oDlg:nHeight,) //"Produto"###"Complemento"###"Produto"###"Complemento"

oFolder:bSetOption := {|nAtu| Al_MudaEnc(nAtu,oFolder:nOption,lB5Descri)}
oFolder:align:= CONTROL_ALIGN_ALLCLIENT
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Enchoice 01                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aPos:={}
dbSelectArea("SB1")
oEnc01:= MsMGet():New("SB1" ,nReg ,nOpc,,,,,aPosObj[1],aCpos,       ,        ,          ,cTudoOk ,oFolder:aDialogs[1], ,.F.)
oEnc01:oBox:align:= CONTROL_ALIGN_ALLCLIENT

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Enchoice 02                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB5")
oEnc02:=MsMGet():New("SB5",nReg,nOpc,,,, ,aPosObj[1],     ,       ,        ,          ,        ,oFolder:aDialogs[2], ,lVirtual)
nPosCodB5 := AsCan(oEnc02:aENTRYCTRLS,{|x| UPPER(TRIM(x:cReadVar))=="M->B5_COD"})
If !Empty(nPosCodB5)
	oEnc02:AENTRYCTRLS[nPosCodB5]:bWhen	:= {|| .F.}
EndIf
oEnc02:oBox:align:= CONTROL_ALIGN_ALLCLIENT
aTela := aClone(oEnc02:aTela)
aGets := aClone(oEnc02:aGets)

dbSelectArea("SB1")
ACTIVATE MSDIALOG oDlg ON INIT (EnchoiceBar(oDlg,{|| nOpcA := 1, Eval(bOk3) .And.;
	If(Obrigatorio(oEnc01:aGets,oEnc01:aTela).And.Obrigatorio(oEnc02:aGets,oEnc02:aTela) .and.;
	Eval(bOk).And.Eval(bOk2,nOpc),oDlg:End(),(nOpcA:=3,.f.))},;
	{|| nOpcA := 3,oDlg:End()},,aButtons)) CENTERED

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gravacao da enchoice                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpcA == 1
	Begin Transaction
		M->B5_COD:=M->B1_COD
		DBSelectArea(cAlias)
		RecLock(cAlias,.T.)
		For nX := 1 TO FCount()
			If "_FILIAL"$FieldName(nX)
				FieldPut(nX,xFilial(cAlias))
			Else
				FieldPut(nX,M->&(EVAL(bCampo,nX)))
			EndIf
		Next nX
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Grava os campos Memos Virtuais					 				  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Type("aMemos") == "A"
			For nX := 1 to Len(aMemos)
				cVar := aMemos[nX][2]
				MSMM(,TamSx3(aMemos[nX][2])[1],,&cVar,1,,,cAlias,aMemos[nX][1])
			Next nX
		EndIf
		If __lSX8
			ConfirmSX8()
		EndIf
		dbSelectArea("SB5")
		dbSetOrder(1)
		If dbSeek(xFilial("SB5")+M->B1_COD)
			RecLock("SB5",.F.)
		Else
			RecLock("SB5",.T.)
		EndIf
		For nX := 1 TO FCount()
			If "_FILIAL"$FieldName(nX)
				FieldPut(nX,xFilial("SB5"))
			Else
				FieldPut(nX,M->&(EVAL(bCampo,nX)))
			EndIf
		Next nX
	End Transaction  
Else
	If __lSX8
		RollBackSX8()
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade dos dados                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aSvRot != Nil
	aRotina := aClone(aSvRot)
EndIf
RestArea(aArea)
lRefresh := .T.
Return(nOpcA)


Static Function Al_MudaEnc(nAtu,oFolder,lB5Descri)
DEFAULT lB5Descri:= .T.

If nAtu== 1
	dbSelectArea("SB1")
	oEnc01:oBox:align:= CONTROL_ALIGN_ALLCLIENT
	oEnc01:Refresh()
Else
	dbSelectArea("SB5")
	M->B5_COD:=M->B1_COD 
	If lB5Descri
		M->B5_CEME:=If(Empty(M->B5_CEME),PADR(M->B1_DESC,Len(SB5->B5_CEME)),PADR(M->B5_CEME,Len(SB5->B5_CEME)))
	EndIf
	oEnc02:oBox:align:= CONTROL_ALIGN_ALLCLIENT
	oEnc02:Refresh()
EndIf	
Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³A010AltPeC³ Autor ³Alex Sandro Valario    ³ Data ³ 16/09/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Alteracao de produto junto com o complemento               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpN1 := A010IncPeC(ExpC1,ExpN2,ExpN3,ExpA1,ExpA2,ExpC2,	  ³±±
±±³          ³          ExpC3,ExpA3)				                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 =  Alias do arquivo								  ³±±
±±³          ³ ExpN2 =  Numero do registro			                      ³±±
±±³          ³ ExpN3 =  Numero da opcao selecionada	                      ³±±
±±³          ³ ExpA1 =  nao utilizado									  ³±±
±±³          ³ ExpA2 =  Array dos campos                                  ³±±
±±³          ³ ExpC2 =  Nome da funcao para TUDOOK		                  ³±±
±±³          ³ ExpC3 =  Nome da funcao para duplicar o reg.na inclusao,   ³±±
±±³          ³          qdo semaforo lcopia estiver ligado.            	  ³±±
±±³          ³ ExpA3 =  Array dos botoes								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpN1 = nOpca											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010AltPeC(cAlias,nReg,nOpc,aAcho,aCpos,cTudoOk,cFunc,aButtons)

Local aArea    := GetArea(cAlias)
Local bCampo   := {|nCPO| Field(nCPO) }
Local bOk      := Nil
Local bOk2     := {|| .T.}
Local cCpoFil  := PrefixoCpo(cAlias)+"_FILIAL"
Local cMemo    := ""
Local nOpcA    := 0
Local nX       := 0
Local nPosInsPat:= 0
Local nPosCodAtv:= 0
Local oDlg
Local nTop
Local nLeft
Local nBottom
Local nRight
Local cAliasMemo  
Local aObjects    := {}
Local aSize       := {} 
Local aInfo       := {}
Local aPosObj     := {}  
Local nG

//enchoice
Local oFolder
Private oEnc01
Private oEnc02
Private aTELA:= NIL,aGets:=NIL

DEFAULT cTudoOk := ".T."
DEFAULT nReg    := (cAlias)->(RecNO())
DEFAULT bOk 	 := &("{|| "+cTudoOk+"}")   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta a largura para o tamanho padrao Protheus ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSize := MsAdvSize()       
		
aObjects := {}
AAdd( aObjects, { 100, 100, .T., .T. } )
aInfo    := { aSize[1], aSize[2], aSize[3], aSize[4], 2, 2 } 

aPosObj := MsObjSize(aInfo,aObjects,.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³VerIfica se esta' alterando um registro da mesma filial               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SB5")
DbSelectArea(cAlias)
If (cAlias)->(FieldPos(cCpoFil))==0 .Or. (cAlias)->(FieldGet(FieldPos(cCpoFil))) == xFilial(cAlias)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta a entrada de dados do arquivo						     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SoftLock(cAlias) .AND.SoftLock("SB5")
		RegToMemory(cAlias,.F.,.F.)
		RegToMemory("SB5",.F. ,.F. )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa variaveis para campos Memos Virtuais		 			  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Type("aMemos")=="A"
			For nX:=1 to Len(aMemos)
				cMemo := aMemos[nX][2]
				If ExistIni(cMemo)
					&cMemo := InitPad(SX3->X3_RELACAO)
				Else
					&cMemo := ""
				EndIf
			Next nX
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa variaveis para campos Memos Virtuais		 			  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( ValType( cFunc ) == 'C' )
			If ( !("(" $ cFunc) )
				cFunc+= "()"
			EndIf
			&cFunc
		EndIf
		
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL
		oFolder := TFolder():New(13,,{STR0033,STR0034},{STR0033,STR0034},oDlg,,,,.t.,.F.,oDlg:nWidth,oDlg:nHeight,) //"Produto"###"Complemento"###"Produto"###"Complemento"

		oFolder:bSetOption := {|nAtu| Al_MudaEnc(nAtu,oFolder:nOption)}
		oFolder:align:= CONTROL_ALIGN_ALLCLIENT
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Enchoice 01                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aPos:={}
		dbSelectArea("SB1")
		oEnc01:= MsMGet():New("SB1" ,nReg ,nOpc,,,,,aPosObj[1],aCpos,       ,        ,          ,cTudoOk ,oFolder:aDialogs[1], ,)
		oEnc01:oBox:align:= CONTROL_ALIGN_ALLCLIENT

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Enchoice 02                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SB5")
		oEnc02:=MsMGet():New("SB5",nReg,nOpc,,,,,aPosObj[1],     ,       ,        ,          ,        ,oFolder:aDialogs[2], ,)
		nPosCodB5 := AsCan(oEnc02:aENTRYCTRLS,{|x| UPPER(TRIM(x:cReadVar))=="M->B5_COD"})
		nPosInsPat 	:= AsCan(oEnc02:aENTRYCTRLS,{|x| UPPER(TRIM(x:cReadVar))=="M->B5_INSPAT"})
		nPosCodAtv 	:= AsCan(oEnc02:aENTRYCTRLS,{|x| UPPER(TRIM(x:cReadVar))=="M->B5_CODATIV"})
		If !Empty(nPosCodB5)
			oEnc02:AENTRYCTRLS[nPosCodB5]:bWhen	:= {|| .F.}
		EndIf                                              
		If nPosInsPat >0 .And. nPosCodAtv >0 
			oEnc02:AENTRYCTRLS[nPosInsPat]:bWhen	:= {|| .T.}
			oEnc02:AENTRYCTRLS[nPosCodAtv]:bWhen	:= {|| .T.}
		EndIf
		oEnc02:oBox:align:= CONTROL_ALIGN_ALLCLIENT
		aTela := aClone(oEnc02:aTela)
		aGets := aClone(oEnc01:aGets)
		For nG := 1 TO Len(oEnc02:aGets)
			aADD(aGets,oEnc02:aGets[nG])
		Next nG

		ACTIVATE MSDIALOG oDlg ON INIT (EnchoiceBar(oDlg,{|| nOpcA := 1,;
			If(Obrigatorio(oEnc01:aGets,oEnc01:aTela).And.Obrigatorio(oEnc02:aGets,oEnc02:aTela) .and.;
			Eval(bOk).And.Eval(bOk2,nOpc),oDlg:End(),(nOpcA:=3,.f.))},;
			{|| nOpcA := 3,oDlg:End()},,aButtons,nReg,cAlias))

		(cAlias)->(MsGoTo(nReg))
		If nOpcA == 1
			Begin Transaction
				RecLock(cAlias,.F.)
				For nX := 1 TO FCount()
					FieldPut(nX,M->&(EVAL(bCampo,nX)))
				Next nX
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Grava os campos Memos Virtuais					  				  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If Type("aMemos") == "A"
					For nX := 1 to Len(aMemos)
						cVar := aMemos[nX][2]
						cVar1:= aMemos[nX][1]
						//Incluído parametro com o nome da tabela de memos => para módulo APT
						cAliasMemo := If(len(aMemos[nX]) == 3,aMemos[nX][3],Nil)
						MSMM(&cVar1,TamSx3(aMemos[nX][2])[1],,&cVar,1,,,cAlias,aMemos[nX][1],cAliasMemo)
					Next nX
				EndIf

				DBSelectArea("SB5")
				RecLock("SB5",SB5->(Eof()))
				For nX := 1 TO FCount()
					If SB5->(FieldName(nX)) =="B5_FILIAL"
						SB5->B5_FILIAL:= xFilial("SB5")
					Else
						FieldPut(nX,M->&(EVAL(bCampo,nX)))
					EndIf
				Next nX

			End Transaction  			
		EndIf
	Else
		nOpcA := 3
	EndIf
Else
	Help(" ",1,"A000FI")
	nOpcA := 3
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade dos dados                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MsUnLockAll()
RestArea(aArea)
Return(nOpcA)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010Consul ³ Autor ³Alexandre Inacio Lemes³ Data ³31/03/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Excuta a chamada da funcao MC050con para consulta do produto³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum 	                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010Consul()

Local aArea    := GetArea()

SetKey( VK_F12, { || pergunte("MTC050",.T.) } )
pergunte("MTC050",.F.)
MC050Con()
Set Key VK_F4 TO MT010F4()

SetKey( VK_F12, { || pergunte("MTA010",.T.) } )
Pergunte("MTA010",.F.)

RestArea(aArea)
Return Nil


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A010SeleOpc³ Autor ³Alex Sandro Valario   ³ Data ³04/05/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao p/ selecao de Opcionais                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum 	                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A010SeleOpc()

Local cAnt:=M->B1_OPC
Local cRet:=""
Private aRetorOpc:={}
If ! SeleOpc(1,"MATA010",SB1->B1_COD,Nil,@cRet)
	M->B1_OPC:=cAnt
Else
	M->B1_OPC:=cRet
EndIf
Return Nil


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³VerB1_OPC  ³ Autor ³Alex Sandro Valario   ³ Data ³04/05/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao que acerta o x3_visual, obrigatorio se visual        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum 	                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function VerB1_OPC()

Local aArea := SX3->(GetArea())
SX3->(DbSetOrder(2))
If SX3->(DbSeek("B1_OPC")) .and. SX3->X3_VISUAL <> "V"
	SX3->(RecLock("SX3",.F.))
	SX3->X3_VISUAL:="V"
	SX3->(MsUnLock())
EndIf
SX3->(RestArea(aArea))
Return Nil


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a010Refresh³ Autor ³Patricia A. Salomao   ³ Data ³08/06/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao executada pela TudoOk();Carrega as Variaveis de memo ³±±
±±³          ³ria e efetua o refresh da pasta em que nao esta posicionado.³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Objeto Folder                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.    	                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a010Refresh(oFolder)
Local nPasta1 := oFolder:nOption     //-- Pasta em que esta posionado
Local nPasta2 := IIf(nPasta1==1,2,1) 
Al_MudaEnc(nPasta2) //-- Efetua a atualizacao das variaveis e Refresh da outra pasta
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Fabio Alves Silva     ³ Data ³01/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³    1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()

Local lPyme		:= Iif(Type("__lPyme") <> "U",__lPyme,.F.)
Local aRotAdic

PRIVATE aRotina	:= {}
Private lIntLox := GetMv("MV_QALOGIX") == "1"

If !lIntLox

	aRotina	:= { 	{ OemToAnsi(STR0003) ,"AxPesqui"		, 0 , 1, 0, .F.}}		//"Pesquisar"

	If IsInCallStack("FATA320") .AND. FindFunction("FT060Permi")
		aPermissoes := FT060Permi(__cUserId, "ACA_ACPROD")
	Else
		aPermissoes := {.T.,.T.,.T.,.T.}
	EndIf 
	If aPermissoes[4] 
		aAdd(aRotina, { OemtoAnsi(STR0004) ,"A010Visul"	  		, 0 , 2, 0, nil} )	//"Visualizar"
	EndIf
	If aPermissoes[1]
		aAdd(aRotina, { OemtoAnsi(STR0005) ,"A010Inclui"		, 0 , 3, 0, nil} )	//"Incluir"
	EndIf
	If aPermissoes[2]	
		aAdd(aRotina, { OemtoAnsi(STR0006) ,"A010Altera"		, 0 , 4, 2, nil} )	//"Alterar"
	EndIf
	If aPermissoes[3]					
		aAdd(aRotina, { OemtoAnsi(STR0007) ,"Mata010Deleta"	, 0 , 5, 1, nil} )		//"Excluir"
	EndIf
	aAdd(aRotina, { OemtoAnsi(STR0017) ,"A010Consul"		, 0 , 2, 0, .F.} )		//"Consulta"
	aAdd(aRotina, { OemtoAnsi(STR0015) ,"A010Copia"		, 0 , 4, 0, nil} )			//"Copia"	
Else
	aRotina	:= { 	{ OemToAnsi(STR0003) ,"AxPesqui"		, 0 , 1, 0, .F.},;		//"Pesquisar"
		{ OemtoAnsi(STR0004) ,"A010Visul"		, 0 , 2, 0, nil},;				//"Visualizar"
		{ OemtoAnsi(STR0017) ,"A010Consul"		, 0 , 2, 0, .F.}}		      //"Consulta"
	l010Auto  := If (Type("l010Auto") =="U",.T.,l010Auto)
	If l010Auto //Adicionar caso seja rotina automatica - integracao com o Logix
		aAdd(aRotina, { OemtoAnsi(STR0005) ,"A010Inclui"		, 0 , 3, 0, nil} )	//"Incluir"		
		aAdd(aRotina, { OemtoAnsi(STR0006) ,"A010Altera"		, 0 , 4, 2, nil} )	//"Alterar"
		aAdd(aRotina, { OemtoAnsi(STR0007) ,"Mata010Deleta"	, 0 , 5, 1, nil} )		//"Excluir"				
	Endif	
Endif
aAdd(aRotina,{OemtoAnsi(STR0040), "A010WizFac", 0, 4, 2, .F.})	//"Facilitador"
If nModulo == 4 .Or. nModulo == 10 .Or. nModulo == 28
	aAdd(aRotina, { OemToAnsi(STR0042) ,"A010ProdAl", 0 , 4, 0, .F.} )			//"Alte&rnativos"	
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada utilizado para inserir novas opcoes no array aRotina  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MTA010MNU")
	ExecBlock("MTA010MNU",.F.,.F.)
EndIf

If IntDl()
	AAdd( aRotina, { OemtoAnsi(STR0020) ,"A010Wizard", 0 , 3}) // "Assis&tente WMS" 
EndIf

// Banco de conhecimento para Série 3 e Série T
AAdd( aRotina, { STR0018, "MsDocument", 0, 4 } )  //"Conhecimento"
       
// Trade-Easy -  05/05/2011                                                                 //RRC - 03/09/2013
                                 // SIGAEIC         // SIGAEDC          // SIGAEEC          //SIGAESS
If FindFunction("EYJCOMP") .And. (nModulo == 17 .Or. nModulo == 50  .Or. nModulo == 29 .Or. nModulo == 85 .Or. (FindFunction("AvFlags") .And. (AvFlags("CONTROLE_SERVICOS_AQUISICAO") .Or. AvFlags("CONTROLE_SERVICOS_VENDA")))) 
   AAdd( aRotina, { "Dados Comp. Prod.", "EYJCOMP", 0, 4 } )  //"Dados Comp. Prod." //FSM - 15/10/2012
EndIf
If lHistFiscal
	Aadd(aRotina, { OemtoAnsi(STR0066),  "A010Hist" , 0 ,0,0, NIL })//"Histórico
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ P.E. utilizado para adicionar items no Menu da mBrowse       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MT010BRW")
	aRotAdic := ExecBlock("MT010BRW",.f.,.f.)
	If ValType(aRotAdic) == "A"
		AEval(aRotAdic,{|x| AAdd(aRotina,x)})
	EndIf
EndIf

If nModulo == 73 .AND. FindFunction("CRMXAcesso") 
	aRotina := CRMXAcesso("MATA010",aRotina)
EndIf

Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³A010WizFac³ Autor ³ Gustavo G. Rueda      ³ Data ³27/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada da rotina que permite uma manutencao facil no      ³±±
±±³          ³ cadastro.                                                  ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³.T.                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A010WizFac()

Local	cCmps		:=	""
Local	aPar 		:=	{}
Local	cMVA010FAC	:=	GetNewPar("MV_A010FAC","")

cCmps	:=	"B1_TIPO/B1_CODITE/B1_UM/B1_LOCPAD/B1_GRUPO/B1_PICM/B1_IPI/B1_ESPECIE/B1_ALIQISS/B1_MSBLQL/"
cCmps	+=	"B1_CODISS/B1_TE/B1_TS/B1_PICMRET/B1_PICMENT/B1_IMPZFRC/B1_SEGUM/B1_CONV/B1_TIPCONV/B1_CONTA/B1_CC/B1_ITEMCC/B1_ORIGEM/"
cCmps	+=	"B1_CLASFIS/B1_GRTRIB/B1_CONTSOC/B1_IRRF/B1_IMPORT/B1_CODOBS/B1_OBS/B1_SITPROD/B1_INSS/B1_REDINSS/B1_REDIRRF/B1_TAB_IPI/"
cCmps	+=	"B1_REDPIS/B1_REDCOF/B1_PCSLL/B1_PCOFINS/B1_PPIS/B1_VLR_IPI/B1_CLVL/B1_ATIVO/B1_VLR_ICM/B1_INT_ICM/B1_VLRSELO/B1_SELO/B1_PIS/"
cCmps	+=	"B1_CLASSE/B1_VLR_PIS/B1_COFINS/B1_CSLL/B1_FRETISS/B1_CALCFET/B1_PAUTFET/B1_VLR_COF/B1_PRFDSUL/B1_RETOPER/B1_CNAE/B1_CRDEST/"
cCmps	+=	cMVA010FAC

aAdd(aPar,{"SB1","B1_COD+' - '+B1_DESC", cCmps,""})

MATA984(aPar[1,1],aPar[1,2],aPar[1,3],,aPar[1,4])

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MATA010   ºAutor  ³Andre Anjos         º Data ³  27/02/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Valida o campo B1_ESTRORI                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA010                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010EstOri()
Local aArea := GetArea()
Local lRet := .T.

dbSelectArea("SG1")
dbSetOrder(1)
If !Empty(M->B1_ESTRORI) .And. !dbSeek(xFilial("SG1")+M->B1_ESTRORI+M->B1_COD) .Or. G1_QUANT > 0
	Help(" ",1,"REGNOIS")
	lRet := .F.
EndIf

RestArea(aArea)
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A010ProdAlºAutor  |Andre Anjos         º Data ³  19/03/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Interface para cadastramento dos produtos alternativos.     º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada 			              ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA010                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010ProdAl(cAlias,nReg,nOpc)
// Variaveis p/ processamento
Local aArea 	:= GetArea()
Local lOk 		:= .F.
Local nPosOrdem := 0
Local nX 		:= 0
Local nY 		:= 0
Local nGrOrdem 	:= 0
Local lContinua := .T.
// Variaveis p/ objetos da tela
Local aNoFields := {"GI_FILIAL","GI_PRODORI"}
Local aButtons 	:= {}
Local oSize
Local oTela, oProdOri ,	oTelaNew
Local cSeek 	:= xFilial("SGI")+SB1->B1_COD
Local bWhile 	:= {|| SGI->(GI_FILIAL+GI_PRODORI)}
Local bFor	 	:= {|| .T.}

Private aHeader := {}
Private aCols 	:= {}
Private cProdOri:= SB1->B1_COD

//Verifica atualizacao do dicionario
If !AliasInDic("SGI")
	Help(" ",1,"UPDPCP10")
	lContinua := .F.
EndIf

//Verifica se e fantasma
If RetFldProd(SB1->B1_COD,"B1_FANTASM") == "S"
	Help(" ",1,"ALTERFAN")
	lContinua := .F.
EndIf

If lContinua
	FillGetDados(4,"SGI",1,cSeek,bWhile,bFor,aNoFields)
	If Empty(GDFieldGet("GI_ORDEM",1))
		//Se nao ha alternativos prepara para inclusao
		GDFieldPut("GI_ORDEM",StrZero(1,Len(SGI->GI_ORDEM)),1)
	Else
		//Se houver alternativos, ordena pela ordem
		aSort(aCols,,,{|x,y| x[GDFieldPos("GI_ORDEM")] < y[GDFieldPos("GI_ORDEM")]})
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calcula dimensões                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSize := FwDefSize():New()             
	oSize:AddObject( "CABECALHO",  100, 05, .T., .T. ) // Totalmente dimensionavel
	oSize:AddObject( "GETDADOS" ,  100, 95, .T., .T. ) // Totalmente dimensionavel      
	        
	oSize:lProp 	:= .T. // Proporcional             
	oSize:aMargins 	:= { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3 
	
	oSize:Process() 	   // Dispara os calculos   
	
	Define MSDialog oTela Title OemToAnsi(STR0033)+Space(1)+AllTrim(cProdOri); //Produto Alternativo\
									FROM oSize:aWindSize[1],oSize:aWindSize[2] TO oSize:aWindSize[3],oSize:aWindSize[4] OF oMainWnd PIXEL
	oProdOri := tSay():New(oSize:GetDimension("CABECALHO","LININI")+3 ,oSize:GetDimension("CABECALHO","COLINI"),{|| RetTitle("GI_PRODORI")},oTela,,,,,,.T.,,,110,20)
	@ oSize:GetDimension("CABECALHO","LININI") ,oSize:GetDimension("CABECALHO","COLINI")+33 MsGet cProdOri Size 100,10 When .F. Of oTela Pixel 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ P.E. utilizado para Adicionar novos campos na getdados         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock("MT010GETD")      
		oTelaNew:=ExecBlock("MT010GETD",.f.,.f.,{oTela})	
		If ValType(oTelaNew) == "A"
			oTela := aClone(oTelaNew)
		EndIf
	EndIf
	
	oGetD := MsGetDados():New(oSize:GetDimension("GETDADOS","LININI"),oSize:GetDimension("GETDADOS","COLINI"),;
						  oSize:GetDimension("GETDADOS","LINEND"),oSize:GetDimension("GETDADOS","COLEND"),;
						  4,"AlterLinOK",/*TudoOk*/,"+GI_ORDEM",.T.,,1,,999,"A010FldOk")          
						  
	Activate MSDialog oTela On Init EnchoiceBar(oTela,{||lOk:=.T.,If(oGetD:TudoOk(),oTela:End(),lOk:=.F.)},{||oTela:End()},,aButtons)
	
	//Processa gravação se clicou no OK e tem linha preenchida
	If lOk .And. !Empty(GDFieldGet("GI_PRODALT",1))
		dbSelectArea("SGI")
		For nX := 1 To Len(aCols)
			If !aCols[nX,Len(aHeader)+1]
				//Grava
				If !Empty(aCols[nX,Len(aHeader)])
					//Alteracao
					msGoTo(aCols[nX,Len(aHeader)])
					RecLock("SGI",.F.)			
					Replace GI_ORDEM With aCols[nX,1]
				Else
					//Inclusao
					RecLock("SGI",.T.)
					Replace GI_FILIAL  With xFilial("SGI")
					Replace GI_PRODORI With cProdOri
					Replace GI_ORDEM With aCols[nX,1]
				EndIf
				For nY := 1 To Len(aHeader)-2
					If AllTrim(aHeader[nY,2]) # "GI_ORDEM"
						Replace &(aHeader[nY,2]) With aCols[nX,nY]
					EndIf
				Next nY
				MsUnLock()
			ElseIf !Empty(aCols[nX,Len(aHeader)])
				//Exclui
				msGoTo(aCols[nX,Len(aHeader)])
				RecLock("SGI",.F.,.T.)
				dbDelete()
				MsUnLock()
			EndIf
		Next nX
	EndIf
EndIf

RestArea(aArea)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A010FldOk ºAutor  ³Andre Anjos		 º Data ³  20/03/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida campos dos produtos alternativos.                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA010                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010FldOk()
Local lRet    := .T.
Local cCampo  := AllTrim(Substr(ReadVar(),4))
Local nLinScn := aScan(aCols,{|x| x[GDFieldPos(cCampo)] == &(ReadVar())})

If cCampo == "GI_ORDEM"
	If !Empty(nLinScn) .And. nLinScn # n
		If Empty(GDFieldGet("GI_PRODALT",n))
			Help(" ",1,"OBRIGAT2",,AllTrim(RetTitle("GI_PRODALT")),3,0)
			&(ReadVar()) := GDFieldGet("GI_ORDEM",n)
			lRet := .F.
		ElseIf Aviso(STR0032,STR0043+AllTrim(GDFieldGet("GI_PRODALT",nLinScn))+STR0044,{STR0045,STR0046}) == 1 //Ja existe um produto alternativo nesta ordem
			//Caso encontre mesma ordem, verifica se troca
			aCols[nLinScn,GDFieldPos(cCampo)] := GDFieldGet(cCampo,n)
			aCols[n,GDFieldPos(cCampo)] := &(ReadVar())
			&(ReadVar()) := GDFieldGet(cCampo,nLinScn)
		Else 
			//Se nao trocar retorna conteudo da memoria para o original
			&(ReadVar()) := GDFieldGet(cCampo,n)
			lRet := .F.
		EndIf
	Else
		&(ReadVar()) := GDFieldGet(cCampo,n)
	EndIf
	//Caso tenha trocado ordem, reordena
	If lRet	.And. !Empty(nLinScn)
		aSort(aCols,,,{|x,y| x[GDFieldPos(cCampo)] < y[GDFieldPos(cCampo)]})
	EndIf	
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A010VldAltºAutor  ³Andre Anjos	     º Data ³  31/03/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida produto alternativo digitado                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA010                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010VldAlt()
Local lRet     := .T.
Local aArea    := GetArea()
Local cProduto := &(ReadVar())
Local aEstru   := {}
Local nPos 	   := 0
Local cCodOri := If(Type('cProdOri') == 'C',cProdOri,If(Type('M->GI_PRODORI') == 'C',M->GI_PRODORI,SGI->GI_PRODORI))
Private nEstru :=0

//Verifica se esta vazio
If Empty(cProduto)
	Help(" ",1,"NVAZIO")
	lRet := .F.
EndIf

//Valida existencia e produtos bloqueados
If lRet
	lRet := ExistCpo('SB1',cProduto)
EndIf

//Bloqueia alternativo dele mesmo
If lRet .And. cProduto == cCodOri
	Help(" ",1,"ALTERSELF")
	lRet := .F.
EndIf

//Bloqueia produto fantasma
If lRet
	SB1->(dbSetOrder(1))
	SB1->(dbSeek(xFilial("SB1")+cProduto))
	If RetFldProd(SB1->B1_COD,"B1_FANTASM") == "S"
		Help(" ",1,"ALTERFAN")
		lRet := .F.
	EndIf
EndIf

//Analisa possivel recursividade
If lRet
	aEstru := Estrut(cProduto,1)
	If aScan(aEstru,{|x| x[3] == cCodOri}) > 0
		Help(" ",1,"ALTERREC")
		lRet := .F.
	EndIf
EndIf

//Verifica duplicidade
If lRet .And. Type("aCols") == "A"
	nPos := aScan(aCols,{|x| x[GDFieldPos("GI_PRODALT")] == cProduto})
	If nPos > 0 .And. nPos # n
		Help(" ",1,"JAGRAVADO")
		lRet := .F.
	EndIf
EndIf

RestArea(aArea)
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AlterLinOkºAutor  ³Andre Anjos	     º Data ³  01/04/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida linha do alternativo		                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA010                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AlterLinOK()                                                  
Local lRet     := .T.
Local lMT010LIN:= ExistBlock("MT010LIN")
//Alternativo obrigatorio
If !aCols[n,Len(aHeader)+1] .And. Empty(GDFieldGet("GI_PRODALT",n))
	Help(" ",1,"OBRIGAT2",,AllTrim(RetTitle("GI_PRODALT")),3,0)
	lRet := .F.
EndIf

If  lMT010LIN .And. lRet
	lRet := ExecBlock("MT010LIN",.F.,.F.)
	If ValType(lRet) <> "L"
		lRet := .T.
	EndIf
EndIf


Return lRet

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AjustaSX3    ³ Autor ³Andre Anjos		    ³ Data ³ 30/11/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Efetua ajuste no Dicionario de Dados.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³AjustaSX3()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA010                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AjustaSX3()
Local aAreaSX3	:= SX3->( GetArea() )
Local cValid	:= "If(!Empty(M->B1_OPERPAD),ExistCPO('SG2',M->B1_COD+M->B1_OPERPAD),.T.)" 
Local cVldB1_TE	:= "vazio().or.existcpo('SF4').And.M->B1_TE<='500'"
Local cVldB1_TS	:= "vazio().or.existcpo('SF4').And.M->B1_TS>'500'"  
Local cVldAlt	:= "ExistCpo('SB1') .And. A010VldAlt()"
Local cB1Proc	:= "A010Proc()"  
Local cBox		:= "P=Produzindo;C=Comprando"                                                                                                      
Local cBmGrupo	:= 'FREEFORUSE("SBM")'
Local lVer116    	:= (VAL(GetVersao(.F.)) == 11 .And. GetRpoRelease() >= "R6"  .Or.  VAL(GetVersao(.F.))  > 11)
Local lPyme		:= If( Type( "__lPyme" ) <> "U", __lPyme, .F. )

SX3->( dbSetOrder(2) )
If SX3->(dbSeek("B1_TE"))
	RecLock("SX3",.F.)
	SX3->X3_VALID  := cVldB1_TE
	SX3->(MsUnlock())
Endif
If SX3->(dbSeek("B1_TS"))
	RecLock("SX3",.F.)
	SX3->X3_VALID  := cVldB1_TS
	SX3->(MsUnlock())
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza X3_VALID do campo B1_OPERPAD se necessario						     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If SX3->(dbSeek("B1_OPERPAD")) .And. Trim(Upper(SX3->X3_VALID)) != Upper(cValid)
	RecLock("SX3",.F.)
	SX3->X3_VALID  := cValid
	SX3->(MsUnlock())
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza X3_VALID do campo B1_PROC se necessario		 		      	 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If SX3->(dbSeek("B1_PROC")) .And. Trim(Upper(SX3->X3_VALID)) != Upper(cB1Proc)
	RecLock("SX3",.F.)
	SX3->X3_VALID  := cB1Proc
	SX3->(MsUnlock())
Endif         

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza X3_CBOX do campo B1_PRODSBP se necessario		 		      	 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If SX3->(dbSeek("B1_PRODSBP")).And. Trim (Upper(SX3->X3_CBOX)) != Upper(cBox)
	Reclock ("SX3",.F.)
	SX3->X3_CBOX	:= cBox
	SX3->(MsUnlock())
Endif    

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza X3_VALID do campo GI_PRODALT se necessario		 		      	 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 

If SX3->(dbSeek("GI_PRODALT")).And. AllTrim (Upper(SX3->X3_VALID)) != Upper(cVldAlt)
	Reclock ("SX3",.F.)
	SX3->X3_VALID	:= cVldAlt
	SX3->(MsUnlock())
Endif  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ativa campo PYME da tabela SYD				     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
               
DbSelectArea("SX3")
DbSetOrder(1)
MsSeek("SYD")
While ( !EOF() .And. SX3->X3_ARQUIVO == "SYD" )
	If SX3->X3_PYME <> "S"
		RecLock("SX3", .F.)
		SX3->X3_PYME := "S"
		SX3->(MsUnLock())
	EndIf
	DbSelectArea("SX3")
	dbSkip()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ativa campo PYME da tabela SGI				     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
               
DbSelectArea("SX3")
DbSetOrder(1)                               
MsSeek("SGI")
While ( !EOF() .And. SX3->X3_ARQUIVO == "SGI" )
	If SX3->X3_PYME <> "S"
		RecLock("SX3", .F.)
		SX3->X3_PYME := "S"
		SX3->(MsUnLock())
	EndIf
	DbSelectArea("SX3")
	dbSkip()
EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza X3_VALID do campo BM_GRUPO se necessario						     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

dbSelectArea("SX3")
dbSetOrder(2)
If dbSeek("BM_GRUPO") .And. !cBmGrupo $ Upper(X3_VALID)
	If X3_VALID<>Nil
		cBmGrupo := AllTrim(X3_VALID) + '.And.' + AllTrim(cBmGrupo)
	EndIf
	RecLock("SX3",.F.)
	Replace X3_VALID With cBmGrupo
	SX3->( MsUnlock() )
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Corrige X3_VALID do campo B1_TE e B1_TS (Tes padrão)
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

dbSelectArea("SX3")
dbSetOrder(2)
If dbSeek("B1_TE") .And. alltrim(X3_VALID) <> 'vazio().or.existcpo("SF4").And.M->B1_TE<"501"'
	RecLock("SX3",.F.)
	Replace X3_VALID With 'vazio().or.existcpo("SF4").And.M->B1_TE<"501"'
	SX3->( MsUnlock() )
EndIf

dbSelectArea("SX3")
dbSetOrder(2)
If dbSeek("B1_TS") .And. alltrim(X3_VALID) <> 'vazio().or.existcpo("SF4").And.M->B1_TS>"500"'
	RecLock("SX3",.F.)
	Replace X3_VALID With 'vazio().or.existcpo("SF4").And.M->B1_TS>"500"'
	SX3->( MsUnlock() )
EndIf
RestArea( aAreaSX3 )

If lVer116 .And. !lPyme
	dbSelectArea("SX3")
	dbSetOrder(2)
	If dbSeek("B1_LOCPAD") .And. alltrim(X3_VALID) <> 'ExistCpo("NNR") .and. A010VLoc()'
		RecLock("SX3",.F.)
		Replace X3_VALID With 'ExistCpo("NNR") .and. A010VLoc()'
		If alltrim(X3_F3) <> 'NNR'
			Replace X3_F3 With 'NNR'
		Endif	
		SX3->( MsUnlock() )
	EndIf
	RestArea( aAreaSX3 )
Else
	dbSelectArea("SX3")
	dbSetOrder(2)
	If dbSeek("B1_LOCPAD") .And. alltrim(X3_VALID) <> 'A010VLoc()'
		RecLock("SX3",.F.)
		Replace X3_VALID With 'A010VLoc()'
		If alltrim(X3_F3) <> ''
			Replace X3_F3 With ''
		Endif
		SX3->( MsUnlock() )
	EndIf
	RestArea( aAreaSX3 )	
Endif

Return Nil
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AjustaHelp    ³ Autor ³Aline Sebrian         ³ Data ³20.02.2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Ajusta os helps                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³AjustaHelp()                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function AjustaHelp()
Local aArea 	:= GetArea()
Local aHelpPor	:= {}
Local aHelpEng	:= {}
Local aHelpSpa	:= {}

aHelpPor :=	{"Produto não poderá ser excluído."}
aHelpSpa :=	{"Producto no puede ser borrado."}
aHelpEng :=	{"Product cannot be deleted."}
PutHelp("PMA010AGR",aHelpPor,aHelpEng,aHelpSpa,.F.)     

aHelpPor :=	{"Verificar se este produto faz parte ","de alguma Variedades de Produtos, ","no módulo Gestão Agrícola."}
aHelpSpa :=	{"Compruebe si este producto forma parte ","de algun Archivo de Variedads, en el "," modulo Gestion Agricola."}
aHelpEng :=	{"Check whether this product is part of  ","some Products of Variety in ","Agriculture Management module."}
PutHelp("SMA010AGR",aHelpPor,aHelpEng,aHelpSpa,.F.) 

aHelpPor :=	{"Um produto não pode ser alternativo ","dele mesmo."}
aHelpSpa :=	{"Un producto no puede ser alternativo ","de él aún."}
aHelpEng :=	{"An product can't be alternative for ","itself."}
PutHelp("PALTERSELF",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Não se aplica."}
aHelpSpa :=	{"No se aplica."}
aHelpEng :=	{"Not applicable."}
PutHelp("SALTERSELF",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Este produto é fantasma e por ","este motivo não pode ter alternativos ","ou ser alternativo."}
aHelpSpa :=	{"Este producto es fantasma y por ","este motivo no puede tener alternativos","o ser alternativo."}
aHelpEng :=	{"This product is ghost and for","this reason may not have alternatives","or be an alternative."}
PutHelp("PALTERFAN",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Não se aplica."}
aHelpSpa :=	{"No se aplica."}
aHelpEng :=	{"Not applicable."}
PutHelp("SALTERFAN",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Este cadastro pode gerar recursividade ","na estrutura do produto origem."}
aHelpSpa :=	{"Este registro puede generar recursividade ","en la estructura del producto origen."}
aHelpEng :=	{"This register can generate recursively","in the origin product structure."}
PutHelp("PALTERREC",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Neste caso, este produto não poderá ","ser alternativo."}
aHelpSpa :=	{"En este caso, este producto no podrá ","ser alternativo."}
aHelpEng :=	{"In this case, this product can't","be alternative."}
PutHelp("SALTERREC",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"A utilização deste recurso depende de ","uma atualização no dicionário de dados."}
aHelpSpa :=	{"La utilización de este recurso depende ","de una actualización en el diccionario ","de datos."}
aHelpEng :=	{"The use of this feature depends of an ","update in data dictionary."}
PutHelp("PUPDPCP10",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Execute o compatibilizador UPDPCP10."}
aHelpSpa :=	{"Ejecute el compatibilizador UPDPCP10."}
aHelpEng :=	{"Run UPDPCP10 consistent."}
PutHelp("SUPDPCP10",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"O produto não pode ser excluido, pois ","existe amarração no cadastro de ","Produto X Cliente."}
aHelpSpa :=	{"El producto no puede borrarse, pues ","existe vinculo en el archivo ","Producto vs. Cliente."}
aHelpEng :=	{"The product can not be excluded since ","there is binding in the ","Product X Client file."}
PutHelp("PMT010DPRO",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Exclua todas as amarrações que existam ","no cadastro de Produto X Cliente ","com este produto."}
aHelpSpa :=	{"Borre todos los vinculos que existen en ","el archivo Producto vs. Cliente ","con este producto."}
aHelpEng :=	{"Exclude all bindings in the ","Product x Client file that ","present this product."}
PutHelp("SMT010DPRO",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Produto não poderá ser excluído."}
aHelpSpa :=	{"Producto no puede ser borrado.  "}
aHelpEng :=	{"Product cannot be deleted.      "}
PutHelp("PMT010WMS",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Verificar o cadastro de", "Produtos x Serviços do WMS."}
aHelpSpa :=	{"Verificar el archivo de", "ProductosxUbicación del WMS."}
aHelpEng :=	{"Check WMS Product x Services file."}
PutHelp("SMT010WMS",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Produto não poderá ser excluído."}
aHelpSpa :=	{"Producto no puede ser borrado.  "}
aHelpEng :=	{"Product cannot be deleted.      "}
PutHelp("PMT010WM1",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Verificar o cadastro de", "Produtos x Zonas de Armazenagem do WMS."}
aHelpSpa :=	{"Verificar el archivo de", "Productos x Zonas de Almacenaje del WMS."}
aHelpEng :=	{"Check WMS Product x Storage Zones file."}
PutHelp("SMT010WM1",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Produto não poderá ser excluído."}
aHelpSpa :=	{"Producto no puede ser borrado.  "}
aHelpEng :=	{"Product cannot be deleted.      "}
PutHelp("PMT010WM2",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Verificar o cadastro de", "Sequencia de Abastecimento do WMS."}
aHelpSpa :=	{"Verificar el archivo de", "Secuencia de Abastecimiento del WMS."}
aHelpEng :=	{"Check WMS Product x Supply Sequence file."}
PutHelp("SMT010WM2",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Utiliza FEFO? 'First Expired First Out'", "Significa que o Sistema irá utilizar os",;
			 "produtos baseado nas datas de validade,","(o produto cuja data de validade será a",;
			 "próxima a vencer será utilizado","primeiro)."}
aHelpSpa :=	{"¿Usa FEFO? 'First Expired First Out'","Significa que el Sistema utilizara los",;
			"productos con base en las fechas de ","validez, (el producto cuya fecha de",;	
			"validez sera la próxima a vencer se ","utilizara primero)."}
aHelpEng :=	{"Use FEFO? 'First Expired First Out'","It means that the system uses products",;
			"based on expiration dates, (the product","with the closest expiration date is ",;
			"used first)."}
PutHelp("PB1_USAFEFO",aHelpPor,aHelpEng,aHelpSpa,.T.)

aHelpPor	:=	{"Indica se o produto é transgênico. "}		 
aHelpEng	:= {" "}
aHelpSpa	:= {" "}
PutHelp("PB1_TRANSGE",aHelpPor,aHelpEng,aHelpSpa,.F.)

If AliasInDic("LBT")
	aHelpPor	:=	{"Nesse campo deve-se informar qual  ",;
					 "moeda foi atribuída ao produto.    ",;
					 "Obs.: O template trata cada espécie",;
					 " ou grupo de produto (soja, milho, ",;
					 "trigo, etc) como uma moeda. Deve-se",;
					 " atribuir a cada espécie uma moeda,",;
					 " com exceção das moedas 1 e 2."}
	aHelpEng	:= {" "}
	aHelpSpa	:= {" "}
	PutHelp("PB1_MOEDA",aHelpPor,aHelpEng,aHelpSpa,.T.)

	aHelpPor	:=	{"Determina se o produto:            ",;
					 "1 - Sim (É um grão comercial)      ",;
					 "2 - Não (Não é um grão ou semente) ",;
					 "3 - Grão Semente (É semente)       "}		 
	aHelpEng	:= {" "}
	aHelpSpa	:= {" "}
	PutHelp("PB1_GRAO",aHelpPor,aHelpEng,aHelpSpa,.T.)
	
	aHelpPor	:=	{"Código do produto utilizado como   ",;
					 "descarte, para o produto que está  ",;
					 "sendo cadastrado. O código de      ",;
					 "descarte será utilizado na rotina  ",;
					 "de romaneio de entrada. "}		 
	aHelpEng	:= {" "}
	aHelpSpa	:= {" "}
	PutHelp("PB1_CODDES",aHelpPor,aHelpEng,aHelpSpa,.T.)
	
	aHelpPor	:=	{"Indica se o produto é da espécie   ",;
					 "Trigo ou não."}		 
	aHelpEng	:= {" "}
	aHelpSpa	:= {" "}
	PutHelp("PB1_TRIGO",aHelpPor,aHelpEng,aHelpSpa,.T.)
	
	aHelpPor	:=	{"Código do produto considerado      ",;
					 "triguilho, que é o descarte do     ",;
					 "trigo. Somente os produtos da      ",;
					 "espécie trigo possuem código de    ",;
					 "triguilho."}		 
	aHelpEng	:= {" "}
	aHelpSpa	:= {" "}
	PutHelp("PB1_DSCTRIG",aHelpPor,aHelpEng,aHelpSpa,.T.)
	
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Help para validacao do cadastro de Apontamento de Visitas ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//Problema
aHelpPor := {"Exclusão não permitida deste produto ", "pois existe vínculo do mesmo no ","cadastro de Apontamento de Visitas." }
aHelpEng := {"Exclusão não permitida deste produto ", "pois existe vínculo do mesmo no ","cadastro de Apontamento de Visitas." }
aHelpSpa := {"Exclusão não permitida deste produto ", "pois existe vínculo do mesmo no ","cadastro de Apontamento de Visitas." }

PutHelp("PAPTNODEL", aHelpPor, aHelpEng, aHelpSpa,.T.) 

//Solucao
aHelpPor := {"Exclua primeiramente o Apontamento de ","Visitas cadastrada para este produto." }
aHelpEng := {"Exclua primeiramente o Apontamento de ","Visitas cadastrada para este produto." }
aHelpSpa := {"Exclua primeiramente o Apontamento de ","Visitas cadastrada para este produto." }

PutHelp("SAPTNODEL", aHelpPor, aHelpEng, aHelpSpa,.T.) 
                                                          

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Help para validacao do cadastro de Apontamento de Visitas ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//Problema
aHelpPor := {"Exclusão não permitida deste produto ", "pois existe vínculo do mesmo no ","cadastro de Produto Master." }
aHelpEng := {"Exclusão não permitida deste produto ", "pois existe vínculo do mesmo no ","cadastro de Produto Master." }
aHelpSpa := {"Exclusão não permitida deste produto ", "pois existe vínculo do mesmo no ","cadastro de Produto Master." }

PutHelp("PMA010_13", aHelpPor, aHelpEng, aHelpSpa,.T.) 

//Solucao
aHelpPor := {"Exclua primeiramente o Produto Master ","que possui este produto vinculado." }
aHelpEng := {"Exclua primeiramente o Produto Master ","que possui este produto vinculado." }
aHelpSpa := {"Exclua primeiramente o Produto Master ","que possui este produto vinculado." }

PutHelp("SMA010_13", aHelpPor, aHelpEng, aHelpSpa,.T.) 

RestArea(aArea)
Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A010Int³ Autor ³ Vendas CRM               ³ Data ³ 15/09/2009 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Realiza integracao com a criterium ou outra integracao       ³±±
±±³          ³que utiliza o framework do SIGALOJA de integracao.            ³±±
±±³          ³ O parâmetro aIntSB1 normalmente é vazio.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A010Int()                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Momento da chamada, sendo:                             ³±±
±±³          ³           1: Antes de qualquer alteração                     ³±±
±±³          ³           2: Depois das alterações                           ³±±
±±³          ³ExpN2: Opção da rotina                                        ³±±
±±³          ³ExpA3: Array contendo o número do registro e adaptador do SB1.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A010Int( nMomento, nOpc, aIntSB1 )
	Local lIntegra 		:= SuperGetMv("MV_LJGRINT", .F., .F.)	// Se há integração ou não
	Local aArea			:= GetArea()

	If lIntegra
		If nMomento == 1
			MsgRun( STR0051, STR0050, {|| A010IniInt( nOpc, aIntSB1 ) } ) // "Aguarde" "Anotando registros para integração"
		ElseIf nMomento == 2			
			MsgRun( STR0052, STR0050, {|| A010FimInt( nOpc, aIntSB1 ) } ) // "Aguarde" "Executando integração"
		EndIf
	EndIf
	
	RestArea( aArea )
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A010IniInt   ³ Autor ³ Vendas CRM         ³ Data ³ 15/09/2009 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Faz o cache dos itens antes de serem excluídos, possibilitan-³±±
±±³          ³do o envio dos mesmos, mesmo após de serem apagados.          ³±±
±±³          ³ O parâmetro aIntSB1 normalmente é vazio.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A010IniInt()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Opção da rotina                                        ³±±
±±³          ³ExpA2: Array contendo o número do registro e adaptador do SB1.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A010IniInt( nOpc, aIntSB1 )
	Local oFactory		:= LJCAdapXmlEnvFactory():New()
	Local cChave		:= ""
	
	// Se houver integração e não for inclusão ou copia, anota todos os registros para exclusão, caso algum seja excluído
	If nOpc != 3 .AND. !lCopia
		aIntSB1 :=	{ SB1->(Recno()), oFactory:Create( "SB1" ), oFactory:CreateByProcess( "025" ) }		
		cChave 	:= xFilial( "SB1" ) + SB1->B1_COD
	    aIntSB1[2]:Inserir( "SB1", cChave, "1", "5" )
		aIntSB1[3]:Inserir( "SB1", cChave, "1", "5" )
	    aIntSB1[2]:Gerar()
		aIntSB1[3]:Gerar()
	EndIf	
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A010FimInt   ³ Autor ³ Vendas CRM         ³ Data ³ 15/09/2009 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Envia os itens apagados e todos os outros itens.             ³±±
±±³          ³ O parâmetro aIntSB1 normalmente é vazio.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A010FimInt()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Opção da rotina                                        ³±±
±±³          ³ExpA2: Array contendo o número do registro e adaptador do SB1.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A010FimInt( nOpc, aIntSB1 )
	Local oFactory			:= LJCAdapXmlEnvFactory():New( )	// Cria a fabrica de Adaptadores de envio
	Local oEntidadeFactory	:= LJCEntidadeFactory():New()
	Local cChave			:= ""
	Local oEntidadeSBM		:= Nil
	Local oRecords			:= Nil
	
	Default aIntSB1  		:= {}
	
	// Verifica se houve algum registro apagado, e gera a integração desse registro
	If nOpc != 3 .AND. !lCopia .And. Len(aIntSB1) > 0
		// Procura pelo registro do cabeçalho
		SB1->(msGoTo( aIntSB1[1] ) ) 
		
		// Se não encontrar, significa que o cabeçalho foi apagado, então envia somente a exclusão do cabeçalho
		If SB1->( DELETED() )
			aIntSB1[2]:Finalizar()
		EndIf
	EndIf
	
	// Independente de ter registros apagados ou não, gera quando não for exclusão, todos os outros registros
	If nOpc != 5
		aIntSB1 := { SB1->( Recno() ), oFactory:Create( "SB1" ), oFactory:CreateByProcess( "025" )  }		
		cChave 	:= xFilial( "SB1" ) + SB1->B1_COD
		
		// Para a tabela SB1
	    aIntSB1[2]:Inserir( "SB1", cChave, "1", cValToChar( nOpc ) )
	    aIntSB1[2]:Gerar()
		aIntSB1[2]:Finalizar()	    
	    
	    // Para o processo 025	    
		oEntidadeSBM := oEntidadeFactory:Create( "SBM" )
		If oEntidadeSBM != Nil
			oEntidadeSBM:DadosSet( "BM_GRUPO", SB1->B1_GRUPO )
			oRecords := oEntidadeSBM:Consultar(1)
			
			// Insere os registros no adapter e envia pro EAI.
			If aIntSB1[3] != NIL	.And. oRecords:Count() > 0
				//Insere os dados da carga
				aIntSB1[3]:Inserir( "SBM", xFilial("SBM") + oRecords:Elements( 1 ):DadosGet( "BM_GRUPO" ) , "1", cValToChar( nOpc ))
			EndIf 						
		EndIf						
		aIntSB1[3]:Inserir( "SB1", cChave, "1", cValToChar( nOpc ) )
		aIntSB1[3]:Gerar()
		aIntSB1[3]:Finalizar()
	EndIf
Return     

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AjustaSx2 ºAutor  ³Aline Sebrian       º Data ³  09.08.10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Ajustes na tabela SX2(tabelas)					          º±±
±±º          ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AjustaSX2()

Local aArea := GetArea() 
 
dbSelectArea('SX2')

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ativa o campo PYME da tabela SYD³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If MsSeek("SYD") .And. AllTrim(SX2->X2_PYME) <> 'S'
	RecLock('SX2', .F.)		
	X2_PYME := 'S'
	MsUnlock()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ativa o campo PYME da tabela SGI³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If MsSeek("SGI") .And. AllTrim(SX2->X2_PYME) <> 'S'
	RecLock('SX2', .F.)		
	X2_PYME := 'S'
	MsUnlock()
EndIf
RestArea(aArea)

Return(Nil)   

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³A010IntSFC ³ Autor ³ Aecio Ferreira Gomes    ³ Data ³30/04/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³Atualiza tabelas do SFC conforme modelagem dos dados(MVC)     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A010IntSFC(ExpN1)                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1 = Rotina a ser processada(3-Inc, 4-Alt, 5-Exc)          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MATA770                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/             
Function A010IntSFC(nOpc,cError,cNome,oModel)
Local aArea   := GetArea()	// Salva area atual para posterior restauracao
Local lRet    := .T.		// Conteudo de retorno
Local aCampos := {}			// Array dos campos a serem atualizados pelo modelo
Local aAux    := {}			// Array auxiliar com o conteudo dos campos
Local nX	  := 0			// Indexadora de laco For/Next
Default oModel  := FWLoadModel("SFCC101")		// Objeto de modelo de dados

If nOpc == 3
	aAdd(aCampos,{"CZ3_CDAC",M->B1_COD})
	aAdd(aCampos,{"CZ3_DTBG",dDataBase})
	aAdd(aCampos,{"CZ3_TPAC","1"})
EndIf

If nOpc # 5
	aAdd(aCampos,{"CZ3_DSAC"	,M->B1_DESC })
	aAdd(aCampos,{"CZ3_CDUN"	,M->B1_UM})
	aAdd(aCampos,{"CZ3_DSUN"	,Posicione("SAH",1,xFilial("SAH")+M->B1_UM,"AH_DESCPO")})
	aAdd(aCampos,{"CZ3_CDGR"	,M->B1_GRUPO })
	aAdd(aCampos,{"CZ3_DSGR"	,Posicione("SBM",1,xFilial("SBM")+M->B1_GRUPO,"BM_DESC")})
	aAdd(aCampos,{"CZ3_CDFA"	,M->B1_TIPO })
	aAdd(aCampos,{"CZ3_DSFA"	,PadR(Posicione("SX5",1,xFilial("SX5")+"02"+M->B1_TIPO,"X5_DESCRI"),TamSX3("CZ3_DSFA")[1])})
	aAdd(aCampos,{"CZ3_CDDP"	,M->B1_LOCPAD })
	aAdd(aCampos,{"CZ3_QTLOEC"	,M->B1_LE })
	aAdd(aCampos,{"CZ3_QTLOMI"	,M->B1_LM })
	aAdd(aCampos,{"CZ3_VLPSBR"	,M->B1_PESBRU })
	aAdd(aCampos,{"CZ3_VLPSLQ"	,M->B1_PESO })
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Instancia modelo de dados(Model) do cadastro de Item - SFC ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//oModel  := FWLoadModel("SFCC101")		// Objeto de modelo de dados
oModel:SetOperation(nOpc)

If nOpc # 3
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Quando se tratar de alteracao ou exclusao primeiramente o registro devera ser posicionado ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	CZ3->(dbSetOrder(1))
	CZ3->(dbSeek(xFilial("CZ3")+SB1->B1_COD))
EndIf
		
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ativa o modelo de dados ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (lRet := oModel:Activate())
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Obtem a estrutura de dados do Model ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aAux := oModel:GetModel("CZ3MASTER"):GetStruct():GetFields()
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Loop para validacao e atribuicao de dados dos campos do Model ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 To Len(aCampos)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Valida os campos existentes na estrutura do Model ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aScan(aAux,{|x| AllTrim(x[3]) ==  AllTrim(aCampos[nX,1])}) > 0
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atribui os valores aos campos do Model caso passem pela validacao do formulario ³
			//³referente a tipos de dados, tamanho ou outras incompatibilidades estruturais.   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !(oModel:SetValue("CZ3MASTER",aCampos[nX,1],aCampos[nX,2]))
				lRet := .F.
				Exit       
			EndIf
		EndIf
	Next nX
Endif

If lRet
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Valida os dados e integridade conforme dicionario do Model ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (lRet := oModel:VldData())
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Efetiva gravacao dos dados na tabela ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		lRet := oModel:CommitData()
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Gera log de erro caso nao tenha passado pela validacao ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lRet
	A010SFCErr(oModel,@cError,NIL,cNome,SB1->B1_COD)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Desativa o Model ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oModel:DeActivate()

RestArea(aArea)
Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³MA10VerAV  ³ Autor ³ Marco Aurelio - Mano    ³ Data ³19/05/11  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³Verifica se existe vinculo do produto que esta sendo excluido  ³±±
±±³          ³no cadastro de Apontamento de Visitas.                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³MA10VerAV()                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MATA010                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/             
Static Function MA10VerAV()
Local cAliasTRB := GetNextAlias()	// Nome do arquivo temporario
Local cQuery    := ""				// Auxiliar para execucao de query
Local aArea     := GetArea()		// Salva area atual para posterior restauracao
Local lRet      := .F.				// Conteudo de retorno

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Query para pesquisar o produto na tabela de Apontamento de Visitas(AD6) ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cQuery := "SELECT * "

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tabela base do filtro ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cQuery += "FROM "  
cQuery += RetSqlName("AD6")+" AD6 "	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Condicoes de filtro ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cQuery += "WHERE "
cQuery += "AD6_FILIAL = '"+ xFilial("AD6")+"' AND "
cQuery += "AD6_CODPRO = '"+ SB1->B1_COD   +"' AND "
cQuery += "AD6.D_E_L_E_T_  <> '*'"

cQuery := ChangeQuery(cQuery)

DbUseArea(.T.,'TOPCONN',TCGENQRY(,,cQuery),cAliasTRB,.T.,.T.)

If !(cAliasTRB)->(Eof())

	Help(" ",1,"APTNODEL") // Exclusao nao permitida deste produto pois existe vinculo do mesmo no cadastro Apontamento de Visitas. 
	lRet := .T.
	
EndIf

(cAliasTRB)->(DbCloseArea())

RestArea(aArea)

Return(lRet)



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A010SFCErrºAutor  ³ Andre Anjos  		 º Data ³  30/05/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Tratamento de erro da integracao com Chao de Fabrica.      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Integracao SFC                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010SFCErr(oModel,cError,nItErro,cNome,cChave)
Local aErro  := {}
Local lCarga := ValType(cError) == "C"

aErro := oModel:GetErrorMessage()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Estrutura do vetor com erro ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//[1] Id do formulário de origem
//[2] Id do campo de origem
//[3] Id do formulário de erro
//[4] Id do campo de erro
//[5] Id do erro
//[6] mensagem do erro
//[7] mensagem da solução
//[8] Valor atribuido
//[9] Valor anterior

If lCarga
	cError += cNome +" " +AllTrim(cChave) +": " +AllToChar(aErro[6]) +CRLF
Else
	AutoGrLog("Id do formulário de origem: " +'[' +AllToChar(aErro[1]) +']')
	AutoGrLog("Id do campo de origem:      " +'[' +AllToChar(aErro[2]) +']')
	AutoGrLog("Id do formulário de erro:   " +'[' +AllToChar(aErro[3]) +']')
	AutoGrLog("Id do campo de erro:        " +'[' +AllToChar(aErro[4]) +']')
	AutoGrLog("Id do erro:                 " +'[' +AllToChar(aErro[5]) +']')
	AutoGrLog("Mensagem do erro:           " +'[' +AllToChar(aErro[6]) +']')
	AutoGrLog("Mensagem da solução:        " +'[' +AllToChar(aErro[7]) +']')
	AutoGrLog("Valor atribuido:            " +'[' +AllToChar(aErro[8]) +']')
	AutoGrLog("Valor anterior:             " +'[' +AllToChar(aErro[9]) +']')
	
	HELP(" ",1,"Integração",,AllToChar(aErro[6]),1,1)
	
	If !Empty(nItErro)
		AutoGrLog("Erro no Item:               " +'[' +AllTrim(AllToChar(nItErro)) +']')
	EndIf

	MostraErro()
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³IntegraSFCºAutor  ³ Aecio Ferreira Gomes º Data ³  29/09/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida Integração com SFC								    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Integracao SFC                                            	º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function IntegraSFC()       
Local lRet		:= .F.
Local lVer115   := Val(GetVersao(.F.)) > 11 .Or. (Val(GetVersao(.F.)) == 11 .And. GetRpoRelease() >= "R5") 
Local xIntSFC	:= GetMV("MV_INTSFC",.F.,0)

If lVer115
	If ValType(xIntSFC) # "N"
		lRet := xIntSFC
		AjustaSX6() //-- Realiza ajuste na funcao mudando o parametro para numerico
	Else
		lRet := xIntSFC == 1
	EndIf
EndIf

Return(lRet)  

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³IntegraSFCº    Autor  ³ Michele Girardi  º Data ³  08/01/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida Integração com SFC - Parametro/Compartilhamento tabelaº±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Integracao SFC                                            	º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ExisteSFC(cTabela)       
Local lRet		:= .F.
Local lVer115   := Val(GetVersao(.F.)) > 11 .Or. (Val(GetVersao(.F.)) == 11 .And. GetRpoRelease() >= "R5") 
Local xIntSFC	:= GetMV("MV_INTSFC",.F.,0)  

Local nI        := 0        
Local nParSFC   
Local nExistSfc := .F.   
Local cMode
Local aEmpresas := {} 
Local nTamanho  := 0  

//Verificar se a filial corrente possui integração      
If lVer115
	If ValType(xIntSFC) # "N"
		lRet := xIntSFC
		AjustaSX6() //-- Realiza ajuste na funcao mudando o parametro para numerico
	Else
		lRet := xIntSFC == 1
	EndIf
EndIf   

//Se a filial corrente possuir integração, processar a integração.
if lRet
	return lRet
EndIf     

//Se a filial corrente não possuir integração verificar se outra filial possui integração

//Carrega todas as filiais no array
If !OpenSM0(.T.)
	Final("SIGAMAT.EMP com problemas!")//"SIGAMAT.EMP com problemas!"
EndIf     
SM0->(dbGotop())
While !SM0->(EOF())
	If !SM0->(Deleted())	    
		aAdd(aEmpresas, {SM0->M0_CODFIL})   
	EndIf
	SM0->(dbSkip())
End                          

DbSelectArea("SX6")
SX6->(DbSetOrder(1))  
nTamanho := Len(SX6->X6_FIL)

//Busca na SX6 se o parâmetro está marcado para alguma outra filial  
nExistSfc = .F.
For nI := 1 to Len(aEmpresas)            
	DbSelectArea("SX6")
	SX6->(DbSetOrder(1))  
	SX6->(dbSeek(substr(aEmpresas[nI,1],1,nTamanho)+"MV_INTSFC"))
	
	nParSFC := SX6->X6_conteud
	
	If VAL(nParSFC) == 1      
		nExistSfc = .T.
		exit
	EndIf 
Next nI

//Se nenhuma filial possuir integração não processar a integração.
if !nExistSfc
	lRet := .F.
	return lRet
EndIf

//Se a filial corrente não possuir integração e outra filial possuir
//Verificar se a tabela em questão é compartilhada 
cMode := FWModeAccess(cTabela)    

//Se for compartilhada, processa a integração
//Se não for compartilhada não processar a integração.
if cMode == 'E' 
	lRet = .F.
Else       
	lRet = .T.
EndIf

Return(lRet)     

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ OpenSM0   ³ Autor ³ Microsiga		    ³ Data ³07/01/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua a abertura do SM0 exclusivo                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ UPD														  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function OpenSM0(lCompart)
Local lOpen      := .F.
Local nLoop      := 0

If Select('SM0') > 0
	dbSelectArea('SM0')
	dbCloseArea()
EndIf

For nLoop := 1 To 20
	dbUseArea(.T.,, 'SIGAMAT.EMP', 'SM0', lCompart, lCompart )
	If Select('SM0') > 0
		lOpen := .T.
		dbSetIndex('SIGAMAT.IND')
		Exit
	EndIf
	Sleep(500)
Next nLoop

Return lOpen

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Function  ³ IntegDef º Autor ³ Marcelo C. Coutinho  º Data ³  28/10/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Descricao ³ Funcao de tratamento para o recebimento/envio de mensagem    º±±
±±º           ³ unica de cadastro de produtos.                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Uso       ³ MATA010                                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function IntegDef( cXML, nTypeTrans, cTypeMessage )
Local aRet := {}

aRet := MATI010(cXml, nTypeTrans, cTypeMessage)

Return aRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³IntegraDPRºAutor  ³ Leonardo Quintania   º Data ³  06/11/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida Integração com o DPR(Desenvolvedor de Produtos)	    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Integracao DPR                                               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function IntegraDPR()       
Local lRet		:= .F.

If Val(GetVersao(.F.)) > 11 .Or. (Val(GetVersao(.F.)) == 11 .And. GetRpoRelease() >= "R7")
	lRet := SuperGetMv("MV_INTDPR",.F.,0) == 1
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AjustaSX6 ºAutor  ³ Leonardo Quintania   º Data ³  26/11/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida Integração com SFC								    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Integracao SFC                                            	º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AjustaSX6()
Local aArea   := GetArea()
Local cNewVal := If(GetMv("MV_INTSFC",.F.,.T.),"1","0")

dbSelectArea("SX6")
If (SX6->(dbSeek(Space(FWSizeFilial())+"MV_INTSFC")) .Or. dbSeek(cFilAnt+"MV_INTSFC")) .And. AllTrim(SX6->X6_TIPO) <> "N"
	RecLock("SX6",.F.) 
	SX6->X6_TIPO	:= "N"
	SX6->X6_CONTEUD := cNewVal
	SX6->X6_CONTSPA := cNewVal
	SX6->X6_CONTENG := cNewVal
	SX6->X6_DESCRIC := "Define se existe integracao entre o Modulo SIGASFC"
	SX6->X6_DSCSPA  := "Define si existe integracion entre el Mod SIGASFC"
	SX6->X6_DSCENG  := "Defines whether SIGASFC module is integrated"
	SX6->X6_DESC1   := "e outros modulos (0=Nao integra;1=Protheus;"
	SX6->X6_DSCSPA1 := "y otros modulos (0=No integracion;1=Protheus;"
	SX6->X6_DSCENG1 := "with other modules (0=No integration;1=Protheus;"
	SX6->X6_DESC2   := "2=Datasul)"
	SX6->X6_DSCSPA2 := "2=Datasul)"
	SX6->X6_DSCENG2 := "2=Datasul)"
	SX6->X6_INIT 	  := "0"
	SX6->X6_DEFPOR  := "0"
	SX6->X6_DEFSPA  := "0"
	SX6->X6_DEFENG  := "0"
	MsUnlock() 
EndIf

RestArea(aArea)
Return .T.


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ A010Hist     ºAutor  ³Wemerson Randolfo     º Data ³ 03/09/12 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Visualizacao do historico das alteracoes                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ Nao ha                                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ .T. ou .F.                                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºAplicacao ³ Funcao chamada pelo menu                                      º±±
±±º          ³                                                               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A010Hist()
Local lRet

lRet := HistOperFis("SS4",SB1->B1_COD,SB1->B1_DESC,"S4_COD")
Return lRet

/*
| Programa : A010VLoc  			 Autor : TOTVS           Data : 26/11/2013
| Descricao: Valida local padrão diferente de MV_CQ
| Retorno  : .T. ou .F.                              
| Aplicacao: Chamada no valid do B1_LOCAL 
*/
Function A010VLoc()
Local lret:= .T.

If M->B1_LOCPAD == GetMV("MV_CQ") 
	Help(" ",1,"A240LOCCQ")
	lRet := .F.
Endif 
Return lret